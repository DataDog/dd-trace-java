plugins {
  id 'io.franzbecker.gradle-lombok' version '1.14'
  id 'com.jfrog.artifactory' version '4.8.1'
  id 'com.jfrog.bintray' version '1.8.4'
  id 'org.unbroken-dome.test-sets' version '1.5.2'

  id 'com.gradle.build-scan' version '1.16' // 2.0+ requires gradle 5
  // Not applying google java format by default because it gets confused by stray java build
  // files in 'workspace' build directory in CI
  id 'com.github.sherter.google-java-format' version '0.7.1' apply false
  id 'com.dorongold.task-tree' version '1.3.1'
}

def isCI = System.getenv("CI") != null

allprojects {
  group = 'com.datadoghq'
  version = '0.25.0-SNAPSHOT'

  if (isCI) {
    buildDir = "${rootDir}/workspace/${projectDir.path.replace(rootDir.path, '')}/build/"
  }

  apply from: "${rootDir}/gradle/dependencies.gradle"
  apply from: "${rootDir}/gradle/util.gradle"
}

repositories {
  jcenter()
  mavenCentral()
}

description = 'dd-trace-java'

tasks.register("latestDepTest")

// Applied here to allow publishing of artifactory build info
apply from: "${rootDir}/gradle/publish.gradle"

def skipBuildscan = Boolean.valueOf(System.getenv("SKIP_BUILDSCAN"))

buildScan {
  termsOfServiceUrl = 'https://gradle.com/terms-of-service'
  termsOfServiceAgree = 'yes'

  if (isCI && !skipBuildscan) {
    publishAlways()
    tag 'CI'
  }
}

ext.gradleWrapperVersion = '4.10.3'
tasks.register("wrapper", Wrapper) {
  gradleVersion = gradleWrapperVersion
}

allprojects {
  tasks.withType(JavaForkOptions).configureEach {
    maxHeapSize = System.properties["datadog.forkedMaxHeapSize"]
    minHeapSize = System.properties["datadog.forkedMinHeapSize"]
    jvmArgs "-XX:ErrorFile=/tmp/hs_err_pid%p.log"
  }
}
