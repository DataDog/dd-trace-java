plugins {
  id 'org.gradle.playframework'
}

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/dd-smoke-tests/play-common/fix-play-routes.gradle"

testJvmConstraints {
  // TODO Java 17: This version of play doesn't support Java 17
  maxJavaVersion = JavaVersion.VERSION_15
}

def playVer = "2.5.19"
def scalaVer = System.getProperty("scala.binary.version", /* default = */ "2.11")

play {
  platform {
    playVersion = playVer
    scalaVersion = scalaVer
    javaVersion = JavaVersion.VERSION_1_8
  }
  injectedRoutesGenerator = true
}

distributions {
  main {
    contents {
      from("conf") {
        into "conf"
      }
    }
  }
}

repositories {
  mavenLocal()
  if (project.rootProject.hasProperty("mavenRepositoryProxy")) {
    maven {
      url project.rootProject.property("mavenRepositoryProxy")
      allowInsecureProtocol = true
    }
  }
  mavenCentral()
  maven {
    name = "lightbend-maven-releases"
    url = "https://repo.lightbend.com/lightbend/maven-release"
  }
  ivy {
    name = "lightbend-ivy-release"
    url = "https://repo.lightbend.com/lightbend/ivy-releases"
    layout("ivy")
  }
}

description = 'Play 2.5 Integration Tests.'

dependencies {
  implementation "com.typesafe.play:play-logback_$scalaVer:$playVer"
  implementation "com.typesafe.play:filters-helpers_$scalaVer:$playVer"
  implementation "com.typesafe.play:play-java-ws_$scalaVer:$playVer"
  // jaxb is not there anymore in java11+
  implementation "javax.xml.bind:jaxb-api:2.3.1"

  implementation project(':dd-trace-api')
  implementation group: 'io.opentracing', name: 'opentracing-api', version: '0.32.0'
  implementation group: 'io.opentracing', name: 'opentracing-util', version: '0.32.0'

  testImplementation project(':dd-smoke-tests')
  testImplementation project(':dd-smoke-tests:appsec')
  testImplementation(testFixtures(project(":dd-smoke-tests:iast-util")))
}

configurations.testImplementation {
  exclude group:'com.typesafe.play', module:"play-test_$scalaVer"
}

tasks.named("compileTestGroovy", GroovyCompile) {
  dependsOn 'stageMainDist'
  outputs.upToDateWhen {
    !stageMainDist.didWork
  }
}

tasks.withType(ScalaCompile).configureEach {
  // Despite the compatibility matrix, scala compiler doesn't properly
  // get the right compiler flags with JDK11 toolchain. Using 8 works.
  // * https://docs.scala-lang.org/overviews/jdk-compatibility/overview.html
  // * https://github.com/gradle/gradle/issues/19456
  configureCompiler(it, 8)
}

tasks.withType(AbstractCopyTask).configureEach {
  // Don't know why the play plugin tries to copy the logback.xml file twice, but only accept it once
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
