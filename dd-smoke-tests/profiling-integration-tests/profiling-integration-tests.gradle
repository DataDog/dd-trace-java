plugins {
  id "com.github.johnrengelman.shadow"
}

ext {
  enableJunitPlatform = true
  minJavaVersionForTests = JavaVersion.VERSION_1_8
  excludeJdk = ['IBM8']
}

apply from: "$rootDir/gradle/java.gradle"

description = 'Profiling Integration Tests.'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

jar {
  manifest {
    attributes('Main-Class': 'datadog.smoketest.profiling.ProfilingTestApplication')
  }
}

dependencies {
  implementation project(':dd-trace-api')
  api project(':dd-trace-ot')
  // https://mvnrepository.com/artifact/org.apache.commons/commons-math3
  implementation 'org.apache.commons:commons-math3:3.6.1'

  testImplementation project(':dd-smoke-tests')
  testImplementation project(':dd-java-agent:agent-profiling:profiling-testing')
  testImplementation deps.junit5
  testImplementation deps.mockito
  testImplementation deps.jmc
  testImplementation(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.10')
}

tasks.withType(Test).configureEach {
  dependsOn "shadowJar"
  jvmArgs "-Ddatadog.smoketest.profiling.shadowJar.path=${tasks.shadowJar.archivePath}"
}

shadowJar {
  include {
    return it.directory || it.path.endsWith('.jar') ||
      it.path.startsWith('io/opentracing') ||
      it.path.startsWith('datadog/smoketest') ||
      it.path.startsWith("org/apache/commons")
  }
}
