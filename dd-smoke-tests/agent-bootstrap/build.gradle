description = "Check agent can be loaded with another agent on a no protection domain system class-loader"

apply from: rootProject.file('gradle/java.gradle')

dependencies {
  testImplementation(project(':dd-smoke-tests'))
}

sourceSets {
  create('another-agent') {
    java
  }
}

tasks.jar {
  archiveClassifier = 'app'
  manifest {
    attributes('Main-Class': 'datadog.smoketest.app.App')
  }
}

tasks.register('anotherAgentJar', Jar) {
  archiveClassifier = 'another-agent'
  from(sourceSets['another-agent'].output)
  manifest {
    attributes('Premain-Class': 'datadog.smoketest.agent.AnotherAgent')
  }
}

tasks.withType(Test).configureEach {
  dependsOn "jar", "anotherAgentJar"

  def toolchain17 = getJavaLauncherFor(17).get()

  systemProperties(
    'datadog.smoketest.java.home': toolchain17.metadata.installationPath.asFile.absolutePath,
    'datadog.smoketest.jar.path': jar.archiveFile.get().asFile.absolutePath,
    'datadog.smoketest.another.agent.path': anotherAgentJar.archiveFile.get().asFile.absolutePath,
    )
}
