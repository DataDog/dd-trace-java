import java.util.regex.Pattern

apply from: "$rootDir/gradle/java.gradle"

description = 'Spring Boot 3.0 Native Smoke Tests.'

dependencies {
  testImplementation project(':dd-smoke-tests')
  testImplementation libs.bundles.jmc
}

// Check 'testJvm' gradle command parameter is GraalVM (e.g., -PtestJvm=graalvm21),
// if not nothing is done
def testJvm = gradle.startParameter.projectProperties.getOrDefault('testJvm', '')
def graalvmMatcher = Pattern.compile("graalvm([0-9]+)").matcher(testJvm?.toLowerCase(Locale.ROOT) ?: '')
def testGraalvmVersion = graalvmMatcher.find() ? Integer.parseInt(graalvmMatcher.group(1)) : -1

if (testGraalvmVersion >= 17) {
  // GraalVM with native-image capability for native compilation
  def graalvmHome = getLazyJavaHomeFor(testGraalvmVersion, true)
  // For GraalVM 21+ we need Java 17+ to run Gradle, for earlier versions use GraalVM itself
  def gradleJavaHome = testGraalvmVersion >= 21 ? getLazyJavaHomeFor(17) : graalvmHome
  // Configure build directory for application
  def appDir = "$projectDir/application"
  def appBuildDir = "$buildDir/application"
  def isWindows = System.getProperty('os.name').toLowerCase().contains('win')
  def gradlewCommand = isWindows ? 'gradlew.bat' : 'gradlew'

  // Define the task that builds the project
  tasks.register('springNativeBuild', Exec) {
    workingDir "$appDir"
    environment += [
      'GRADLE_OPTS': "-Dorg.gradle.jvmargs='-Xmx1024M'",
      'JAVA_HOME': gradleJavaHome,
      'GRAALVM_HOME': graalvmHome,
      'DD_TRACE_METHODS' : 'datadog.smoketest.springboot.controller.WebController[sayHello]',
      'NATIVE_IMAGE_DEPRECATED_BUILDER_SANITATION' : 'true'
    ]
    commandLine(
      "$rootDir/${gradlewCommand}",
      'nativeCompile',
      '--no-daemon',
      '--max-workers=4',
      "-PappBuildDir=$appBuildDir",
      "-PapiJar=${project(':dd-trace-api').tasks.jar.archiveFile.get()}",
      "-PagentPath=${project(':dd-java-agent').tasks.shadowJar.archiveFile.get()}",
      "-Pprofiler=true"
      )
    outputs.cacheIf { true }
    outputs.dir(appBuildDir)
      .withPropertyName('nativeApplication')
    inputs.files(fileTree(appDir) {
      include '**/*'
      exclude '.gradle/**'
    }).withPropertyName('application')
    .withPathSensitivity(PathSensitivity.RELATIVE)
    inputs.file(project(':dd-trace-api').tasks.jar.archiveFile.get()).withPropertyName('apiJar')
    inputs.file(project(':dd-java-agent').tasks.shadowJar.archiveFile.get()).withPropertyName('agentJar')
  }

  springNativeBuild {
    dependsOn project(':dd-trace-api').tasks.named("jar") // Use dev @Trace annotation
    dependsOn project(':dd-java-agent').tasks.named('shadowJar') // Use dev agent
  }

  tasks.named('compileTestGroovy') {
    dependsOn 'springNativeBuild'
    outputs.upToDateWhen {
      !springNativeBuild.didWork
    }
  }

  tasks.withType(Test).configureEach {
    jvmArgs "-Ddatadog.smoketest.spring.native.executable=$appBuildDir/native/nativeCompile/native-3.0-smoketest"
    jvmArgs "-Ddd.profiling.enabled=true"
  }
} else {
  tasks.withType(Test).configureEach {
    enabled = false
  }
}
