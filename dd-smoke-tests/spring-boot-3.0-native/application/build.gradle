plugins {
  id 'java'
  id 'org.springframework.boot' version '3.0.0'
  id 'io.spring.dependency-management' version '1.0.14.RELEASE'
  id 'org.graalvm.buildtools.native' version '0.9.28'
}

def sharedRootDir = "$rootDir/../../../"
def sharedConfigDirectory = "$sharedRootDir/gradle"
rootProject.ext.sharedConfigDirectory = sharedConfigDirectory

apply from: "$sharedConfigDirectory/repositories.gradle"

ext.withProfiler = hasProperty('profiler')

if (hasProperty('appBuildDir')) {
  buildDir = property('appBuildDir')
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  if (hasProperty('apiJar')) {
    implementation files(property('apiJar'))
  } else {
    implementation "com.datadoghq:dd-trace-api:1.2.0"
  }
}

if (hasProperty('agentPath')) {
  final agentPath = property('agentPath')
  graalvmNative {
    binaries {
      main {
        // quick build mode, enough for smoke test
        buildArgs.add("-Ob")
        buildArgs.add("-J-javaagent:$agentPath")
        buildArgs.add("-J-Dnet.bytebuddy.safe=false")
        if (withProfiler && property('profiler') == 'true') {
          buildArgs.add("-J-Ddd.profiling.enabled=true")
        }
        jvmArgs.add("-Xmx8192M")
      }
    }
  }
  tasks.named('nativeCompile') {
    inputs.file(agentPath).withPropertyName('agentJar')
  }
}
