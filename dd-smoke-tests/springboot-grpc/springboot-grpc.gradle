plugins {
  id "com.github.johnrengelman.shadow"
  id 'com.google.protobuf' version '0.8.18'
}

ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
  // TODO Java 17: This version of spring-boot doesn't support Java 17
  maxJavaVersionForTests = JavaVersion.VERSION_15
}

apply from: "$rootDir/gradle/java.gradle"
description = 'SpringBoot/gRPC combined Smoke Tests.'

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:3.12.0"
  }
  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:1.31.1'
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}

// The standard spring-boot plugin doesn't play nice with our project
// so we'll build a fat jar instead
jar {
  manifest {
    attributes('Main-Class': 'datadog.smoketest.springboot.SpringbootGrpcApplication')
  }
}

shadowJar {
  configurations = [project.configurations.runtimeClasspath, project.configurations.main_java8RuntimeClasspath]
}

dependencies {
  main_java8Implementation 'org.springframework.boot:spring-boot-starter-web:1.5.18.RELEASE'
  main_java8Implementation 'io.grpc:grpc-netty-shaded:1.31.1'
  main_java8Implementation 'io.grpc:grpc-protobuf:1.31.1'
  main_java8Implementation 'io.grpc:grpc-stub:1.31.1'
  main_java8Implementation 'org.apache.tomcat:annotations-api:6.0.53'
  main_java8Implementation 'com.google.cloud:google-cloud-spanner:1.61.0'

  testImplementation project(':dd-smoke-tests')
}

tasks.withType(Test).configureEach {
  dependsOn "shadowJar"

  jvmArgs "-Ddatadog.smoketest.springboot-grpc.shadowJar.path=${tasks.shadowJar.archivePath}"
}
