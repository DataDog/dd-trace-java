ext {
  minJavaVersionForTests = JavaVersion.VERSION_17
}

apply from: "$rootDir/gradle/java.gradle"

description = 'Spring Boot 3.3 WebMvc Smoke Tests.'

dependencies {
  testImplementation project(':dd-smoke-tests')
}

def appDir = "$projectDir/application"
def appBuildDir = "$buildDir/application"
def isWindows = System.getProperty("os.name").toLowerCase().contains("win")
def gradlewCommand = isWindows ? 'gradlew.bat' : 'gradlew'

// define the task that builds the project
tasks.register('webmvcBuild3', Exec) {
  workingDir "$appDir"
  def toolchain17 = getJavaLauncherFor(17).get()
  if (project.rootProject.hasProperty("mavenRepositoryProxy")) {
    environment += ["GRADLE_ARGS": " -PmavenRepositoryProxy=${project.rootProject.property("mavenRepositoryProxy")}"]
  }

  environment += [
    "GRADLE_OPTS": "-Dorg.gradle.jvmargs='-Xmx512M'",
    "JAVA_HOME": "$toolchain17.metadata.installationPath"
  ]
  commandLine "$rootDir/${gradlewCommand}", "bootJar", "--no-daemon", "--max-workers=4", "-PappBuildDir=$appBuildDir", "-PapiJar=${project(':dd-trace-api').tasks.jar.archiveFile.get()}"

  outputs.cacheIf { true }

  outputs.dir(appBuildDir)
    .withPropertyName("applicationJar")

  inputs.files(fileTree(appDir) {
    include '**/*'
    exclude '.gradle/**'
  })
  .withPropertyName("application")
  .withPathSensitivity(PathSensitivity.RELATIVE)
}

compileTestGroovy {
  dependsOn project(':dd-trace-api').tasks.named("jar")
}

tasks.named("compileTestGroovy").configure {
  dependsOn 'webmvcBuild3'
  outputs.upToDateWhen {
    !webmvcBuild3.didWork
  }
}

tasks.withType(Test).configureEach {
  jvmArgs "-Ddatadog.smoketest.springboot.uberJar.path=$appBuildDir/libs/webmvc-3.3-smoketest.jar"
}

spotless {
  java {
    target "**/*.java"
  }

  groovyGradle {
    target '*.gradle', "**/*.gradle"
  }
}
