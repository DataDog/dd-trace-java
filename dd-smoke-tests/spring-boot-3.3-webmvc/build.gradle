apply from: "$rootDir/gradle/java.gradle"

testJvmConstraints {
  minJavaVersion = JavaVersion.VERSION_17
}

description = 'Spring Boot 3.3 WebMvc Smoke Tests.'

dependencies {
  testImplementation project(':dd-smoke-tests')
}

def appDir = "$projectDir/application"
def appBuildDir = "$buildDir/application"
def isWindows = System.getProperty("os.name").toLowerCase().contains("win")
def gradlewCommand = isWindows ? 'gradlew.bat' : 'gradlew'

// define the task that builds the project
tasks.register('webmvcBuild3', Exec) {
  workingDir "$appDir"
  environment += [
    "GRADLE_OPTS": "-Dorg.gradle.jvmargs='-Xmx512M'",
    "JAVA_HOME": getLazyJavaHomeFor(17)
  ]
  commandLine "$rootDir/${gradlewCommand}", "bootJar", "--no-daemon", "--max-workers=4", "-PappBuildDir=$appBuildDir", "-PapiJar=${project(':dd-trace-api').tasks.jar.archiveFile.get()}"

  outputs.cacheIf { true }

  outputs.dir(appBuildDir)
    .withPropertyName("applicationJar")

  inputs.files(fileTree(appDir) {
    include '**/*'
    exclude '.gradle/**'
  })
  .withPropertyName("application")
  .withPathSensitivity(PathSensitivity.RELATIVE)
}

tasks.named("compileTestGroovy", GroovyCompile) {
  dependsOn project(':dd-trace-api').tasks.named("jar")
  dependsOn 'webmvcBuild3'
  outputs.upToDateWhen {
    !webmvcBuild3.didWork
  }
}

tasks.withType(Test).configureEach {
  jvmArgs "-Ddatadog.smoketest.springboot.uberJar.path=$appBuildDir/libs/webmvc-3.3-smoketest.jar"
}
