import java.time.Duration
import java.time.temporal.ChronoUnit

plugins {
  id 'com.gradleup.shadow'
}

apply from: "$rootDir/gradle/java.gradle"
description = 'Gradle Daemon Instrumentation Smoke Tests.'

// Gradle 8.14.3 bundles Groovy 3 via `gradleTestKit()`, so we need Spock for Groovy 3 instead of Groovy 4.
// Gradle 9.x bundles Groovy 4, so eventually we can drop this workaround.
configurations.configureEach {
  // Exclude Groovy 4. Starting from v4 it has group: `org.apache.groovy`.
  exclude group: 'org.apache.groovy'

  resolutionStrategy {
    // Force Spock for Groovy 3.
    force "org.spockframework:spock-core:2.4-groovy-3.0"
  }
}

dependencies {
  testImplementation gradleTestKit()
  testImplementation project(':dd-smoke-tests:backend-mock')
  testImplementation project(':internal-api')
  testImplementation project(':dd-java-agent:agent-ci-visibility:civisibility-test-fixtures')
}

tasks.named("test", Test) {
  testLogging {
    events "passed", "skipped", "failed", "standardOut", "standardError"
  }

  // Overriding the default timeout as Gradle smoke tests might run for a longer duration.
  timeout = Duration.of(25, ChronoUnit.MINUTES)

  if (project.hasProperty("mavenRepositoryProxy")) {
    // propagate proxy URL to tests, to then propagate it to nested Gradle builds
    environment "MAVEN_REPOSITORY_PROXY", project.property("mavenRepositoryProxy")
  }
}
