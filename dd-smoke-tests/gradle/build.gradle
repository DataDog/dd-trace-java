import java.time.Duration
import java.time.temporal.ChronoUnit

plugins {
  id "com.gradleup.shadow"
}

apply from: "$rootDir/gradle/java.gradle"
description = 'Gradle Daemon Instrumentation Smoke Tests.'

dependencies {
  testImplementation gradleTestKit()
  testImplementation project(':dd-smoke-tests:backend-mock')
}

test {
  testLogging {
    events "passed", "skipped", "failed", "standardOut", "standardError"
  }

  if (project.hasProperty("mavenRepositoryProxy")) {
    // propagate proxy URL to tests, to then propagate it to nested Gradle builds
    environment "MAVEN_REPOSITORY_PROXY", project.property("mavenRepositoryProxy")
  }

  // overriding the default timeout of 9 minutes set in configure_tests.gradle,
  // as Gradle smoke tests might run for a longer duration
  timeout = Duration.of(15, ChronoUnit.MINUTES)
}

