import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
  id 'com.gradleup.shadow'
  id 'java-test-fixtures'
}

apply from: "$rootDir/gradle/java.gradle"
description = 'Jersey 3 Smoke Tests.'

tasks.named("jar", Jar) {
  manifest {
    attributes "Main-Class": "smoketest.MainApp"
  }
}

dependencies {
  implementation group: 'org.glassfish.jersey.containers', name: 'jersey-container-grizzly2-http', version:'3.0.2'
  implementation 'org.glassfish.jersey.media:jersey-media-json-jackson:3.0.2'
  implementation group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version:'3.0.2'
  implementation group: 'org.glassfish.hk2', name: 'hk2-metadata-generator', version:'3.0.2'
  implementation group: 'jakarta.activation', name: 'jakarta.activation-api', version:'2.0.1'
  testImplementation project(':dd-smoke-tests')
  testImplementation(testFixtures(project(":dd-smoke-tests:iast-util")))
  testImplementation project(':dd-smoke-tests:appsec')
}

tasks.withType(Test).configureEach {
  dependsOn "shadowJar"
  def shadowJarTask = tasks.named('shadowJar', ShadowJar)
  jvmArgumentProviders.add(new CommandLineArgumentProvider() {
      @Override
      Iterable<String> asArguments() {
        return shadowJarTask.map { ["-Ddatadog.smoketest.jersey3.jar.path=${it.archiveFile.get()}"] }.get()
      }
    })
}


