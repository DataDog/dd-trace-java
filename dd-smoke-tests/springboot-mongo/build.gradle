plugins {
  id "com.github.johnrengelman.shadow"
}

ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

apply from: "$rootDir/gradle/java.gradle"
description = 'SpringBoot Spring-Data-Mongo Smoke Tests.'

// The standard spring-boot plugin doesn't play nice with our project
// so we'll build a fat jar instead
jar {
  manifest {
    attributes('Main-Class': 'datadog.smoketest.springboot.SpringbootApplication')
  }
}

shadowJar {
  configurations = [project.configurations.runtimeClasspath, project.configurations.main_java8RuntimeClasspath]
}

dependencies {
  main_java8Implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.4.1'
  main_java8Implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-mongodb', version: '2.4.1'

  testImplementation project(':dd-smoke-tests')
  testImplementation group: 'org.testcontainers', name: 'mongodb', version: versions.testcontainers
}

tasks.withType(Test).configureEach {
  dependsOn "shadowJar"

  jvmArgs "-Ddatadog.smoketest.springboot.shadowJar.path=${tasks.shadowJar.archivePath}"

  usesService(testcontainersLimit)
}
