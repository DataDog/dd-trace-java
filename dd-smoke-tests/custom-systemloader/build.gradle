import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
  id 'com.gradleup.shadow'
}

description = 'Check classes loaded by custom system class-loader are transformed'
apply from: "$rootDir/gradle/java.gradle"

tasks.named("jar", Jar) {
  manifest {
    attributes('Main-Class': 'sample.app.App')
  }
}

tasks.named("shadowJar", ShadowJar) {
  configurations = [project.configurations.runtimeClasspath]
}

dependencies {
  api group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0'

  testImplementation project(':dd-smoke-tests')
}

tasks.withType(Test).configureEach {
  def shadowJarTask = tasks.named('shadowJar', ShadowJar)
  dependsOn shadowJarTask
  jvmArgumentProviders.add(new CommandLineArgumentProvider() {
      @Override
      Iterable<String> asArguments() {
        return shadowJarTask.map { ["-Ddatadog.smoketest.systemloader.shadowJar.path=${it.archiveFile.get()}"] }.get()
      }
    })
}
