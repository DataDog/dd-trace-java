import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
  id 'com.gradleup.shadow'
}

apply from: "$rootDir/gradle/java.gradle"

testJvmConstraints {
  minJavaVersion = JavaVersion.VERSION_17
}

description = 'RUM Tomcat 11 Smoke Tests'

dependencies {
  implementation 'org.apache.tomcat.embed:tomcat-embed-core:11.0.8'
  implementation 'org.apache.tomcat.embed:tomcat-embed-jasper:11.0.8'
  implementation 'jakarta.servlet:jakarta.servlet-api:6.0.0'

  testImplementation project(':dd-smoke-tests:rum')
}

tasks.named("jar", Jar) {
  manifest {
    attributes('Main-Class': 'com.example.Main')
  }
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

tasks.withType(JavaCompile).configureEach {
  configureCompiler(it, 17, JavaVersion.VERSION_17)
}

tasks.withType(Test).configureEach {
  def shadowJarTask = tasks.named('shadowJar', ShadowJar)
  dependsOn shadowJarTask
  jvmArgumentProviders.add(new CommandLineArgumentProvider() {
      @Override
      Iterable<String> asArguments() {
        return shadowJarTask.map { ["-Ddatadog.smoketest.rum.tomcat11.shadowJar.path=${it.archiveFile.get()}"] }.get()
      }
    })
}
