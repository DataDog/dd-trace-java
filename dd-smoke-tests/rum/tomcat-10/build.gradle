import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
  id 'com.gradleup.shadow'
}

apply from: "$rootDir/gradle/java.gradle"

testJvmConstraint {
  minJavaVersionForTests = JavaVersion.VERSION_11
}

description = 'RUM Tomcat 10 Smoke Tests'

dependencies {
  implementation 'org.apache.tomcat.embed:tomcat-embed-core:10.1.42'
  implementation 'org.apache.tomcat.embed:tomcat-embed-jasper:10.1.42'
  implementation 'jakarta.servlet:jakarta.servlet-api:5.0.0'

  testImplementation project(':dd-smoke-tests:rum')
}

tasks.named("jar", Jar) {
  manifest {
    attributes('Main-Class': 'com.example.Main')
  }
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(11)
  }
}
tasks.withType(JavaCompile).configureEach {
  configureCompiler(it, 11, JavaVersion.VERSION_11)
}

tasks.withType(Test).configureEach {
  def shadowJarTask = tasks.named('shadowJar', ShadowJar)
  dependsOn shadowJarTask
  jvmArgumentProviders.add(new CommandLineArgumentProvider() {
      @Override
      Iterable<String> asArguments() {
        return shadowJarTask.map { ["-Ddatadog.smoketest.rum.tomcat10.shadowJar.path=${it.archiveFile.get()}"] }.get()
      }
    })
}
