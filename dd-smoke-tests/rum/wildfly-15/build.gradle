ext {
  serverName = 'wildfly'
  //serverModule = 'servlet'
  serverModule = 'wildfly'
  serverVersion = '15.0.0.Final'
  serverExtension = 'zip'
}

repositories {
  ivy {
    url = 'https://download.jboss.org/'
    patternLayout {
      // artifact '/[organisation]/[revision]/[module]/[organisation]-[module]-[revision].[ext]'
      // we download the full EE profile and not the servlet minimal one
      artifact '/[organisation]/[revision]/[organisation]-[revision].[ext]'
    }
    metadataSources {
      it.artifact()
    }
  }
}

apply from: "$rootDir/gradle/java.gradle"

testJvmConstraint {
  maxJavaVersionForTests = JavaVersion.VERSION_11
}

description = 'Wildfly Smoke Tests.'

configurations {
  register('serverFile') {
    extendsFrom implementation
    canBeResolved = true
  }
}

dependencies {
  // uses the ivy repository url to download the wildfly servlet zip
  // organisation = serverName, revision = serverVersion, module = serverModule, ext = serverExtension
  serverFile "${serverName}:${serverModule}:${serverVersion}@${serverExtension}"
  testImplementation project(':dd-smoke-tests:rum')
  testImplementation project(':dd-smoke-tests')
}

def appDir = "$projectDir/rum-ear"
def appBuildDir = "$buildDir/rm-ear"
def isWindows = System.getProperty("os.name").toLowerCase().contains("win")
def gradlewCommand = isWindows ? 'gradlew.bat' : 'gradlew'
// define the task that builds the quarkus project
tasks.register('earBuild', Exec) {
  workingDir "$appDir"
  environment += ["GRADLE_OPTS": "-Dorg.gradle.jvmargs='-Xmx512M'"]
  commandLine "$rootDir/${gradlewCommand}", "assemble", "--no-daemon", "--max-workers=4", "-PappBuildDir=$appBuildDir", "-PapiJar=${project(':dd-trace-api').tasks.jar.archiveFile.get()}"

  outputs.cacheIf { true }

  outputs.dir(appBuildDir)
    .withPropertyName("applicationEar")

  inputs.files(fileTree(appDir) {
    include '**/*'
    exclude '.gradle/**'
  })
  .withPropertyName("application")
  .withPathSensitivity(PathSensitivity.RELATIVE)

  dependsOn project(':dd-trace-api').tasks.named("jar")
}

tasks.named("compileTestGroovy", GroovyCompile) {
  dependsOn 'earBuild'
  outputs.upToDateWhen {
    !tasks.named('earBuild').get().didWork
  }
}

spotless {
  java {
    target "**/*.java"
  }

  groovyGradle {
    target '*.gradle', "**/*.gradle"
  }
}

tasks.register("unzip", Copy) {
  dependsOn 'earBuild'
  mustRunAfter 'compileTestGroovy'
  def zipFileNamePrefix = "wildfly"

  def serverZipTree = providers.provider {
    // eager access
    def zipPath = project.configurations.serverFile.find {
      it.name.startsWith(zipFileNamePrefix)
    }
    if (zipPath == null) {
      throw new GradleException("Can't find server zip file that starts with: " + zipFileNamePrefix)
    }
    zipTree(zipPath)
  }

  from serverZipTree
  into layout.buildDirectory

  // When tests are disabled this would still be run, so disable this manually
  onlyIf { !project.rootProject.hasProperty("skipTests") }
}

tasks.withType(Jar).configureEach {
  dependsOn 'unzip'
}

def wildflyDir = "${buildDir}/${serverName}-${serverVersion}"

tasks.register("deploy", Copy) {
  dependsOn 'unzip'
  from "${appBuildDir}/libs/wildfly-rum-ear-smoketest.ear"
  into "${wildflyDir}/standalone/deployments"
}

tasks.withType(Test).configureEach {
  dependsOn 'deploy'
  jvmArgs "-Ddatadog.smoketest.wildflyDir=${wildflyDir}"
}

