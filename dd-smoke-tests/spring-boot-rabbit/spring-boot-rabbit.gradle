plugins {
  id "com.github.johnrengelman.shadow"
}

ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

apply from: "$rootDir/gradle/java.gradle"
description = 'SpringBoot RabbitMQ Smoke Tests.'

// The standard spring-boot plugin doesn't play nice with our project
// so we'll build a fat jar instead
jar {
  manifest {
    attributes('Main-Class': 'datadog.smoketest.springboot.SpringbootApplication')
  }
}

shadowJar {
  configurations = [project.configurations.runtimeClasspath, project.configurations.main_java8RuntimeClasspath]
}

dependencies {
  main_java8Implementation project(':dd-trace-api')
  main_java8Implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.5.4'
  main_java8Implementation group: 'org.springframework.boot', name: 'spring-boot-starter-amqp', version: '2.5.4'

  testImplementation project(':dd-smoke-tests')
  testImplementation group: 'org.testcontainers', name: 'rabbitmq', version: versions.testcontainers
}

tasks.withType(Test).configureEach {
  dependsOn "shadowJar"

  jvmArgs "-Ddatadog.smoketest.springboot.shadowJar.path=${tasks.shadowJar.archivePath}"

  usesService(testcontainersLimit)
}
