apply from: "$rootDir/gradle/java.gradle"

description = 'Spring Boot 2.7 Webflux Smoke Tests.'

dependencies {
  testImplementation project(':dd-smoke-tests')
}

def appDir = "$projectDir/application"
def appBuildDir = "$buildDir/application"
def isWindows = System.getProperty("os.name").toLowerCase().contains("win")
def gradlewCommand = isWindows ? 'gradlew.bat' : 'gradlew'

// define the task that builds the quarkus project
tasks.register('webfluxBuild', Exec) {
  workingDir "$appDir"
  environment.JAVA_HOME = System.getenv("JAVA_8_HOME")
  environment += ["GRADLE_OPTS": "-Dorg.gradle.jvmargs='-Xmx512M'"]
  commandLine "$rootDir/${gradlewCommand}", "bootJar", "--no-daemon", "--max-workers=4", "-PappBuildDir=$appBuildDir", "-PapiJar=${project(':dd-trace-api').tasks.jar.archiveFile.get()}"

  outputs.cacheIf { true }

  outputs.dir(appBuildDir)
    .withPropertyName("applicationJar")

  inputs.files(fileTree(appDir) {
    include '**/*'
    exclude '.gradle/**'
  })
  .withPropertyName("application")
  .withPathSensitivity(PathSensitivity.RELATIVE)
}

tasks.named("webfluxBuild", Exec) {
  dependsOn project(':dd-trace-api').tasks.named("jar")
}

tasks.named("compileTestGroovy", GroovyCompile) {
  dependsOn 'webfluxBuild'
  outputs.upToDateWhen {
    !tasks.named('webfluxBuild').get().didWork
  }
}

tasks.withType(Test).configureEach {
  jvmArgs "-Ddatadog.smoketest.webflux.uberJar.path=$appBuildDir/libs/webflux-2.7-smoketest.jar"
}

spotless {
  java {
    target "**/*.java"
  }

  groovyGradle {
    target '*.gradle', "**/*.gradle"
  }
}
