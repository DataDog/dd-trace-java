import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
  id 'com.gradleup.shadow'
}

apply from: "$rootDir/gradle/java.gradle"
description = 'Command Line Application Smoke Tests.'

tasks.named("jar", Jar) {
  manifest {
    attributes('Main-Class': 'datadog.smoketest.cli.CliApplication')
  }
}

dependencies {
  implementation project(':dd-trace-api')

  testImplementation project(':dd-smoke-tests')
}

tasks.withType(Test).configureEach {
  def shadowTaskRef = tasks.named('shadowJar', ShadowJar)
  dependsOn shadowTaskRef

  jvmArgumentProviders.add(new CommandLineArgumentProvider() {
      @Override
      Iterable<String> asArguments() {
        return shadowTaskRef.map { ["-Ddatadog.smoketest.cli.shadowJar.path=${it.archiveFile.get()}"] }.get()
      }
    })
}
