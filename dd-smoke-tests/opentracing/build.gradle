import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
  id 'com.gradleup.shadow'
}

apply from: "$rootDir/gradle/java.gradle"

dependencies {
  implementation project(path: ':dd-trace-ot', configuration: 'shadow')
  implementation project(':dd-trace-api')
  // OpenTracing
  implementation group: 'io.opentracing', name: 'opentracing-api', version: '0.32.0'
  implementation group: 'io.opentracing', name: 'opentracing-noop', version: '0.32.0'
  implementation group: 'io.opentracing', name: 'opentracing-util', version: '0.32.0'
  implementation group: 'io.opentracing.contrib', name: 'opentracing-tracerresolver', version: '0.1.0'

  implementation libs.slf4j

  testImplementation project(':dd-smoke-tests')
}

tasks.withType(Test).configureEach {
  def shadowJarTask = tasks.named('shadowJar', ShadowJar)
  dependsOn shadowJarTask

  jvmArgumentProviders.add(new CommandLineArgumentProvider() {
      @Override
      Iterable<String> asArguments() {
        return shadowJarTask.map { ["-Ddatadog.smoketest.shadowJar.path=${it.archiveFile.get()}"] }.get()
      }
    })
}
