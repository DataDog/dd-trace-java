import org.springframework.boot.gradle.tasks.bundling.BootJar

plugins {
  id 'java'
  id 'org.springframework.boot' version '3.5.9'
  id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/spring-boot-plugin.gradle"
description = 'Open Feature provider Smoke Tests.'

testJvmConstraints {
  minJavaVersion = JavaVersion.VERSION_11
}

tasks.named("compileJava", JavaCompile) {
  configureCompiler(it, 11, JavaVersion.VERSION_11)
}

dependencies {
  implementation project(':products:feature-flagging:feature-flagging-api')
  implementation 'org.springframework.boot:spring-boot-starter-web'

  testImplementation project(':dd-smoke-tests')
  testImplementation project(':products:feature-flagging:feature-flagging-lib')
}

tasks.withType(Test).configureEach {
  dependsOn "bootJar"
  def bootJarTask = tasks.named('bootJar', BootJar)
  jvmArgumentProviders.add(new CommandLineArgumentProvider() {
      @Override
      Iterable<String> asArguments() {
        return bootJarTask.map { ["-Ddatadog.smoketest.springboot.shadowJar.path=${it.archiveFile.get()}"] }.get()
      }
    })
}
