import org.springframework.boot.gradle.tasks.bundling.BootJar

plugins {
  id 'java'
  id 'org.springframework.boot' version '2.7.15'
  id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

ext {
  minJavaVersionForTests = JavaVersion.VERSION_11
}

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/spring-boot-plugin.gradle"
description = 'Open Feature provider Smoke Tests.'

tasks.named("compileJava", JavaCompile) {
  configureCompiler(it, 11, JavaVersion.VERSION_11)
}

dependencies {
  implementation project(':dd-trace-openfeature')
  implementation 'org.springframework.boot:spring-boot-starter-web'
  testImplementation project(':dd-smoke-tests')
}

tasks.withType(Test).configureEach {
  dependsOn "bootJar"
  def bootJarTask = tasks.named('bootJar', BootJar)
  jvmArgumentProviders.add(new CommandLineArgumentProvider() {
      @Override
      Iterable<String> asArguments() {
        return bootJarTask.map { ["-Ddatadog.smoketest.springboot.shadowJar.path=${it.archiveFile.get()}"] }.get()
      }
    })
}
