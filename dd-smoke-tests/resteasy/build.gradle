import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
  id 'com.gradleup.shadow'
  id 'java-test-fixtures'
}

apply from: "$rootDir/gradle/java.gradle"
description = 'RestEasy Smoke Test.'

tasks.named("jar", Jar) {
  manifest {
    attributes "Main-Class": "smoketest.resteasy.Main"
  }
}

dependencies {
  implementation group: 'org.jboss.resteasy', name: 'resteasy-undertow', version:'3.1.0.Final'
  implementation group: 'org.jboss.resteasy', name: 'resteasy-cdi', version:'3.1.0.Final'
  implementation group: 'org.jboss.weld.servlet', name: 'weld-servlet', version: '2.4.8.Final'
  implementation group: 'org.jboss.resteasy', name: 'resteasy-jackson2-provider', version: '3.1.0.Final'

  implementation group: 'javax.el', name: 'javax.el-api', version:'3.0.0'

  implementation "jakarta.xml.bind:jakarta.xml.bind-api:2.3.2"
  implementation "org.glassfish.jaxb:jaxb-runtime:2.3.2"

  testImplementation project(':dd-smoke-tests')
  testImplementation(testFixtures(project(":dd-smoke-tests:iast-util")))
  testImplementation project(':dd-smoke-tests:appsec')
}

tasks.withType(Test).configureEach {
  def shadowJarTask = tasks.named('shadowJar', ShadowJar)
  dependsOn shadowJarTask
  jvmArgumentProviders.add(new CommandLineArgumentProvider() {
      @Override
      Iterable<String> asArguments() {
        return shadowJarTask.map { ["-Ddatadog.smoketest.resteasy.jar.path=${it.archiveFile.get()}"] }.get()
      }
    })
}
