plugins {
  id 'play'
}

ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

def playVersion = "2.6.25"
def scalaVersion = System.getProperty("scala.binary.version", /* default = */ "2.12")

model {
  components {
    play {
      platform play: playVersion, scala: scalaVersion, java: '1.8'
      injectedRoutesGenerator = true
    }
  }
  distributions {
    playBinary {
      contents {
        from("conf") {
          into "conf"
        }
      }
    }
  }
}

repositories {
  jcenter()
  maven {
    name "lightbend-maven-releases"
    url "https://repo.lightbend.com/lightbend/maven-release"
  }
  ivy {
    name "lightbend-ivy-release"
    url "https://repo.lightbend.com/lightbend/ivy-releases"
    layout "ivy"
  }
}

apply from: "$rootDir/gradle/java.gradle"
spotless {
  groovy {
    excludeJava()
    greclipse().configFile(project.rootProject.rootDir.path + '/gradle/enforcement/spotless-groovy.properties')
  }
}

description = 'Play Integration 2.6 Tests.'

dependencies {
  play "com.typesafe.play:play-guice_$scalaVersion:$playVersion"
  play "com.typesafe.play:play-logback_$scalaVersion:$playVersion"
  play "com.typesafe.play:filters-helpers_$scalaVersion:$playVersion"
  play "com.typesafe.play:play-netty-server_$scalaVersion:$playVersion"
  play "com.typesafe.play:play-ahc-ws_$scalaVersion:$playVersion"

  play project(':dd-trace-api')
  play group: 'io.opentracing', name: 'opentracing-api', version: '0.32.0'
  play group: 'io.opentracing', name: 'opentracing-util', version: '0.32.0'

  testCompile project(':dd-smoke-tests')
}

tasks.withType(Test).configureEach {
  dependsOn 'stagePlayBinaryDist'
  outputs.upToDateWhen {
    !stagePlayBinaryDist.didWork
  }
}

spotless {
  java {
    target "**/*.java"
  }
  scala {
    target "**/*.scala"
  }
}
