import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
  id "com.gradleup.shadow"
}

apply from: "$rootDir/gradle/java.gradle"

tasks.named("jar", Jar) {
  manifest {
    attributes('Main-Class': 'datadog.smoketest.ratpack.RatpackApp')
  }
}
dependencies {
  implementation 'io.ratpack:ratpack-core:1.5.0'
  implementation 'com.sun.activation:jakarta.activation:1.2.2'

  testImplementation project(':dd-smoke-tests')
  testImplementation project(':dd-smoke-tests:appsec')
}

tasks.withType(Test).configureEach {
  def shadowJarTask = tasks.named('shadowJar', ShadowJar)
  dependsOn shadowJarTask
  jvmArgumentProviders.add(new CommandLineArgumentProvider() {
      @Override
      Iterable<String> asArguments() {
        return shadowJarTask.map { ["-Ddatadog.smoketest.ratpack.shadowJar.path=${it.archiveFile.get()}"] }.get()
      }
    })
}

