import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
  id 'com.gradleup.shadow'
  id 'java-test-fixtures'
}

ext {
  maxJavaVersionForTests = JavaVersion.VERSION_15
}

apply from: "$rootDir/gradle/java.gradle"
description = 'SpringBoot Smoke Tests.'

// The standard spring-boot plugin doesn't play nice with our project
// so we'll build a fat jar instead
tasks.named("jar", Jar) {
  manifest {
    attributes('Main-Class': 'datadog.smoketest.springboot.SpringbootApplication')
  }
}

dependencies {
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '1.5.18.RELEASE'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: '1.5.18.RELEASE'
  implementation group: 'com.auth0', name: 'java-jwt', version: '4.0.0-beta.0'
  implementation group: 'com.auth0', name: 'jwks-rsa', version: '0.21.1'
  implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.22'
  implementation group: 'com.google.code.gson', name: 'gson', version: '2.10'

  implementation group: 'org.springframework', name: 'spring-web', version: '1.5.18.RELEASE'

  // ssrf
  implementation group: 'commons-httpclient', name: 'commons-httpclient', version: '2.0'
  implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.0'
  implementation group: 'com.squareup.okhttp', name: 'okhttp', version: '2.2.0'
  implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.0.0'
  implementation group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: '5.0'
  implementation group: 'org.apache.httpcomponents', name: 'httpasyncclient', version: '4.0'

  testImplementation project(':dd-smoke-tests')
  testImplementation(testFixtures(project(":dd-smoke-tests:iast-util")))

  implementation project(':dd-smoke-tests:iast-util')
  implementation 'jakarta.mail:jakarta.mail-api:2.0.1'
  implementation 'jakarta.activation:jakarta.activation-api:2.1.3'
  implementation 'com.sun.mail:jakarta.mail:2.0.1'
  // text sanitization
  implementation group: 'org.apache.commons', name: 'commons-text', version: '1.0'
}

tasks.withType(Test).configureEach {
  dependsOn "shadowJar"

  def shadowJarTask = tasks.named('shadowJar', ShadowJar)
  jvmArgumentProviders.add(new CommandLineArgumentProvider() {
      @Override
      Iterable<String> asArguments() {
        return shadowJarTask.map { ["-Ddatadog.smoketest.springboot.shadowJar.path=${it.archiveFile.get()}"] }.get()
      }
    })
}
