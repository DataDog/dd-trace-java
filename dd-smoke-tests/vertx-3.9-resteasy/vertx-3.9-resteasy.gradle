plugins {
  id 'idea'
}

ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

apply from: "$rootDir/gradle/java.gradle"

dependencies {
  testImplementation project(':dd-smoke-tests')
}

def appDir = "$projectDir/application"
def appBuildDir = "$buildDir/application"
// define the task that builds the quarkus project
tasks.register('vertxBuild', Exec) {
  workingDir "$appDir"
  environment += ["GRADLE_OPTS": "-Dorg.gradle.jvmargs='-Xmx512M'"]
  commandLine "$appDir/gradlew", "assemble", "--no-daemon", "--max-workers=4", "-PappBuildDir=$appBuildDir", "-PapiJar=${project(':dd-trace-api').tasks.jar.archivePath}"
  outputs.dir(appBuildDir)
  inputs.dir(appDir)
}

vertxBuild {
  dependsOn project(':dd-trace-api').tasks.named("jar")
}

tasks.named("compileTestGroovy").configure {
  dependsOn 'vertxBuild'
  outputs.upToDateWhen {
    !vertxBuild.didWork
  }
}

tasks.withType(Test).configureEach {
  jvmArgs "-Ddatadog.smoketest.vertx.uberJar.path=$appBuildDir/libs/vertx-3.9-resteasy-1.0.0-SNAPSHOT-fat.jar"
}

spotless {
  java {
    target "**/*.java"
  }

  groovyGradle {
    target '*.gradle', "**/*.gradle"
  }
}

idea {
  module {
    excludeDirs += [file("$appDir")]
  }
}
