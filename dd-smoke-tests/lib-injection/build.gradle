import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
  id 'application'
  id 'com.gradleup.shadow'
}

apply from: "$rootDir/gradle/java.gradle"
description = 'Lib-injection Smoke Tests'

application {
  mainClass = 'datadog.smoketest.Application'
}

tasks.named("jar", Jar) {
  manifest {
    attributes(
      'Premain-Class': 'datadog.smoketest.Agent',
      )
  }
}

dependencies {
  implementation group: 'io.opentelemetry', name: 'opentelemetry-api', version: '1.4.0'
  testImplementation project(':dd-smoke-tests')
}

tasks.withType(Test).configureEach {
  dependsOn 'shadowJar'
  def shadowJarTask = tasks.named('shadowJar', ShadowJar)
  jvmArgumentProviders.add(new CommandLineArgumentProvider() {
      @Override
      Iterable<String> asArguments() {
        return shadowJarTask.map { ["-Ddatadog.smoketest.shadowJar.path=${it.archiveFile.get()}"] }.get()
      }
    })
}
