import org.springframework.boot.gradle.tasks.bundling.BootJar

plugins {
  id 'java'
  id 'org.springframework.boot' version '2.7.15'
  id 'io.spring.dependency-management' version '1.0.15.RELEASE'
  id 'java-test-fixtures'
}

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/spring-boot-plugin.gradle"
description = 'SpringBoot Velocity Smoke Tests.'

dependencies {
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '1.5.18.RELEASE'
  implementation group: 'org.apache.velocity', name: 'velocity', version: '1.5'
  implementation(group: 'org.apache.velocity', name: 'velocity-tools', version: '1.3') {
    exclude group: 'javax.servlet', module: 'servlet-api'
  }

  testImplementation project(':dd-smoke-tests')
  testImplementation(testFixtures(project(":dd-smoke-tests:iast-util")))

  // Some strange issue with spotbug classpath???
  spotbugs "com.github.spotbugs:spotbugs:4.9.8"
  spotbugs "org.apache.commons:commons-lang3:3.19.0"
}

tasks.withType(Test).configureEach {
  dependsOn "bootJar"
  def bootJarTask = tasks.named('bootJar', BootJar)
  jvmArgumentProviders.add(new CommandLineArgumentProvider() {
      @Override
      Iterable<String> asArguments() {
        return bootJarTask.map { ["-Ddatadog.smoketest.springboot.shadowJar.path=${it.archiveFile.get()}"] }.get()
      }
    })
}
