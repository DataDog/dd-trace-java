
apply from: "$rootDir/gradle/java.gradle"
description = 'SpringBoot Open Liberty 20-22 Smoke Tests'

dependencies {
  testImplementation project(':dd-smoke-tests')
}

def appDir = "$projectDir/application"
def jarName = "demo-open-liberty-app.jar"
def jarPath = "$buildDir/application/target/${jarName}"
def isWindows = System.getProperty("os.name").toLowerCase().contains("win")
def mvnwCommand = isWindows ? 'mvnw.cmd' : 'mvnw'

// compile the Open liberty spring boot server
tasks.register('mvnStage', Exec) {
  workingDir "$appDir"
  environment.JAVA_HOME = System.getenv("JAVA_8_HOME")
  commandLine "$rootDir/${mvnwCommand}", 'package', "-Dtarget.dir=${buildDir}/application/target"
  inputs.dir "$appDir/src"
  inputs.file "$appDir/pom.xml"
  outputs.file jarPath
}

tasks.named("compileTestGroovy", GroovyCompile) {
  dependsOn 'mvnStage'
  outputs.upToDateWhen {
    !mvnStage.didWork
  }
}

// compiled dir of the packaged spring boot app with embedded openliberty
tasks.withType(Test).configureEach {
  jvmArgs "-Ddatadog.smoketest.openliberty.jar.path=${jarPath}"
}


spotless {
  java {
    target fileTree("$appDir") {
      include "**/*.java"
    }
  }

  groovyGradle {
    target '*.gradle', "**/*.gradle"
  }
}
