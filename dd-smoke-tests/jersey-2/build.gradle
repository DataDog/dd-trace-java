import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
  id 'com.gradleup.shadow'
  id 'java-test-fixtures'
}

apply from: "$rootDir/gradle/java.gradle"
description = 'Jersey 2 Smoke Tests.'

tasks.named("jar", Jar) {
  manifest {
    attributes "Main-Class": "com.restserver.Main"
  }
}

dependencies {
  implementation group: 'org.eclipse.jetty', name: 'jetty-server', version:'9.0.4.v20130625'
  implementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version:'9.0.4.v20130625'
  implementation group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet-core', version:'2.0'
  implementation group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version:'2.0'
  implementation group: 'javax.xml', name: 'jaxb-api', version:'2.1'
  testImplementation project(':dd-smoke-tests')
  testImplementation(testFixtures(project(":dd-smoke-tests:iast-util")))
  testImplementation project(':dd-smoke-tests:appsec')
}

tasks.withType(Test).configureEach {
  dependsOn "shadowJar"
  def shadowJarTask = tasks.named('shadowJar', ShadowJar)
  jvmArgumentProviders.add(new CommandLineArgumentProvider() {
      @Override
      Iterable<String> asArguments() {
        return shadowJarTask.map { ["-Ddatadog.smoketest.jersey2.jar.path=${it.archiveFile.get()}"] }.get()
      }
    })
}
