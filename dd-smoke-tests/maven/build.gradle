import java.time.Duration
import java.time.temporal.ChronoUnit

plugins {
  id 'com.gradleup.shadow'
}

apply from: "$rootDir/gradle/java.gradle"
description = 'Maven Instrumentation Smoke Tests.'

dependencies {
  implementation group: 'org.apache.maven.wrapper', name: 'maven-wrapper', version: '3.2.0'

  testImplementation project(':dd-smoke-tests:backend-mock')
  testImplementation project(':internal-api')
  testImplementation project(':dd-java-agent:agent-ci-visibility:civisibility-test-fixtures')
}

jar {
  manifest {
    attributes('Main-Class': 'datadog.smoketest.maven.MavenRunner')
  }
}

tasks.withType(Test).configureEach {
  dependsOn "shadowJar"
  jvmArgs "-Ddatadog.smoketest.maven.jar.path=${tasks.shadowJar.archiveFile.get()}"

  // overriding the default timeout set in configure_tests.gradle, as Maven smoke
  // tests might run for a longer duration
  timeout = Duration.of(25, ChronoUnit.MINUTES)

  if (project.hasProperty("mavenRepositoryProxy")) {
    // propagate proxy URL to tests, to then propagate it to nested Gradle builds
    environment "MAVEN_REPOSITORY_PROXY", project.property("mavenRepositoryProxy")
  }
}
