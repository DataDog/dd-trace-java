import org.springframework.boot.gradle.tasks.bundling.BootJar

plugins {
  id 'java'
  id 'org.springframework.boot' version '2.7.15'
  id 'io.spring.dependency-management' version '1.0.15.RELEASE'
  id 'java-test-fixtures'
}

apply from: "$rootDir/gradle/java.gradle"

testJvmConstraints {
  minJavaVersion = JavaVersion.VERSION_11
}

apply from: "$rootDir/gradle/spring-boot-plugin.gradle"
description = 'SpringBoot Java 11 Smoke Tests.'

dependencies {
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.2.0.RELEASE'

  testImplementation project(':dd-smoke-tests')
  testImplementation testFixtures(project(":dd-smoke-tests:iast-util:iast-util-11"))
  testImplementation testFixtures(project(':dd-smoke-tests:iast-util'))

  implementation project(':dd-smoke-tests:iast-util:iast-util-11')
}

tasks.named("compileJava", JavaCompile) {
  configureCompiler(it, 11, JavaVersion.VERSION_11)
}

tasks.withType(Test).configureEach {
  dependsOn "bootJar"
  def bootJarTask = tasks.named('bootJar', BootJar)
  jvmArgumentProviders.add(new CommandLineArgumentProvider() {
      @Override
      Iterable<String> asArguments() {
        return bootJarTask.map { ["-Ddatadog.smoketest.springboot.shadowJar.path=${it.archiveFile.get()}"] }.get()
      }
    })
}
