plugins {
  id 'com.gradleup.shadow'
  id 'java-test-fixtures'
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import com.github.jengelman.gradle.plugins.shadow.transformers.PropertiesFileTransformer

apply from: "$rootDir/gradle/java.gradle"
description = 'Spring Boot 2.6 Webmvc Smoke Tests.'

// The standard spring-boot plugin doesn't play nice with our project
// so we'll build a fat jar instead
tasks.named("jar", Jar) {
  manifest {
    attributes('Main-Class': 'datadog.smoketest.springboot.SpringbootApplication')
  }
}

tasks.named("shadowJar", ShadowJar) {
  configurations = [project.configurations.runtimeClasspath]
  mergeServiceFiles()
  append 'META-INF/spring.handlers'
  append 'META-INF/spring.schemas'
  append 'META-INF/spring.tooling'
  transform(PropertiesFileTransformer) {
    paths = ['META-INF/spring.factories']
    mergeStrategy = "append"
  }
}

dependencies {
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.6.0+'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '2.6.0+'
  implementation group: 'com.h2database', name: 'h2', version: '2.1.214'
  implementation group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '2.3.3'
  implementation group: 'com.sun.xml.bind', name: 'jaxb-ri', version: '2.3.3'
  implementation group: 'com.google.code.gson', name: 'gson', version: '2.10'

  // ssrf
  implementation group: 'commons-httpclient', name: 'commons-httpclient', version: '2.0'
  implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.0'
  implementation group: 'com.squareup.okhttp', name: 'okhttp', version: '2.2.0'
  implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.0.0'
  implementation group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: '5.0'
  implementation group: 'org.apache.httpcomponents', name: 'httpasyncclient', version: '4.0'

  testImplementation project(':dd-smoke-tests')
  implementation project(':dd-smoke-tests:iast-util')
  testImplementation(testFixtures(project(":dd-smoke-tests:iast-util")))

  implementation 'jakarta.mail:jakarta.mail-api:2.0.1'
  implementation 'com.sun.mail:jakarta.mail:2.0.1'
  implementation 'jakarta.activation:jakarta.activation-api:2.1.3'
  // text sanitization
  implementation group: 'org.apache.commons', name: 'commons-text', version: '1.0'
}

tasks.withType(Test).configureEach {
  def shadowJarTask = tasks.named('shadowJar', ShadowJar)
  dependsOn shadowJarTask

  jvmArgumentProviders.add(new CommandLineArgumentProvider() {
      @Override
      Iterable<String> asArguments() {
        return shadowJarTask.map { ["-Ddatadog.smoketest.springboot.shadowJar.path=${it.archiveFile.get()}"] }.get()
      }
    })
}
