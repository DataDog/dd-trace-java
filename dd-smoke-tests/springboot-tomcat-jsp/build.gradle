import org.springframework.boot.gradle.tasks.bundling.BootWar

plugins {
  id 'java'
  id 'war'
  id 'org.springframework.boot' version '2.7.15'
  id 'io.spring.dependency-management' version '1.0.15.RELEASE'
  id 'java-test-fixtures'
}

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/spring-boot-plugin.gradle"
description = 'SpringBoot Tomcat JSP Smoke Tests.'

java {
  sourceCompatibility = '1.8'
}

sourceSets {
  main {
    resources.srcDir("src/main/webapp")
  }
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'

  runtimeOnly("javax.servlet:jstl")
  runtimeOnly("org.apache.tomcat.embed:tomcat-embed-jasper")

  providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")

  testImplementation project(':dd-smoke-tests')
  testImplementation(testFixtures(project(":dd-smoke-tests:iast-util")))

  // Some strange issue with spotbug classpath???
  spotbugs "com.github.spotbugs:spotbugs:4.9.8"
  spotbugs "org.apache.commons:commons-lang3:3.19.0"
}

tasks.withType(Test).configureEach {
  dependsOn "war", "bootWar"

  jvmArgumentProviders.add(new CommandLineArgumentProvider() {
      @Override
      Iterable<String> asArguments() {
        def bootWarTask = tasks.named('bootWar', BootWar).get()
        return ["-Ddatadog.smoketest.springboot.war.path=${bootWarTask.archiveFile.get().getAsFile()}"]
      }
    })
}
