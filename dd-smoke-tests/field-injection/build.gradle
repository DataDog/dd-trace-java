import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
  id 'com.gradleup.shadow'
}

description = 'Check fields get injected where expected'
apply from: "$rootDir/gradle/java.gradle"

tasks.named("jar", Jar) {
  manifest {
    attributes('Main-Class': 'datadog.smoketest.fieldinjection.FieldInjectionApp')
  }
}

tasks.named("shadowJar", ShadowJar) {
  configurations = [project.configurations.runtimeClasspath]
}

dependencies {
  testImplementation project(':dd-smoke-tests')
}

tasks.withType(Test).configureEach {
  dependsOn "shadowJar"

  def shadowJarTask = tasks.named('shadowJar', ShadowJar)
  jvmArgumentProviders.add(new CommandLineArgumentProvider() {
      @Override
      Iterable<String> asArguments() {
        return shadowJarTask.map { ["-Ddatadog.smoketest.fieldinjection.shadowJar.path=${it.archiveFile.get()}"] }.get()
      }
    })
}
