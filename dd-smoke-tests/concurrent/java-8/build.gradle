import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
  id 'application'
  id 'com.gradleup.shadow'
}

apply from: "$rootDir/gradle/java.gradle"

description = 'JDK 8 Concurrent Integration Tests'

application {
  mainClass = 'datadog.smoketest.concurrent.ConcurrentApp'
}

dependencies {
  implementation group: 'io.opentelemetry.instrumentation', name: 'opentelemetry-instrumentation-annotations', version: '2.13.3'
  testImplementation project(':dd-smoke-tests')
}


tasks.withType(Test).configureEach {
  def shadowJarTask = tasks.named('shadowJar', ShadowJar)
  dependsOn shadowJarTask
  jvmArgumentProviders.add(new CommandLineArgumentProvider() {
      @Override
      Iterable<String> asArguments() {
        return shadowJarTask.map { ["-Ddatadog.smoketest.shadowJar.path=${it.archiveFile.get()}"] }.get()
      }
    })
}
