import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
  id 'application'
  id 'com.gradleup.shadow'
}

apply from: "$rootDir/gradle/java.gradle"

testJvmConstraints {
  // This smoke test should be limited to Java 25 and above
  // But the groovy testing framework cannot run on Java 25
  // Using Java 8 for now and runs a JVM 25 when forking tests process.
  // Relying on forked JVM 25 is hardcoded in the test suites (see createProcessBuilder()).
  minJavaVersion = JavaVersion.VERSION_1_8
  maxJavaVersion = JavaVersion.VERSION_1_8
  // Only runs on Temurin build as it spawns a Temurin 25 for test process.
  excludeJdk = ['IBM8', 'SEMERU8']
}

description = 'JDK 25 Concurrent Integration Tests'

javaToolchains {
  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(25)
    }
  }
}

tasks.named('compileJava', JavaCompile) {
  configureCompiler(it, 25, JavaVersion.VERSION_25)
  options.compilerArgs.add("--enable-preview")
}

tasks.named('compileTestGroovy') {
  configureCompiler(it, 8, JavaVersion.VERSION_1_8)
}

application {
  mainClass = 'datadog.smoketest.concurrent.ConcurrentApp'
}

dependencies {
  implementation group: 'io.opentelemetry.instrumentation', name: 'opentelemetry-instrumentation-annotations', version: '2.19.0'
  testImplementation project(':dd-smoke-tests')
}

tasks.withType(Test).configureEach {
  def shadowJarTask = tasks.named('shadowJar', ShadowJar)
  dependsOn shadowJarTask
  environment("JAVA_HOME", getLazyJavaHomeFor(25))
  jvmArgumentProviders.add(new CommandLineArgumentProvider() {
      @Override
      Iterable<String> asArguments() {
        return shadowJarTask.map { ["-Ddatadog.smoketest.shadowJar.path=${it.archiveFile.get()}"] }.get()
      }
    })
}
