plugins {
  id 'java'
  id 'io.quarkus'
  id 'com.diffplug.gradle.spotless' version '4.3.0'
  id 'pl.allegro.tech.build.axion-release' version '1.10.3'
}

def sharedRootDir = "$rootDir/../../../"
def sharedConfigDirectory = "$sharedRootDir/gradle"
rootProject.ext.sharedConfigDirectory = sharedConfigDirectory

apply from: "$sharedConfigDirectory/repositories.gradle"
apply from: "$sharedConfigDirectory/spotless.gradle"

if (hasProperty('appBuildDir')) {
  buildDir = property('appBuildDir')
}

scmVersion {
  repository {
    directory = project.rootProject.file(sharedRootDir)
  }
}
apply from: "$sharedConfigDirectory/scm.gradle"

version = scmVersion.version

dependencies {
  implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
  implementation 'io.quarkus:quarkus-resteasy'
  if (hasProperty('apiJar')) {
    implementation files(property('apiJar'))
  } else {
    implementation "com.datadoghq:dd-trace-api:0.68.0"
  }
}

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

quarkusBuild {
  uberJar = true
}
