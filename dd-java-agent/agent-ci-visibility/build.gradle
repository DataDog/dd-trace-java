plugins {
  id 'com.github.johnrengelman.shadow'
  id 'java-test-fixtures'
  id 'org.jetbrains.kotlin.jvm' version '1.9.24'
  id 'scala'
}

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/version.gradle"

minimumBranchCoverage = 0.0
minimumInstructionCoverage = 0.0

dependencies {
  api libs.slf4j

  implementation libs.bundles.asm
  implementation group: 'org.jacoco', name: 'org.jacoco.core', version: '0.8.9'
  implementation group: 'org.jacoco', name: 'org.jacoco.report', version: '0.8.9'

  implementation project(':communication')
  implementation project(':internal-api')
  implementation project(':internal-api:internal-api-9')

  testImplementation project(":utils:test-utils")
  testImplementation("com.google.jimfs:jimfs:1.1") // an in-memory file system for testing code that works with files

  testImplementation libs.scala
  testImplementation libs.kotlin

  testFixturesApi project(':dd-java-agent:testing')
  testFixturesApi project(':utils:test-utils')

  testFixturesApi group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.1'
  testFixturesApi group: 'org.freemarker', name: 'freemarker', version: '2.3.30'
  testFixturesApi group: 'com.jayway.jsonpath', name: 'json-path', version: '2.8.0'
  testFixturesApi group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.16.0'
  testFixturesApi group: 'org.msgpack', name: 'jackson-dataformat-msgpack', version: '0.9.6'
}

compileTestGroovy {
  dependsOn compileTestJava
  dependsOn compileTestScala
  dependsOn compileTestKotlin
  classpath += files(sourceSets.test.java.destinationDirectory)
  classpath += files(sourceSets.test.kotlin.destinationDirectory)
  classpath += files(sourceSets.test.scala.destinationDirectory)
}

shadowJar {
  dependencies deps.excludeShared
}

jar {
  archiveClassifier = 'unbundled'
}
