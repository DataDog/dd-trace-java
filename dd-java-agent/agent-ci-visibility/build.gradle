import java.time.Duration
import java.time.temporal.ChronoUnit

buildscript {
  repositories {
    mavenLocal()
    if (project.rootProject.hasProperty("gradlePluginProxy")) {
      maven {
        url project.rootProject.property("gradlePluginProxy")
        allowInsecureProtocol true
      }
    }
    if (project.rootProject.hasProperty("mavenRepositoryProxy")) {
      maven {
        url project.rootProject.property("mavenRepositoryProxy")
        allowInsecureProtocol true
      }
    }
    gradlePluginPortal()
    mavenCentral()
  }

  dependencies {
    classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: libs.versions.kotlin.get()
  }
}

plugins {
  id 'com.gradleup.shadow'
  id 'java-test-fixtures'
}

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/version.gradle"
apply from: "$rootDir/gradle/test-with-kotlin.gradle"
apply from: "$rootDir/gradle/test-with-scala.gradle"

minimumBranchCoverage = 0.0
minimumInstructionCoverage = 0.0

dependencies {
  api libs.slf4j

  implementation libs.bundles.asm
  implementation group: 'org.jacoco', name: 'org.jacoco.core', version: '0.8.12'
  implementation group: 'org.jacoco', name: 'org.jacoco.report', version: '0.8.12'

  implementation project(':communication')
  implementation project(':components:json')
  implementation project(':internal-api')
  implementation project(':internal-api:internal-api-9')

  testImplementation project(":utils:test-utils")
  testImplementation("com.google.jimfs:jimfs:1.1") // an in-memory file system for testing code that works with files

  testImplementation libs.scala
  testImplementation libs.kotlin

  testFixturesApi project(':dd-java-agent:instrumentation-testing')
  testFixturesApi project(':utils:test-utils')

  testFixturesApi group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.1'
  testFixturesApi group: 'org.freemarker', name: 'freemarker', version: '2.3.31'
  testFixturesApi group: 'com.jayway.jsonpath', name: 'json-path', version: '2.8.0'
  testFixturesApi group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.16.0'
  testFixturesApi group: 'org.msgpack', name: 'jackson-dataformat-msgpack', version: '0.9.6'
  testFixturesApi group: 'org.xmlunit', name: 'xmlunit-core', version: '2.10.3'

  testRuntimeOnly("org.junit.platform:junit-platform-launcher:1.9.2") // Required to update dependency lock files
}

shadowJar {
  dependencies deps.excludeShared
}

jar {
  archiveClassifier = 'unbundled'
}

tasks.withType(Test).configureEach {
  timeout = Duration.of(12, ChronoUnit.MINUTES)
}
