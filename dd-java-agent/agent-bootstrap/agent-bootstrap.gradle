// The shadowJar of this project will be injected into the JVM's bootstrap classloader
plugins {
  id "com.github.johnrengelman.shadow"
}

ext {
  // need access to sun.* packages
  skipSettingCompilerRelease = true
}

apply from: "$rootDir/gradle/java.gradle"
apply plugin: "idea"

// FIXME: Improve test coverage.
minimumBranchCoverage = 0.0
minimumInstructionCoverage = 0.0

dependencies {
  compile project(':dd-java-agent:agent-logging')
  compile project(':dd-trace-api')
  compile project(':internal-api')
  implementation project(':internal-api:internal-api-8')
  compile deps.slf4j
  // ^ Generally a bad idea for libraries, but we're shadowing.

  testCompile project(':dd-java-agent:testing')
}

// Must use Java 11 to build JFR enabled code - there is no JFR in OpenJDK 8 (revisit once JFR in Java 8 is available)
sourceSets {
  "main_java11" {
    java.srcDirs "${project.projectDir}/src/main/java11"
  }
}
compileMain_java11Java.doFirst {
  if (!System.env.JAVA_11_HOME) {
    throw new GradleException('JAVA_11_HOME must be set to build profiling helpers')
  }
  options.fork = true
  options.forkOptions.javaHome = file(System.env.JAVA_11_HOME)
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}
dependencies {
  main_java11CompileOnly project(':internal-api:internal-api-8')
}
jar {
  from sourceSets.main_java11.output
}
idea {
  module {
    jdkName = '11'
  }
}
