import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.apache.maven.model.License

plugins {
  id 'com.gradleup.shadow'
}

apply from: "$rootDir/gradle/java.gradle"
// We do not publish a separate jar, but having a version file is useful for checking if cws is included
apply from: "$rootDir/gradle/version.gradle"

excludedClassesCoverage += ['datadog.cws.erpc.*', 'datadog.cws.tls.*',]

dependencies {
  implementation group: 'net.java.dev.jna', name: 'jna', version: '5.8.0'
  implementation group: 'net.java.dev.jna', name: 'jna-platform', version: '5.8.0'

  implementation project(':internal-api')
  implementation project(':dd-trace-api')

  testImplementation project(":dd-java-agent:testing")
}

tasks.named("shadowJar", ShadowJar) {
  dependencies deps.excludeShared

  // exclude 'jna' this since it's available in the instrumentation jar
  dependencies {
    exclude(dependency("net.java.dev.jna:jna"))
    exclude(dependency("net.java.dev.jna:jna-platform"))
  }
}
