apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'idea'

tracerJava {
  addSourceSetFor(JavaVersion.VERSION_11) {
    // By default tests with be compiled for `minJavaVersion` version,
    // but in this case we would like to avoid this since we would like to run with ZULU8
    applyForTestSources = false
  }
}

testJvmConstraint {
  minJavaVersion = JavaVersion.VERSION_11
  // Zulu has backported profiling support
  forceJdk = ['ZULU8']
  excludeJdk = ['SEMERU11', 'SEMERU17']
}

dependencies {
  api libs.slf4j
  api project(':internal-api')
  api project(':utils:version-utils')
  api(project(':dd-java-agent:agent-bootstrap')) {
    exclude group: 'com.datadoghq', module: 'agent-logging'
  }
  api project(':dd-java-agent:agent-profiling:profiling-controller')
  api project(':dd-java-agent:agent-profiling:profiling-controller-jfr')

  testImplementation libs.bundles.junit5
  testImplementation libs.bundles.mockito
  testImplementation files(project(':dd-java-agent:agent-profiling:profiling-controller-jfr').sourceSets.test.output)
  testImplementation project(':dd-java-agent:agent-profiling')
}

/*
 Setup here is as following:
 * We compile with Java11 compiler to get JFR definitions.
 * We specify source/target as Java8 to get code that is loadable on Java8 - JFR defs are Java8 compatible.
 * We force IDEA to treat this as Java11 project with 'idea' plugin below.
 */
[JavaCompile, GroovyCompile].each {
  tasks.withType(it).configureEach {
    configureCompiler(it, 11, JavaVersion.VERSION_1_8, "Need access to jdk.jfr package")
    // Disable '-processing' because some annotations are not claimed.
    // Disable '-options' because we are compiling for java8 without specifying bootstrap - intentionally.
    // Disable '-path' because we do not have some of the paths seem to be missing.
    options.compilerArgs.addAll(['-Xlint:all,-processing,-options,-path'/*, '-Werror'*/])
  }
}

tasks.named("forbiddenApisMain") {
  failOnMissingClasses = false
}

idea {
  module {
    jdkName = '11'
  }
}
