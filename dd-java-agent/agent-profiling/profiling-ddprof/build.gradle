plugins {
  id 'com.gradleup.shadow'
}

ext {
}

apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'idea'

def osName = System.getProperty('os.name', '').toLowerCase()
// currently, only linux binaries are included
minimumInstructionCoverage = osName.contains("linux") ? 0.5 : 0
minimumBranchCoverage = osName.contains("linux") ? 0.4 : 0

excludedClassesCoverage += [
  // enums with no additional functionality
  'com.datadog.profiling.controller.ddprof.Arch',
  'com.datadog.profiling.controller.ddprof.OperatingSystem',
  'com.datadog.profiling.ddprof.DatadogProfilingIntegration',
  'com.datadog.profiling.ddprof.DatadogProfilerConfig',
  // --
  'com.datadog.profiling.ddprof.DatadogProfiler.Singleton',
  // although it is quite well covered jacoco complains about branch coverage due to exception handlers
  'com.datadog.profiling.ddprof.DatadogProfilerRecording',
  'com.datadog.profiling.ddprof.DatadogProfilerRecordingData',
]

dependencies {
  api project(':dd-java-agent:agent-profiling:profiling-controller')
  api project(':dd-java-agent:agent-profiling:profiling-utils')
  api project(path: ':dd-java-agent:ddprof-lib', configuration: 'shadow')

  annotationProcessor libs.autoservice.processor
  compileOnly libs.autoservice.annotation

  implementation libs.slf4j

  // Antithesis SDK for assertions and property testing - bundled in tracer JAR
  implementation group: 'com.antithesis', name: 'sdk', version: '1.4.5'

  testImplementation libs.bundles.jmc
  testImplementation libs.bundles.junit5
}


configurations.configureEach {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

[JavaCompile, GroovyCompile].each {
  tasks.withType(it).configureEach {
    configureCompiler(it, 11, JavaVersion.VERSION_1_8)
    // Disable '-processing' because some annotations are not claimed.
    // Disable '-options' because we are compiling for java8 without specifying bootstrap - intentionally.
    // Disable '-path' because we do not have some of the paths seem to be missing.
    options.compilerArgs.addAll(['-Xlint:all,-processing,-options,-path'/*, '-Werror'*/])
  }
}

idea {
  module {
    jdkName = '11'
  }
}
