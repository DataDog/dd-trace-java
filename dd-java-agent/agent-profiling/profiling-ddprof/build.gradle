plugins {
  id "com.github.johnrengelman.shadow"
}

ext {
}

apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'idea'

def osName = System.getProperty('os.name', '').toLowerCase()
// currently, only linux binaries are included
minimumInstructionCoverage = osName.contains("linux") ? 0.5 : 0
minimumBranchCoverage = osName.contains("linux") ? 0.4 : 0

excludedClassesCoverage += [
  // enums with no additional functionality
  'com.datadog.profiling.controller.ddprof.Arch',
  'com.datadog.profiling.controller.ddprof.OperatingSystem',
  'com.datadog.profiling.ddprof.DatadogProfilingIntegration',
  'com.datadog.profiling.ddprof.DatadogProfilerConfig',
  // --
  'com.datadog.profiling.ddprof.DatadogProfiler.Singleton',
  // although it is quite well covered jacoco complains about branch coverage due to exception handlers
  'com.datadog.profiling.ddprof.DatadogProfilerRecording',
  'com.datadog.profiling.ddprof.DatadogProfilerRecordingData',
]

dependencies {
  api project(':dd-java-agent:agent-profiling:profiling-controller')
  api project(':dd-java-agent:agent-profiling:profiling-utils')
  implementation project.hasProperty('ddprof.jar') ? files(project.getProperty('ddprof.jar')) : deps.ddprof

  annotationProcessor deps.autoserviceProcessor
  compileOnly deps.autoserviceAnnotation

  implementation deps.slf4j

  testImplementation deps.jmc
  testImplementation deps.junit5
}

shadowJar {
  archiveClassifier = ''
  include {
    def rslt = false
    rslt |= it.path == "com" || it.path == "com/datadog"
    || it.path.startsWith("com/datadog/") || it.path == "com/datadoghq" || it.path == "com/datadoghq/profiler"
    || it.path.startsWith("com/datadoghq/profiler")
    rslt |= it.path == "META-INF" || it.path == "META-INF/services" || it.path.startsWith("META-INF/services/") || it.path.startsWith("META-INF/native-libs/")
    rslt |= (it.path.contains("ddprof") && it.path.endsWith(".jar"))
    return rslt
  }
}

build.dependsOn shadowJar

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

[JavaCompile, GroovyCompile].each {
  tasks.withType(it).configureEach {
    setJavaVersion(it, 11)
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    // Disable '-processing' because some annotations are not claimed.
    // Disable '-options' because we are compiling for java8 without specifying bootstrap - intentionally.
    // Disable '-path' because we do not have some of the paths seem to be missing.
    options.compilerArgs.addAll(['-Xlint:all,-processing,-options,-path'/*, '-Werror'*/])
  }
}

forbiddenApisMain {
  failOnMissingClasses = false
}

idea {
  module {
    jdkName = '11'
  }
}
