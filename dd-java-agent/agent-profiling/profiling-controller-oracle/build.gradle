// Set properties before any plugins get loaded
ext {
  // By default tests with be compiled for `minJavaVersionForTests` version,
  // but in this case we would like to avoid this since we would like to run with ORACLE8
  skipSettingTestJavaVersion = true
}

apply from: "$rootDir/gradle/java.gradle"

minimumBranchCoverage = 0.5
minimumInstructionCoverage = 0.7

excludedClassesCoverage += ['com.datadog.profiling.controller.oracle.JdkTypeIDs_8']

dependencies {
  api libs.slf4j
  api project(':internal-api')
  api project(':dd-java-agent:agent-profiling:profiling-controller')
  api project(':dd-java-agent:agent-profiling:profiling-controller-jfr')

  testImplementation libs.bundles.junit5
  testImplementation libs.bundles.mockito
  testImplementation files(project(':dd-java-agent:agent-profiling:profiling-controller-jfr').sourceSets.test.output)
  testImplementation project(':dd-java-agent:agent-profiling')
}

// Oracle JDK requires extra JVM arguments to enable JFR
tasks.withType(Test).configureEach {
  testJvmConstraint {
    includeJdk = ['ORACLE8']
  }

  jvmArgs += [
    "-XX:+IgnoreUnrecognizedVMOptions",
    "-XX:+UnlockCommercialFeatures",
    "-XX:+FlightRecorder"
  ]
}
