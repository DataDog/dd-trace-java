import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
  id 'com.gradleup.shadow'
}

apply from: "$rootDir/gradle/java.gradle"

excludedClassesCoverage += [
  'com.datadog.profiling.agent.CompositeController',
  'com.datadog.profiling.agent.CompositeController.CompositeRecordingData',
  'com.datadog.profiling.agent.CompositeController.CompositeOngoingRecording',
  'com.datadog.profiling.agent.ProfilingAgent',
  'com.datadog.profiling.agent.ProfilingAgent.ShutdownHook',
  'com.datadog.profiling.agent.ProfilingAgent.DataDumper',
  'com.datadog.profiling.agent.ProfilerFlare',
  'com.datadog.profiling.agent.ScrubRecordingDataListener',
  'com.datadog.profiling.agent.ScrubRecordingDataListener.ScrubbedRecordingData'
]

dependencies {
  api libs.slf4j
  api project(':internal-api')

  api project(':dd-java-agent:agent-profiling:profiling-ddprof')
  api project(':dd-java-agent:agent-profiling:profiling-uploader')
  api project(':dd-java-agent:agent-profiling:profiling-controller')
  implementation project(':dd-java-agent:agent-profiling:profiling-scrubber')
  api project(':dd-java-agent:agent-profiling:profiling-controller-jfr')
  api project(':dd-java-agent:agent-profiling:profiling-controller-jfr:implementation')
  api project(':dd-java-agent:agent-profiling:profiling-controller-ddprof')
  api project(':dd-java-agent:agent-profiling:profiling-controller-openjdk')
  api project(':dd-java-agent:agent-profiling:profiling-controller-oracle')

  testImplementation libs.bundles.junit5
  testImplementation libs.bundles.mockito
}

configurations {
  // exclude bootstrap dependencies from shadowJar
  runtime {
    exclude(group: 'org.slf4j', module: 'slf4j-api')
  }
}

tasks.named("shadowJar", ShadowJar) {
  dependencies deps.excludeShared

  // Exclude multi-release versioned classes from jafar-parser.
  // These are duplicates of base classes for newer Java APIs and confuse
  // the GraalVM native-image builder when the profiling jar is embedded in the agent.
  exclude 'META-INF/versions/**'
}

tasks.named("jar", Jar) {
  archiveClassifier = 'unbundled'
}
