plugins {
  id "com.github.johnrengelman.shadow"
}

// Set properties before any plugins get loaded
ext {
  enableJunitPlatform = true
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

apply from: "$rootDir/gradle/java.gradle"
// We do not publish separate jar, but having version file is useful
apply from: "$rootDir/gradle/version.gradle"
apply plugin: 'minimize'

dependencies {
  compile deps.slf4j
  compile project(':internal-api')

  compile project(':dd-java-agent:agent-profiling:profiling-uploader')
  compile project(':dd-java-agent:agent-profiling:profiling-controller')
  compile project(':dd-java-agent:agent-profiling:profiling-controller-openjdk')
  compile project(':dd-java-agent:agent-profiling:profiling-controller-oracle')
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

configurations {
  // exclude bootstrap dependencies from shadowJar
  runtime.exclude module: deps.slf4j
  runtime.exclude group: 'org.slf4j'
}

shadowJar {
  dependencies deps.sharedInverse
  dependencies {
    exclude(project(':dd-java-agent:agent-bootstrap'))
    exclude(project(':dd-java-agent:agent-logging'))
    exclude(project(':dd-trace-api'))
    exclude(project(':internal-api'))
    exclude(dependency('org.slf4j::'))
  }
}
minimize {
  additionalInclusions = [
    // https://github.com/lz4/lz4-java/blob/55bede61a72803574c638a1ef1a70eadc6cefa6e/src/java/net/jpountz/lz4/LZ4Factory.java#L193-L196
    ~'^net/jpountz/lz4/LZ4',
    // https://github.com/lz4/lz4-java/blob/55bede61a72803574c638a1ef1a70eadc6cefa6e/src/java/net/jpountz/xxhash/XXHashFactory.java#L179-L182
    ~'^net/jpountz/xxhash/(?:Streaming)?XXHash\\d{2}'
  ]
}

jar {
  classifier = 'unbundled'
}
