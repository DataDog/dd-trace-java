apply from: "$rootDir/gradle/java.gradle"
apply plugin: "idea"

minimumBranchCoverage = 0.6
excludedClassesCoverage += ['datadog.trace.agent.tooling.*']

dependencies {
  api project(':dd-java-agent:agent-tooling')

  implementation project(':dd-java-agent:agent-otel:otel-tooling')

  testImplementation project(':dd-java-agent:testing')
}

// Use Java 11 to build a delegating ClassFileTransformer that understands Java modules
sourceSets {
  "main_java11" {
    java.srcDirs "${project.projectDir}/src/main/java11"
  }
}

tasks.named("compileMain_java11Java", JavaCompile).configure {
  setJavaVersion(it, 11)
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
  main_java11CompileOnly project(':dd-java-agent:agent-tooling')
  main_java11CompileOnly sourceSets.main.output
  runtimeOnly sourceSets.main_java11.output
}

tasks.named("jar", Jar).configure {
  from sourceSets.main_java11.output
}

tasks.named("forbiddenApisMain_java11").configure {
  failOnMissingClasses = false
}

idea {
  module {
    jdkName = '11'
  }
}
