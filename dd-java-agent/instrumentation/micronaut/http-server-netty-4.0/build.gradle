ext {
  minJavaVersionForTests = JavaVersion.VERSION_17
}
muzzle {
  pass {
    group = "io.micronaut"
    module = "micronaut-http-server-netty"
    versions = "[4,)"
    assertInverse = true
  }
}

apply from: "$rootDir/gradle/java.gradle"

addTestSuiteForDir('latestDepTest', 'test')

["compileMain_java17Java", "compileTestJava", "compileLatestDepTestJava"].each {
  tasks.named(it, JavaCompile) {
    configureCompiler(it, 17, JavaVersion.VERSION_1_8)
  }
}

["compileTestGroovy", "compileLatestDepTestGroovy"].each {
  tasks.named(it, GroovyCompile) {
    configureCompiler(it, 17)
  }
}

dependencies {
  main_java17CompileOnly group: 'io.micronaut', name: 'micronaut-http-server-netty', version: '4.0.0'
  implementation project(':dd-java-agent:instrumentation:micronaut')

  // Added to ensure cross compatibility:
  testImplementation project(':dd-java-agent:instrumentation:micronaut:http-server-netty-2.0')
  testImplementation project(':dd-java-agent:instrumentation:micronaut:http-server-netty-3.0')
  testImplementation project(':dd-java-agent:instrumentation:netty:netty-4.1')
  testImplementation project(':dd-java-agent:agent-debugger')
  testImplementation group: 'io.micronaut', name: 'micronaut-http-server-netty', version: '4.0.0', {
    exclude group: 'org.slf4j', module: 'slf4j-api'
    exclude group: 'ch.qos.logback', module: 'logback-classic'
  }
  testImplementation group: 'io.projectreactor', name: 'reactor-core', version: '3.5.7'
  testImplementation group: 'io.micronaut', name: 'micronaut-jackson-databind', version: '4.0.0', {
    exclude group: 'org.slf4j', module: 'slf4j-api'
    exclude group: 'ch.qos.logback', module: 'logback-classic'
  }
  testAnnotationProcessor "io.micronaut:micronaut-inject-java:4.0.0"

  latestDepTestImplementation group: 'io.micronaut', name: 'micronaut-http-server-netty', version: '4.+', {
    exclude group: 'org.slf4j', module: 'slf4j-api'
    exclude group: 'ch.qos.logback', module: 'logback-classic'
  }
  latestDepTestImplementation group: 'io.micronaut', name: 'micronaut-jackson-databind', version: '4.+', {
    exclude group: 'org.slf4j', module: 'slf4j-api'
    exclude group: 'ch.qos.logback', module: 'logback-classic'
  }
}
