ext {
  latestDepForkedTestMinJavaVersionForTests = JavaVersion.VERSION_17
  latestDepTestMinJavaVersionForTests = JavaVersion.VERSION_17
  spring6TestMinJavaVersionForTests = JavaVersion.VERSION_17
}

muzzle {
  pass {
    group = 'org.springframework'
    module = 'spring-context'
    versions = "[3.1.0.RELEASE,6)"
    // assertInverse = true
  }
  pass {
    group = 'org.springframework'
    module = 'spring-context'
    versions = "[6,)"
    javaVersion = "17"
    // assertInverse = true
  }
}

apply from: "$rootDir/gradle/java.gradle"

addTestSuite('latestDepTest')
addTestSuiteExtendingForDir('latestDepForkedTest','latestDepTest', 'latestDepTest')
addTestSuiteForDir('spring6Test', 'test')
addTestSuiteForDir('latestSpring5Test', 'test')

[compileSpring6TestJava, compileLatestDepTestJava, compileLatestDepForkedTestJava].each {
  setJavaVersion(it, 17)
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}


[
  compileSpring6TestGroovy,
  compileLatestDepTestGroovy,
  compileLatestDepForkedTestGroovy,
  spring6Test,
  latestDepTest,
  latestDepForkedTest
].each {
  it.javaLauncher = getJavaLauncherFor(17)
}

[spring6Test, latestDepTest, latestDepForkedTest].each {
  it.jvmArgs '--add-opens', 'java.base/java.util=ALL-UNNAMED'
}

dependencies {
  // choose a recent version so that we can test both lambdas (JDK8)
  // @Async requires proxying and older versions can't read classfile versions > 51
  // we muzzle older versions of spring anyway
  compileOnly group: 'org.springframework', name: 'spring-context', version: '5.0.0.RELEASE'

  testImplementation group: 'org.springframework', name: 'spring-context', version: '5.0.0.RELEASE'
  testImplementation project(':dd-java-agent:instrumentation:trace-annotation')

  testImplementation group: 'net.javacrumbs.shedlock', name: 'shedlock-spring', version: '4.21.0'
  testImplementation group: 'net.javacrumbs.shedlock', name: 'shedlock-provider-jdbc-template', version: '4.21.0'
  testImplementation group: 'com.h2database', name: 'h2', version: '1.4.199'
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '2.4.0'
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: '2.4.0'


  latestSpring5TestImplementation group: 'org.springframework', name: 'spring-context', version: '5.+'

  latestSpring5TestImplementation group: 'net.javacrumbs.shedlock', name: 'shedlock-spring', version: '4.+'
  latestSpring5TestImplementation group: 'net.javacrumbs.shedlock', name: 'shedlock-provider-jdbc-template', version: '4.+'
  latestSpring5TestImplementation group: 'com.h2database', name: 'h2', version: '2.2.+' // 2.3+ requires Java 11
  latestSpring5TestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '2.+'

  spring6TestImplementation group: 'org.springframework', name: 'spring-context', version: '6.0.0.RELEASE'
  spring6TestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '3.0.0'

  latestDepTestImplementation group: 'com.h2database', name: 'h2', version: '+'
  latestDepTestImplementation group: 'org.springframework', name: 'spring-context', version: '6.+'
  latestDepTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '3.+'
}
