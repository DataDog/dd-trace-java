ext {
  minJavaVersionForTests = JavaVersion.VERSION_11
  // Zulu has backported profiling support
  forceJdk = ['ZULU8']
  // By default tests with be compiled for `minJavaVersionForTests` version,
  // but in this case we would like to avoid this since we would like to run with ZULU8
  skipSettingTestJavaVersion = true
}

apply from: "$rootDir/gradle/java.gradle"
apply plugin: "idea"

dependencies {
  testImplementation libs.bundles.junit5
  testImplementation libs.bundles.jmc
  testImplementation libs.commons.math
  testImplementation libs.bundles.mockito
}

// Must use Java 11 to build JFR enabled code - there is no JFR in OpenJDK 8 (revisit once JFR in Java 8 is available)
for (final taskName in ['compileMain_java11Java', 'compileTestJava']) {
  tasks.named(taskName, JavaCompile).configure {
    setJavaVersion(it, 11)
    it.sourceCompatibility = JavaVersion.VERSION_1_8
    it.targetCompatibility = JavaVersion.VERSION_1_8
    it.options.compilerArgs.addAll(['-Xlint:all,-processing,-options,-path'])
  }
}

tasks.named("forbiddenApisMain_java11").configure {
  failOnMissingClasses = false
}

idea {
  module {
    jdkName = '11'
  }
}
