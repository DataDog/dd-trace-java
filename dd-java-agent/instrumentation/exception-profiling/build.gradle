apply from: "$rootDir/gradle/java.gradle"
apply plugin: "idea"

tracerJava {
  addSourceSetFor(JavaVersion.VERSION_11) {
    // By default tests with be compiled for `minJavaVersion` version,
    // but in this case we would like to avoid this since we would like to run with ZULU8
    applyForTestSources = false
  }
}

testJvmConstraint {
  minJavaVersion = JavaVersion.VERSION_11
  // Zulu has backported profiling support
  forceJdk = ['ZULU8']
}

dependencies {
  testImplementation 'de.thetaphi:forbiddenapis:3.8'
  testImplementation libs.bundles.junit5
  testImplementation libs.bundles.jmc
  testImplementation libs.commons.math
  testImplementation libs.bundles.mockito
}

// Must use Java 11 to build JFR enabled code - there is no JFR in OpenJDK 8 (revisit once JFR in Java 8 is available)
["compileMain_java11Java", "compileTestJava"].each { name ->
  tasks.named(name, JavaCompile) {
    configureCompiler(it, 11, JavaVersion.VERSION_1_8, "Must use Java 11 to build JFR enabled code - there is no JFR in OpenJDK 8 (revisit once JFR in Java 8 is available)")
    it.options.compilerArgs.addAll(['-Xlint:all,-processing,-options,-path'])
  }
}

tasks.named("forbiddenApisMain_java11") {
  failOnMissingClasses = false
}

tasks.named("test", Test) {
  useJUnitPlatform()
}

idea {
  module {
    jdkName = '11'
  }
}
