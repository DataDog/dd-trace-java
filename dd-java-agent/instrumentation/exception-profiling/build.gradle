ext {
  minJavaVersionForTests = JavaVersion.VERSION_11
  // Zulu has backported profiling support
  forceJdk = ['ZULU8']
  // By default tests with be compiled for `minJavaVersionForTests` version,
  // but in this case we would like to avoid this since we would like to run with ZULU8
  skipSettingTestJavaVersion = true
}

apply from: "$rootDir/gradle/java.gradle"
apply plugin: "idea"

dependencies {
  testImplementation deps.junit5
  testImplementation deps.jmc
  testImplementation deps.commonsMath
  testImplementation deps.mockito
}

// Must use Java 11 to build JFR enabled code - there is no JFR in OpenJDK 8 (revisit once JFR in Java 8 is available)
[compileMain_java11Java, compileTestJava].each {
  setJavaVersion(it, 11)
  it.sourceCompatibility = JavaVersion.VERSION_1_8
  it.targetCompatibility = JavaVersion.VERSION_1_8
  it.options.compilerArgs.addAll(['-Xlint:all,-processing,-options,-path'])
}

forbiddenApisMain_java11 {
  failOnMissingClasses = false
}

idea {
  module {
    jdkName = '11'
  }
}
