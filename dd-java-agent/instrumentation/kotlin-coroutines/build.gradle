plugins {
  id 'java-test-fixtures'
}

muzzle {
  pass {
    group = 'org.jetbrains.kotlin'
    module = 'kotlin-stdlib'
    versions = "[1.3.0,)"
    extraDependency "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0"
  }
  pass {
    group = 'org.jetbrains.kotlinx'
    module = 'kotlinx-coroutines-core'
    versions = "[1.3.0,)"
  }
  pass {
    group = 'org.jetbrains.kotlinx'
    module = 'kotlinx-coroutines-core-jvm'
    versions = "[1.5.0,)"
  }
}

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/test-with-kotlin.gradle"

addTestSuite('latestDepTest')

tasks.named("compileTestFixturesGroovy").configure {
  classpath += files(compileTestFixturesKotlin.destinationDirectory)
}

tasks.named("compileTestGroovy").configure {
  classpath += files(compileTestKotlin.destinationDirectory)
}

tasks.named("compileLatestDepTestGroovy").configure {
  classpath += files(compileLatestDepTestKotlin.destinationDirectory)
}

dependencies {
  api project(':dd-java-agent:instrumentation:java-concurrent')
  compileOnly libs.kotlin
  compileOnly libs.coroutines

  testFixturesImplementation libs.kotlin
  testFixturesImplementation libs.coroutines
  testFixturesApi project(':dd-trace-api')
  testFixturesApi project(':dd-java-agent:instrumentation:trace-annotation')
  testFixturesApi project(':dd-java-agent:testing')
  testFixturesApi 'com.github.spotbugs:spotbugs-annotations:4.2.0'

  testImplementation libs.kotlin
  testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0"
  testImplementation testFixtures(project(':dd-java-agent:instrumentation:kotlin-coroutines'))

  latestDepTestImplementation libs.kotlin
  latestDepTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:1.6.+"
  latestDepTestImplementation testFixtures(project(':dd-java-agent:instrumentation:kotlin-coroutines'))
}
