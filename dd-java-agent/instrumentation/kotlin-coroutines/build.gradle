import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion

plugins {
  id 'java-test-fixtures'
  id 'org.jetbrains.kotlin.jvm'
}

muzzle {
  pass {
    group = 'org.jetbrains.kotlin'
    module = 'kotlin-stdlib'
    versions = "[1.3.0,)"
    extraDependency "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0"
  }
  pass {
    group = 'org.jetbrains.kotlinx'
    module = 'kotlinx-coroutines-core'
    versions = "[1.3.0,)"
  }
  pass {
    group = 'org.jetbrains.kotlinx'
    module = 'kotlinx-coroutines-core-jvm'
    versions = "[1.5.0,)"
  }
}

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/test-with-kotlin.gradle"

kotlin {
  compilerOptions {
    jvmTarget = JvmTarget.JVM_1_8
    apiVersion = KotlinVersion.KOTLIN_1_6
    languageVersion = KotlinVersion.KOTLIN_1_6
  }
}

addTestSuite('latestDepTest')

tasks.named("compileTestFixturesGroovy", GroovyCompile) {
  classpath += files(tasks.named('compileTestFixturesKotlin').map { it.destinationDirectory })
}

tasks.named("compileTestGroovy", GroovyCompile) {
  classpath += files(tasks.named('compileTestKotlin').map { it.destinationDirectory })
}

tasks.named("compileLatestDepTestGroovy", GroovyCompile) {
  classpath += files(tasks.named('compileLatestDepTestKotlin').map { it.destinationDirectory })
}

dependencies {
  api project(':dd-java-agent:instrumentation:java:java-concurrent:java-concurrent-1.8')
  compileOnly libs.kotlin
  compileOnly libs.coroutines

  testFixturesImplementation libs.kotlin
  testFixturesImplementation libs.coroutines
  testFixturesApi project(':dd-trace-api')
  testFixturesApi project(':dd-java-agent:instrumentation:trace-annotation')
  testFixturesApi project(':dd-java-agent:instrumentation-testing')
  testFixturesApi 'com.github.spotbugs:spotbugs-annotations:4.2.0'

  testImplementation libs.kotlin
  testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0"
  testImplementation testFixtures(project(':dd-java-agent:instrumentation:kotlin-coroutines'))

  latestDepTestImplementation libs.kotlin
  latestDepTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:1.6.+"
  latestDepTestImplementation testFixtures(project(':dd-java-agent:instrumentation:kotlin-coroutines'))
}
