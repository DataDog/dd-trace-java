apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'scala'

tracerJava {
  addSourceSetFor(JavaVersion.VERSION_11)
}

muzzle {
  pass {
    group = 'com.typesafe.akka'
    module = 'akka-http_2.13'
    versions = "[10.6.0,)"
    javaVersion = "11"
    extraDependency 'com.typesafe.akka:akka-stream_2.13:2.9.0'

    if (project.rootProject.hasProperty("akkaRepositoryToken")) {
      extraRepository('akka', "https://repo.akka.io/${project.rootProject.property("akkaRepositoryToken")}/secure")
    } else {
      extraRepository('akka', 'https://repo.akka.io/maven')
    }

    assertInverse = true
  }
}

repositories {
  if (project.rootProject.hasProperty("akkaRepositoryToken")) {
    maven {
      url = "https://repo.akka.io/${project.rootProject.property("akkaRepositoryToken")}/secure"
    }
  } else {
    maven {
      url = "https://repo.akka.io/maven"
    }
  }
}

addTestSuiteForDir('latestDepTest', 'test')

["compileMain_java11Java", "compileTestScala", "compileLatestDepTestScala"].each {
  tasks.named(it, AbstractCompile) {
    configureCompiler(it, 11, JavaVersion.VERSION_1_8)
  }
}

tasks.named("compileTestGroovy", GroovyCompile) {
  configureCompiler(it, 11)
  classpath += files(tasks.named('compileTestScala').map { it.destinationDirectory })
}

tasks.named("compileLatestDepTestGroovy", GroovyCompile) {
  configureCompiler(it, 11)
  classpath += files(tasks.named('compileLatestDepTestScala').map { it.destinationDirectory })
}

dependencies {
  main_java11CompileOnly libs.scala213
  main_java11CompileOnly group: 'com.typesafe.akka', name: 'akka-http_2.13', version: '10.6.0'
  main_java11CompileOnly group: 'com.typesafe.akka', name: 'akka-actor_2.13', version: '2.9.0'

  //testImplementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.6.0'
  testImplementation group: 'com.typesafe.akka', name: 'akka-http_2.13', version: '10.6.0'
  testImplementation group: 'com.typesafe.akka', name: 'akka-stream_2.13', version: '2.9.0'
  testImplementation group: 'com.typesafe.akka', name: 'akka-http-jackson_2.13', version: '10.6.0'
  testImplementation group: 'com.typesafe.akka', name: 'akka-http-spray-json_2.13', version: '10.6.0'
  testImplementation libs.scala213
  testImplementation project(':dd-java-agent:instrumentation:trace-annotation')
  testImplementation project(':dd-java-agent:instrumentation:akka:akka-actor-2.5')
  testImplementation project(':dd-java-agent:instrumentation:scala:scala-concurrent-2.8')
  testImplementation project(':dd-java-agent:instrumentation:akka:akka-http:akka-http-10.0')
  testImplementation project(':dd-java-agent:instrumentation:scala:scala-promise:scala-promise-2.13')

  latestDepTestImplementation group: 'com.typesafe.akka', name: 'akka-http_2.13', version: '+'
  latestDepTestImplementation group: 'com.typesafe.akka', name: 'akka-stream_2.13', version: '+'
  latestDepTestImplementation group: 'com.typesafe.akka', name: 'akka-pki_2.13', version: '+'
  latestDepTestImplementation group: 'com.typesafe.akka', name: 'akka-protobuf-v3_2.13', version: '+'
  latestDepTestImplementation group: 'com.typesafe.akka', name: 'akka-http-jackson_2.13', version: '+'
  latestDepTestImplementation group: 'com.typesafe.akka', name: 'akka-http-spray-json_2.13', version: '+'
  latestDepTestImplementation group: 'org.scala-lang.modules', name: 'scala-java8-compat_2.13', version: '1.0.+'
}

configurations.named("latestDepTestRuntimeClasspath") {
  it.resolutionStrategy {
    it.force libs.slf4j
  }
}
