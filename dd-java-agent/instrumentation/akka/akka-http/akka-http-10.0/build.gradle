apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'scala'
apply plugin: 'call-site-instrumentation'

// Since we are using different scala versions for different test sets,
// we put the test classes in the baseTest test set so that the scala
// version is not inherited
addTestSuite('baseTest')
addTestSuiteExtendingForDir('baseForkedTest', 'baseTest', 'baseTest')
addTestSuiteForDir('version101Test', 'baseTest')
addTestSuiteExtendingForDir('version101ForkedTest', 'version101Test', 'baseTest')
addTestSuiteForDir('version102Scala213Test', 'latestDepTest')
addTestSuite('latestDepTest')
addTestSuite('lagomTest')
addTestSuite('iastTest')
addTestSuiteForDir 'version101IastTest', 'iastTest'
addTestSuiteForDir 'version102IastTest', 'iastTest'
addTestSuiteForDir 'latestDepIastTest', 'iastTest'

tasks.named("lagomTest", Test) {
  testJvmConstraints {
    maxJavaVersion = JavaVersion.VERSION_1_8
  }
}

tasks.named("compileLagomTestJava", JavaCompile) {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.named("compileLagomTestGroovy", GroovyCompile) {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

muzzle {
  pass {
    group = 'com.typesafe.akka'
    module = 'akka-http_2.11'
    versions = "[10.0.0,10.1.0)"
    // later versions of akka-http expect streams to be provided
    extraDependency 'com.typesafe.akka:akka-stream_2.11:2.4.14'
  }
  pass {
    group = 'com.typesafe.akka'
    module = 'akka-http_2.12'
    versions = "[10.0.0,10.1.0)"
    // later versions of akka-http expect streams to be provided
    extraDependency 'com.typesafe.akka:akka-stream_2.12:2.4.14'
  }
  pass {
    group = 'com.typesafe.akka'
    module = 'akka-http_2.11'
    versions = "[10.1.0,)"
    // later versions of akka-http expect streams to be provided
    extraDependency 'com.typesafe.akka:akka-stream_2.11:2.5.11'
  }
  pass {
    group = 'com.typesafe.akka'
    module = 'akka-http_2.12'
    versions = "[10.1.0,)"
    // later versions of akka-http expect streams to be provided
    extraDependency 'com.typesafe.akka:akka-stream_2.12:2.5.11'
  }
  //There is no akka-http 10.0.x series for scala 2.13
  pass {
    group = 'com.typesafe.akka'
    module = 'akka-http_2.13'
    versions = "[10.1.8,)"
    // later versions of akka-http expect streams to be provided
    extraDependency 'com.typesafe.akka:akka-stream_2.13:2.5.23'
  }
}

configurations {
  testArtifacts

  iastTestImplementation.extendsFrom(baseTestImplementation)

  version101IastTestImplementation.extendsFrom(version101TestImplementation)
  version101IastTestRuntimeOnly.extendsFrom(iastTestRuntimeOnly)

  version102IastTestRuntimeOnly.extendsFrom(iastTestRuntimeOnly)

  latestDepIastTestRuntimeOnly.extendsFrom(iastTestRuntimeOnly)
}

// Create test artifacts so play-2.6 can reuse the akka-http test instrumentation
artifacts {
  testArtifacts baseTestJar
}

sourceSets {
  version102Scala213Test.groovy.srcDir sourceSets.baseTest.groovy
  version102Scala213Test.scala.srcDir sourceSets.baseTest.scala

  latestDepTest.groovy.srcDir sourceSets.baseTest.groovy
  latestDepTest.scala.srcDir sourceSets.baseTest.scala
}

dependencies {
  compileOnly libs.scala211
  compileOnly group: 'com.typesafe.akka', name: 'akka-http_2.11', version: '10.0.0'

  // These are the common dependencies that are inherited by the other test sets
  testImplementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.6.0'
  testImplementation project(':dd-java-agent:instrumentation:trace-annotation')
  testImplementation project(':dd-java-agent:instrumentation:akka:akka-actor-2.5')
  testImplementation project(':dd-java-agent:instrumentation:scala-concurrent')
  testImplementation project(':dd-java-agent:instrumentation:scala-promise:scala-promise-2.10')
  testImplementation project(':dd-java-agent:instrumentation:scala-promise:scala-promise-2.13')

  baseTestImplementation project(':utils:test-utils')
  baseTestImplementation libs.scala211
  // First 10.0.x version with a convenient way to test http2 support
  baseTestImplementation group: 'com.typesafe.akka', name: 'akka-http_2.11', version: '10.0.10'
  baseTestImplementation group: 'com.typesafe.akka', name: 'akka-http2-support_2.11', version: '10.0.10'
  baseTestImplementation group: 'com.typesafe.akka', name: 'akka-http-jackson_2.11', version: '10.0.10'
  baseTestImplementation group: 'com.typesafe.akka', name: 'akka-http-spray-json_2.11', version: '10.0.10'

  iastTestImplementation project(':dd-java-agent:agent-iast:iast-test-fixtures')
  iastTestCompileOnly group: 'de.thetaphi', name: 'forbiddenapis', version: '3.4'
  iastTestRuntimeOnly project(':dd-java-agent:instrumentation:jackson-core')
  iastTestRuntimeOnly project(':dd-java-agent:instrumentation:jackson-core:jackson-core-2.8')
  iastTestRuntimeOnly project(':dd-java-agent:instrumentation:iast-instrumenter')
  iastTestRuntimeOnly project(':dd-java-agent:instrumentation:akka:akka-http:akka-http-10.2-iast')

  // There are some internal API changes in 10.1 that we would like to test separately for
  version101TestImplementation libs.scala212
  version101TestImplementation group: 'com.typesafe.akka', name: 'akka-http_2.12', version: '10.1.+'
  version101TestImplementation group: 'com.typesafe.akka', name: 'akka-http2-support_2.12', version: '10.1.+'
  version101TestImplementation group: 'com.typesafe.akka', name: 'akka-stream_2.12', version: '2.5.+'
  version101TestImplementation group: 'com.typesafe.akka', name: 'akka-http-jackson_2.12', version: '10.1.+'
  version101TestImplementation group: 'com.typesafe.akka', name: 'akka-http-spray-json_2.12', version: '10.1.+'

  version101IastTestImplementation project(':dd-java-agent:agent-iast:iast-test-fixtures')

  version102IastTestImplementation libs.scala212
  version102IastTestImplementation group: 'com.typesafe.akka', name: 'akka-http_2.12', version: '10.2.+'
  version102IastTestImplementation group: 'com.typesafe.akka', name: 'akka-stream_2.12', version: '2.6.+'
  version102IastTestImplementation group: 'com.typesafe.akka', name: 'akka-http-jackson_2.12', version: '10.2.+'
  version102IastTestImplementation project(':dd-java-agent:agent-iast:iast-test-fixtures')

  version102Scala213TestImplementation libs.scala213
  version102Scala213TestImplementation group: 'com.typesafe.akka', name: 'akka-http_2.13', version: '10.2.+'
  version102Scala213TestImplementation group: 'com.typesafe.akka', name: 'akka-stream_2.13', version: '2.6.+'
  version102Scala213TestImplementation group: 'com.typesafe.akka', name: 'akka-http-jackson_2.13', version: '10.2.+'
  version102Scala213TestImplementation group: 'com.typesafe.akka', name: 'akka-http-spray-json_2.13', version: '10.2.+'

  latestDepTestImplementation libs.scala213
  latestDepTestImplementation group: 'com.typesafe.akka', name: 'akka-http_2.13', version: '10.5.+'
  // http2 support is included in akka-http since 10.2.x
  latestDepTestImplementation group: 'com.typesafe.akka', name: 'akka-stream_2.13', version: '2.7.0'
  latestDepTestImplementation group: 'com.typesafe.akka', name: 'akka-http-jackson_2.13', version: '10.5.+'
  latestDepTestImplementation group: 'com.typesafe.akka', name: 'akka-http-spray-json_2.13', version: '10.5.+'

  // TODO: test with Scala 3
  latestDepIastTestImplementation libs.scala213
  // Use akka-2.8.+ since latest akka-http release is still not compatible with akka-2.9.0-M1+.
  // akka-http versions are limited to 10.5.2 as this is the final version with Java 8 support
  // See: https://github.com/akka/akka-http/releases
  latestDepIastTestImplementation group: 'com.typesafe.akka', name: 'akka-http_2.13', version: '[10.+,10.5.2)'
  latestDepIastTestImplementation group: 'com.typesafe.akka', name: 'akka-stream_2.13', version: '2.8.+'
  latestDepIastTestImplementation group: 'com.typesafe.akka', name: 'akka-actor_2.13', version: '2.8.+'
  latestDepIastTestImplementation group: 'com.typesafe.akka', name: 'akka-http-jackson_2.13', version: '[10.+,10.5.2)'
  latestDepIastTestImplementation project(':dd-java-agent:agent-iast:iast-test-fixtures')
  latestDepIastTestImplementation project(':dd-java-agent:instrumentation:jackson-core:jackson-core-2.12')

  lagomTestImplementation libs.scala211
  lagomTestImplementation group: 'com.typesafe.akka', name: 'akka-http_2.11', version: '10.0.0'
  lagomTestImplementation project(':dd-java-agent:instrumentation:akka:akka-actor-2.5')
  lagomTestImplementation group: 'com.lightbend.lagom', name: 'lagom-javadsl-testkit_2.11', version: '1.4.0'
}

tasks.named("compileBaseTestGroovy", GroovyCompile) {
  classpath += files(tasks.named('compileBaseTestScala').map { it.destinationDirectory })
}

tasks.named("compileBaseForkedTestGroovy", GroovyCompile) {
  classpath += files(tasks.named('compileBaseTestScala').map { it.destinationDirectory })
}

tasks.named("compileVersion101TestGroovy", GroovyCompile) {
  classpath += files(tasks.named('compileVersion101TestScala').map { it.destinationDirectory })
}

tasks.named("compileVersion101ForkedTestGroovy", GroovyCompile) {
  classpath += files(tasks.named('compileVersion101TestScala').map { it.destinationDirectory })
}

tasks.named("compileVersion102Scala213TestGroovy", GroovyCompile) {
  classpath += files(tasks.named('compileVersion102Scala213TestScala').map { it.destinationDirectory })
}

tasks.named("compileLatestDepTestGroovy", GroovyCompile) {
  classpath += files(tasks.named('compileLatestDepTestScala').map { it.destinationDirectory })
}
