ext  {
  jetty10TestMinJavaVersionForTests = JavaVersion.VERSION_11
  jetty10LatestDepTestMinJavaVersionForTests = JavaVersion.VERSION_11
  jetty11TestMinJavaVersionForTests = JavaVersion.VERSION_11
  latestDepTestMinJavaVersionForTests = JavaVersion.VERSION_11
}

muzzle {
  /**
   * Jetty Client 10+ passes if muzzle is run with Java 11 but fails otherwise because it
   * is compiled with Java 11, so we can't validate with muzzle which uses Java 8.
   */
  pass {
    group = "org.eclipse.jetty"
    module = "jetty-client"
    versions = "[9.1,10)"
  }
  pass {
    group = "org.eclipse.jetty"
    module = "jetty-client"
    versions = "[10,12)"
    javaVersion = "11"
  }
  fail {
    group = "org.eclipse.jetty"
    module = "jetty-client"
    versions = "[,9.1)"
  }
  fail {
    group = "org.eclipse.jetty"
    module = "jetty-client"
    versions = "[12,)"
    javaVersion = "17"
  }
}

apply from: "$rootDir/gradle/java.gradle"

addTestSuiteForDir('jetty9LatestDepTest', 'test')
addTestSuite('jetty10Test')
addTestSuiteForDir('jetty10LatestDepTest', 'jetty10')
addTestSuiteForDir('jetty11Test', 'jetty10Test')
addTestSuiteForDir('latestDepTest', 'jetty10')


[
  compileJetty10TestGroovy,
  compileJetty11TestGroovy,
  compileJetty10LatestDepTestGroovy,
  compileLatestDepTestGroovy
].each {
  it.javaLauncher = getJavaLauncherFor(11)
}

configurations{
  all {
    resolutionStrategy {
      force "org.slf4j:slf4j-api:${versions.slf4j}"
    }
  }
}

dependencies {
  compileOnly group: 'org.eclipse.jetty', name: 'jetty-client', version: '9.1.0.v20131115'

  testImplementation(project(path:':dd-java-agent:testing', configuration: 'shadow')) {
    // explicitly declared below.
    exclude module: 'jetty-server'
  }
  testImplementation group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
  testImplementation project(':dd-java-agent:instrumentation:jetty-util')

  // jetty 9
  testImplementation group: 'org.eclipse.jetty', name: 'jetty-client', version: '9.1.0.v20131115'
  testImplementation group: 'org.eclipse.jetty', name: 'jetty-util', version: '9.1.0.v20131115'
  jetty9LatestDepTestImplementation group: 'org.eclipse.jetty', name: 'jetty-client', version: '9.+'
  jetty9LatestDepTestImplementation group: 'org.eclipse.jetty', name: 'jetty-util', version: '9.+'

  // jetty 10
  jetty10TestImplementation group: 'org.eclipse.jetty', name: 'jetty-client', version: '10.0.0'
  jetty10TestImplementation group: 'org.eclipse.jetty', name: 'jetty-util', version: '10.0.0'
  jetty10LatestDepTestImplementation group: 'org.eclipse.jetty', name: 'jetty-client', version: '10.+'
  jetty10LatestDepTestImplementation group: 'org.eclipse.jetty', name: 'jetty-util', version: '10.+'

  //jetty 11
  jetty11TestImplementation group: 'org.eclipse.jetty', name: 'jetty-client', version: '11.0.0'
  jetty11TestImplementation group: 'org.eclipse.jetty', name: 'jetty-util', version: '11.0.0'
  latestDepTestImplementation group: 'org.eclipse.jetty', name: 'jetty-client', version: '11.+'
  latestDepTestImplementation group: 'org.eclipse.jetty', name: 'jetty-util', version: '11.+'
}
