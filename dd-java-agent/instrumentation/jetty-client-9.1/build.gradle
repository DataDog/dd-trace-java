muzzle {
  /**
   * Jetty Client 10+ passes if muzzle is run with Java 11 but fails otherwise because it
   * is compiled with Java 11, so we can't validate with muzzle which uses Java 8.
   */
  pass {
    group = "org.eclipse.jetty"
    module = "jetty-client"
    versions = "[9.1,10)"
  }
  fail {
    group = "org.eclipse.jetty"
    module = "jetty-client"
    versions = "[,9.1)"
  }
}

apply from: "$rootDir/gradle/java.gradle"

addTestSuiteForDir('latestDepTest', 'test')

dependencies {
  compileOnly group: 'org.eclipse.jetty', name: 'jetty-client', version: '9.1.0.v20131115'

  testImplementation(project(':dd-java-agent:testing')) {
    // explicitly declared below.
    exclude module: 'jetty-server'
  }
  testImplementation project(':dd-java-agent:instrumentation:jetty-util')
  testImplementation group: 'org.eclipse.jetty', name: 'jetty-client', version: '9.1.0.v20131115'
  testImplementation group: 'org.eclipse.jetty', name: 'jetty-util', version: '9.1.0.v20131115'
  latestDepTestImplementation group: 'org.eclipse.jetty', name: 'jetty-client', version: '9.+' // 10+ requires Java 11

  // because there are shared dependencies between the client and server
  // we have to be explicit about which version to use.
  testImplementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.1.0.v20131115'
  latestDepTestImplementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.+'
}
