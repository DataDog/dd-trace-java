/*
 * Integration for Hibernate between 3.5 and 4.
 * Has the same logic as the Hibernate 4+ integration, but is copied rather than sharing a codebase. This is because
 * the root interface for Session/StatelessSession - SharedSessionContract - isn't present before version 4. So the
 * instrumentation isn't able to reference it.
 */

muzzle {
  pass {
    group = "org.hibernate"
    module = "hibernate-core"
    versions = "[3.3.0.GA,4.0.0.Final)"
  }
  fail {
    group = "org.hibernate"
    module = "hibernate-core"
    versions = "[4.0.0.Final,6)"
  }
  // hibernate-core has been moved
  fail {
    group = "org.hibernate.orm"
    module = "hibernate-core"
    versions = "[6,)"
  }
}

apply from: "$rootDir/gradle/java.gradle"

addTestSuiteForDir('latestDepTest', 'test')

dependencies {
  compileOnly group: 'org.hibernate', name: 'hibernate-core', version: '3.3.0.GA'

  implementation project(':dd-java-agent:instrumentation:hibernate')

  testImplementation project(':dd-java-agent:instrumentation:jdbc')
  // Added to ensure cross compatibility:
  testImplementation project(':dd-java-agent:instrumentation:hibernate:core-4.0')
  testImplementation project(':dd-java-agent:instrumentation:hibernate:core-4.3')

  testImplementation group: 'org.hibernate', name: 'hibernate-core', version: '3.3.0.SP1'
  testImplementation group: 'org.hibernate', name: 'hibernate-annotations', version: '3.4.0.GA'
  testImplementation group: 'javassist', name: 'javassist', version: '+'
  testImplementation group: 'com.h2database', name: 'h2', version: '1.4.197'
  testImplementation "javax.xml.bind:jaxb-api:2.2.11"
  testImplementation "com.sun.xml.bind:jaxb-core:2.2.11"
  testImplementation "com.sun.xml.bind:jaxb-impl:2.2.11"
  testImplementation "javax.activation:activation:1.1.1"

  latestDepTestImplementation group: 'org.hibernate', name: 'hibernate-core', version: '3.+'
}

configurations {
  // Needed for test, but for latestDepTest this would otherwise bundle a second incompatible version of hibernate-core.
  latestDepTestImplementation.exclude group: 'org.hibernate', module: 'hibernate-annotations'
}
