// Set properties before any plugins get loaded
ext {
  minJavaVersionForTests = JavaVersion.VERSION_11
}

apply from: "$rootDir/gradle/java.gradle"

muzzle {
  pass {
    group = 'io.vertx'
    module = 'vertx-web'
    versions = '[5.0.0.CR1,)'
    javaVersion = '11'
    includeSnapshots = true // to remove when vert.x 5 is finally released
  }
}

addTestSuiteForDir('latestDepTest', 'test')
addTestSuiteExtendingForDir('latestDepForkedTest', 'latestDepTest', 'test')

configurations {
  testArtifacts
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(11))
  }
}

// Create test artifacts so vertx-rx can reuse the server test instrumentation and base class
artifacts {
  testArtifacts testJar
}

final vertxVersion = '5.0.0.CR3'

dependencies {
  api project(':dd-java-agent:instrumentation:netty-4.1-shared')

  compileOnly group: 'io.vertx', name: 'vertx-web', version: vertxVersion

  testImplementation project(':dd-java-agent:instrumentation:netty-4.1')
  testImplementation project(':dd-java-agent:instrumentation:vertx:vertx-web:vertx-web-4.0')

  testImplementation project(':dd-java-agent:instrumentation:trace-annotation')
  testImplementation project(':dd-java-agent:agent-iast:iast-test-fixtures')
  testRuntimeOnly project(':dd-java-agent:instrumentation:iast-instrumenter')

  testImplementation group: 'io.vertx', name: 'vertx-web', version: vertxVersion
  testImplementation group: 'io.vertx', name: 'vertx-web-client', version: vertxVersion

  testImplementation project(':dd-java-agent:appsec:appsec-test-fixtures')

  testRuntimeOnly project(':dd-java-agent:instrumentation:jackson-core')
  testRuntimeOnly project(':dd-java-agent:instrumentation:netty-buffer-4')

  latestDepTestImplementation group: 'io.vertx', name: 'vertx-web', version: '+'
  latestDepTestImplementation group: 'io.vertx', name: 'vertx-web-client', version: '+'
}
