apply from: "$rootDir/gradle/java.gradle"

testJvmConstraints {
  // TODO Java 17: This version of vertx-web doesn't support Java 17
  maxJavaVersion = JavaVersion.VERSION_15
}

muzzle {
  pass {
    group = 'io.vertx'
    module = "vertx-web"
    versions = "[3.4.0,4)"
    assertInverse = true
  }
}

addTestSuiteForDir('latestDepTest', 'test')
addTestSuiteExtendingForDir('latestDepForkedTest', 'latestDepTest', 'test')

tasks.named("latestDepTest", Test) {
  finalizedBy 'latestDepForkedTest'
}

configurations {
  testArtifacts
}

// Create test artifacts so vertx-rx can reuse the server test instrumentation and base class
artifacts {
  testArtifacts testJar
}

dependencies {
  compileOnly group: 'io.vertx', name: 'vertx-web', version: '3.4.0'

  testImplementation project(':dd-java-agent:instrumentation:netty:netty-4.1')
  testImplementation project(':dd-java-agent:instrumentation:datadog:tracing:trace-annotation')
  testImplementation project(':dd-java-agent:agent-iast:iast-test-fixtures')
  testRuntimeOnly project(':dd-java-agent:instrumentation:datadog:asm:iast-instrumenter')


  testImplementation group: 'io.vertx', name: 'vertx-web', version: '3.4.0'
  testImplementation group: 'io.vertx', name: 'vertx-web-client', version: '3.4.0'

  testImplementation group: 'org.mockito', name: 'mockito-inline', version: '4.11.0'

  testImplementation project(':dd-java-agent:appsec:appsec-test-fixtures')

  latestDepTestImplementation group: 'io.vertx', name: 'vertx-web', version: '3.+'
  latestDepTestImplementation group: 'io.vertx', name: 'vertx-web-client', version: '3.+'
}
