// Set properties before any plugins get loaded
ext {
  // TODO Java 17: This version of vertx-web doesn't support Java 17
  maxJavaVersionForTests = JavaVersion.VERSION_15
}

apply from: "$rootDir/gradle/java.gradle"

muzzle {
  pass {
    group = 'io.vertx'
    module = "vertx-web"
    versions = "[3.5.0,4)"
    assertInverse = true
  }
}

addTestSuiteForDir('latestDepTest', 'test')
addTestSuiteExtendingForDir('latestDepForkedTest', 'latestDepTest', 'test')

apply from: "$rootDir/gradle/configure_tests.gradle"

latestDepTest {
  finalizedBy 'latestDepForkedTest'
}

configurations {
  testArtifacts
}

// Create test artifacts so vertx-rx can reuse the server test instrumentation and base class
artifacts {
  testArtifacts testJar
}

dependencies {
  compileOnly group: 'io.vertx', name: 'vertx-web', version: '3.5.0'

  testImplementation project(':dd-java-agent:instrumentation:netty-4.1')
  testImplementation project(':dd-java-agent:instrumentation:trace-annotation')

  testImplementation group: 'io.vertx', name: 'vertx-web', version: '3.5.0'

  latestDepTestImplementation group: 'io.vertx', name: 'vertx-web', version: '3.+'
}
