// Set properties before any plugins get loaded
ext {
  // vertx-web doesn't support Java 17 until v4.2
  maxJavaVersionForTests = JavaVersion.VERSION_15
  // unbound it for latest
  latestDepTestMinJavaVersionForTests = JavaVersion.VERSION_11
  latestDepForkedTestMinJavaVersionForTests = JavaVersion.VERSION_11
  latestDepTestMaxJavaVersionForTests = JavaVersion.VERSION_25
  latestDepForkedTestMaxJavaVersionForTests = JavaVersion.VERSION_25
}

apply from: "$rootDir/gradle/java.gradle"

muzzle {
  pass {
    group = 'io.vertx'
    module = "vertx-web"
    versions = "[4.0.0,5)"
  }
  fail {
    group = 'io.vertx'
    module = "vertx-web"
    versions = "(,4.0.0)"
  }
}

addTestSuiteForDir('latestDepTest', 'latestDepTest')
addTestSuiteExtendingForDir('latestDepForkedTest', 'latestDepTest', 'latestDepTest')

configurations {
  testArtifacts
}

// Create test artifacts so vertx-rx can reuse the server test instrumentation and base class
artifacts {
  testArtifacts testJar
}

dependencies {
  api project(':dd-java-agent:instrumentation:netty:netty-common')

  compileOnly group: 'io.vertx', name: 'vertx-web', version: '4.0.0'

  testImplementation project(':dd-java-agent:instrumentation:netty:netty-4.1')
  testImplementation project(':dd-java-agent:instrumentation:trace-annotation')
  testImplementation project(':dd-java-agent:agent-iast:iast-test-fixtures')
  testRuntimeOnly project(':dd-java-agent:instrumentation:iast-instrumenter')

  testImplementation group: 'io.vertx', name: 'vertx-web', version: '4.0.0'
  testImplementation group: 'io.vertx', name: 'vertx-web-client', version: '4.0.0'

  testImplementation project(':dd-java-agent:appsec:appsec-test-fixtures')

  testRuntimeOnly project(':dd-java-agent:instrumentation:jackson-core')
  testRuntimeOnly project(':dd-java-agent:instrumentation:netty:netty-buffer-4.0')

  latestDepTestImplementation group: 'io.vertx', name: 'vertx-web', version: '4.+'
  latestDepTestImplementation group: 'io.vertx', name: 'vertx-web-client', version: '4.+'
}

["compileLatestDepTestJava", "compileLatestDepForkedTestJava"].each { name ->
  tasks.named(name, JavaCompile) {
    configureCompiler(it, 11)
  }
}
["compileLatestDepForkedTestGroovy", "compileLatestDepTestGroovy"].each {
  tasks.named(it, GroovyCompile) {
    configureCompiler(it, 11)
  }
}
