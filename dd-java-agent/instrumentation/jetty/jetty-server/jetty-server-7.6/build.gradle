muzzle {
  pass {
    group = "org.eclipse.jetty"
    module = 'jetty-server'
    // Includes 7.6.x plus 8.1-8.2 (8.0 covered by 7.0 instrumentation)
    versions = "[7.6,8),[8.1,9)"
    assertInverse = true
  }
}

apply from: "$rootDir/gradle/java.gradle"

addTestSuiteForDir('latestDepForkedTest', 'test')

// Exclude servlet 3.x API (coming from dd-java-agent:testing) to ensure servlet 2.x instrumentation applies
configurations.testImplementation {
  exclude group: 'javax.servlet', module: 'javax.servlet-api'
}
configurations.testRuntimeOnly {
  exclude group: 'javax.servlet', module: 'javax.servlet-api'
}

dependencies {
  compileOnly group: 'org.eclipse.jetty', name: 'jetty-server', version: '7.6.0.v20120127'
  implementation project(':dd-java-agent:instrumentation:jetty:jetty-common')

  // Don't want to conflict with jetty from the test server.
  testImplementation(project(':dd-java-agent:instrumentation-testing')) {
    exclude group: 'org.eclipse.jetty', module: 'jetty-server'
  }
  testImplementation project(':dd-java-agent:instrumentation:jetty:jetty-util-9.4.31')

  testImplementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '7.6.0.v20120127'
  testImplementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '7.6.0.v20120127'
  testImplementation group: 'org.eclipse.jetty', name: 'jetty-continuation', version: '7.6.0.v20120127'
  testImplementation testFixtures(project(':dd-java-agent:instrumentation:servlet:javax-servlet:javax-servlet-3.0'))
  testRuntimeOnly project(':dd-java-agent:instrumentation:servlet:javax-servlet:javax-servlet-2.2')
  testRuntimeOnly project(':dd-java-agent:instrumentation:jetty:jetty-appsec:jetty-appsec-7.0')

  latestDepTestImplementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '8.+'
  latestDepTestImplementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '8.+'
  latestDepTestImplementation group: 'org.eclipse.jetty', name: 'jetty-continuation', version: '8.+'
}
