muzzle {
  pass {
    group = "org.eclipse.jetty"
    module = 'jetty-server'
    versions = "[9.3.0.M1,9.4.21)"
    assertInverse = true
  }
}

apply from: "$rootDir/gradle/java.gradle"

addTestSuiteForDir('jetty92ForkedTest', 'test')
addTestSuiteForDir('jetty94ForkedTest', 'test')
addTestSuiteForDir('latestDepJetty9ForkedTest', 'test')
addTestSuiteForDir('latestDepForkedTest', 'test')

tasks.named("latestDepForkedTest") {
  javaLauncher = getJavaLauncherFor(11)
}
tasks.named("compileLatestDepForkedTestGroovy", GroovyCompile) {
  setJavaVersion(it, 11)
}

dependencies {
  compileOnly group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.0.0.v20130308'

  implementation project(':dd-java-agent:instrumentation:jetty:jetty-common')

  main_jetty904CompileOnly group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.0.4.v20130625'
  main_jetty904CompileOnly project(':internal-api')
  main_jetty904CompileOnly project(':dd-java-agent:agent-tooling')
  main_jetty904CompileOnly project(':dd-java-agent:agent-bootstrap')
  // not pretty, but we can't depend on sourceSets.main.output;
  // that would make a dependency on the classes task due to the
  // intermediation of the InstrumentPlugin, creating a circular
  // dependency (the instrument plugin needs all the sourceSets
  // compiled to properly generate References)
  main_jetty904CompileOnly files("$project.buildDir/classes/java/raw")

  main_jetty93CompileOnly group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.3.0.v20150612'
  main_jetty93CompileOnly project(':internal-api')
  main_jetty93CompileOnly project(':dd-java-agent:agent-tooling')
  main_jetty93CompileOnly project(':dd-java-agent:agent-bootstrap')
  main_jetty93CompileOnly files("$project.buildDir/classes/java/raw") {
    builtBy = ['compileJava']
  }

  main_jetty9421CompileOnly group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.21.v20190926'
  main_jetty9421CompileOnly project(':internal-api')
  main_jetty9421CompileOnly project(':dd-java-agent:agent-tooling')
  main_jetty9421CompileOnly project(':dd-java-agent:agent-bootstrap')
  main_jetty9421CompileOnly files("$project.buildDir/classes/java/raw") {
    builtBy = ['compileJava']
  }

  main_jetty10CompileOnly group: 'org.eclipse.jetty', name: 'jetty-server', version: '10.0.0'

  main_jetty10CompileOnly project(':internal-api')
  main_jetty10CompileOnly project(':dd-java-agent:agent-tooling')
  main_jetty10CompileOnly project(':dd-java-agent:agent-bootstrap')
  main_jetty10Implementation project(':dd-java-agent:instrumentation:jetty:jetty-common')
  main_jetty10CompileOnly files("$project.buildDir/classes/java/raw") {
    builtBy = ['compileJava']
  }

  testFixturesImplementation(project(':dd-java-agent:instrumentation-testing')) {
    exclude group: 'org.eclipse.jetty', module: 'jetty-server'
  }
  // Don't want to conflict with jetty from the test server.
  testImplementation(project(':dd-java-agent:instrumentation-testing')) {
    exclude group: 'org.eclipse.jetty', module: 'jetty-server'
  }
  testImplementation project(':dd-java-agent:instrumentation:jetty:jetty-util-9.4.31')

  String jetty9Version = '9.0.0.v20130308'
  testFixturesCompileOnly group: 'org.eclipse.jetty', name: 'jetty-server', version: jetty9Version
  testFixturesCompileOnly group: 'org.eclipse.jetty', name: 'jetty-servlet', version: jetty9Version
  testFixturesImplementation  group: 'javax.websocket', name: 'javax.websocket-api', version: '1.0'
  testImplementation group: 'org.eclipse.jetty', name: 'jetty-server', version: jetty9Version
  testImplementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version: jetty9Version
  testImplementation group: 'org.eclipse.jetty', name: 'jetty-continuation', version: jetty9Version
  testImplementation project(':dd-java-agent:instrumentation:jetty:jetty-appsec:jetty-appsec-7.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:servlet:javax-servlet:javax-servlet-2.2')
  testRuntimeOnly project(':dd-java-agent:instrumentation:jetty:jetty-appsec:jetty-appsec-8.1.3')
  testRuntimeOnly project(':dd-java-agent:instrumentation:websocket:javax-websocket-1.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:websocket:jakarta-websocket-2.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:websocket:jetty-websocket:jetty-websocket-10')
  testImplementation testFixtures(project(':dd-java-agent:instrumentation:servlet:javax-servlet:javax-servlet-3.0'))
  testFixturesImplementation testFixtures(project(':dd-java-agent:instrumentation:servlet:javax-servlet:javax-servlet-3.0'))
  testImplementation project(':dd-java-agent:appsec:appsec-test-fixtures')

  jetty92TestImplementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.2.30.v20200428'
  jetty92TestImplementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.2.30.v20200428'
  jetty92TestImplementation group: 'org.eclipse.jetty', name: 'jetty-continuation', version: '9.2.30.v20200428'
  jetty92TestImplementation group: 'org.eclipse.jetty.websocket', name: 'javax-websocket-server-impl', version: '9.2.30.v20200428'
  jetty92TestImplementation project(':dd-java-agent:instrumentation:jetty:jetty-appsec:jetty-appsec-9.2')
  jetty92TestImplementation testFixtures(project(':dd-java-agent:instrumentation:servlet:javax-servlet:javax-servlet-3.0'))

  jetty94TestImplementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.15.v20190215'
  jetty94TestImplementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.4.15.v20190215'
  jetty94TestImplementation group: 'org.eclipse.jetty', name: 'jetty-continuation', version: '9.4.15.v20190215'
  jetty94TestImplementation group: 'org.eclipse.jetty.websocket', name: 'javax-websocket-server-impl', version: '9.4.15.v20190215'
  jetty94TestImplementation project(':dd-java-agent:instrumentation:jetty:jetty-appsec:jetty-appsec-9.3')
  jetty94TestImplementation testFixtures(project(':dd-java-agent:instrumentation:servlet:javax-servlet:javax-servlet-3.0'))

  latestDepJetty9TestImplementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.+'
  latestDepJetty9TestImplementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.+'
  latestDepJetty9TestImplementation group: 'org.eclipse.jetty', name: 'jetty-continuation', version: '9.+'
  latestDepJetty9TestImplementation group: 'org.eclipse.jetty.websocket', name: 'javax-websocket-server-impl', version: '9.+'

  latestDepJetty9TestImplementation project(':dd-java-agent:instrumentation:jetty:jetty-appsec:jetty-appsec-9.3')
  latestDepJetty9TestImplementation testFixtures(project(':dd-java-agent:instrumentation:servlet:javax-servlet:javax-servlet-3.0'))

  latestDepTestImplementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '10.+'
  latestDepTestImplementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '10.+'
  latestDepTestImplementation group: 'org.eclipse.jetty.websocket', name: 'websocket-javax-server', version: '10.+'
  latestDepTestImplementation project(':dd-java-agent:instrumentation:jetty:jetty-appsec:jetty-appsec-9.3')
  latestDepTestImplementation testFixtures(project(':dd-java-agent:instrumentation:servlet:javax-servlet:javax-servlet-3.0'))
}
configurations.getByName('latestDepForkedTestRuntimeClasspath').resolutionStrategy {
  force libs.slf4j
}
