plugins {
  id 'com.google.protobuf' version '0.9.6'
}

muzzle {
  pass {
    group = "io.grpc"
    module = "grpc-core"
    versions = "[1.5.0,)"
  }
}

apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'idea'

// First version with Mac M1 support
def grpcVersion = '1.42.2'
protobuf {
  protoc {
    // Download compiler rather than using locally installed version:
    // First version with Mac M1 support
    artifact = 'com.google.protobuf:protoc:3.17.3'
  }
  plugins {
    // First version with aarch support
    grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
  }
  generateProtoTasks {
    ofSourceSet("test").configureEach {
      plugins { grpc {} }
    }
  }
}

addTestSuiteForDir('latestDepTest', 'test')

dependencies {
  compileOnly group: 'io.grpc', name: 'grpc-core', version: grpcVersion

  testImplementation group: 'io.grpc', name: 'grpc-netty', version: grpcVersion
  testImplementation group: 'io.grpc', name: 'grpc-protobuf', version: grpcVersion
  testImplementation group: 'io.grpc', name: 'grpc-stub', version: grpcVersion
  testImplementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
  testImplementation project(':dd-java-agent:agent-debugger:debugger-bootstrap')

  latestDepTestImplementation sourceSets.test.output // include the protobuf generated classes
  latestDepTestCompileOnly group: 'io.grpc', name: 'grpc-core', version: '1.+'

  latestDepTestImplementation group: 'io.grpc', name: 'grpc-inprocess', version: '1.+'
  latestDepTestImplementation group: 'io.grpc', name: 'grpc-netty', version: '1.+'
  latestDepTestImplementation group: 'io.grpc', name: 'grpc-protobuf', version: '1.+'
  latestDepTestImplementation group: 'io.grpc', name: 'grpc-stub', version: '1.+'
}
