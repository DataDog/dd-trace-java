plugins {
  id 'dd-trace-java.instrumentation.testing-framework-tests'
}

apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'scala'

muzzle {
  pass {
    group = 'org.scalameta'
    module = 'munit_2.13'
    versions = '[0.7.28,)'
  }
}

addTestSuiteForDir('latestDepTest', 'test')

dependencies {
  implementation project(':dd-java-agent:instrumentation:junit:junit-4.10')
  compileOnly group: 'org.scalameta', name: 'munit_2.13', version: '0.7.28'

  testImplementation project(':dd-java-agent:agent-ci-visibility:civisibility-instrumentation-test-fixtures')

  // version used below is not the minimum one that we support,
  // but the tests need to use it in order to be compliant with Spock 2.x
  testImplementation(group: 'junit', name: 'junit') {
    version {
      strictly '4.13.2'
    }
  }

  testImplementation group: 'org.scala-lang', name: 'scala-library', version: '2.13.10'
  testImplementation group: 'org.scalameta', name: 'munit_2.13', version: '0.7.28'

  latestDepTestImplementation group: 'org.scalameta', name: 'munit_2.13', version: '+'
}

tasks.named("compileTestGroovy", GroovyCompile) {
  dependsOn compileTestScala
  classpath += files(sourceSets.test.scala.destinationDirectory)
}

tasks.named("compileLatestDepTestGroovy", GroovyCompile) {
  dependsOn compileLatestDepTestScala
  classpath += files(sourceSets.latestDepTest.scala.destinationDirectory)
}

tasks.withType(ScalaCompile).configureEach {
  // Despite the compatibility matrix, scala compiler doesn't properly
  // get the right compiler flags with JDK11 toolchain. Using 8 works.
  // * https://docs.scala-lang.org/overviews/jdk-compatibility/overview.html
  // * https://github.com/gradle/gradle/issues/19456
  configureCompiler(it, 8)
}

