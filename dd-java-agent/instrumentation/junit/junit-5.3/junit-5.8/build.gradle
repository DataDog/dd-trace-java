plugins {
  id 'dd-trace-java.instrumentation.testing-framework-tests'
}

apply from: "$rootDir/gradle/java.gradle"

def jupiterVersion = '5.8.0'
def platformVersion = '1.8.0'

muzzle {
  pass {
    group = 'org.junit.platform'
    module = 'junit-platform-launcher'
    versions = "[$platformVersion,)"
    extraDependency "org.junit.jupiter:junit-jupiter-engine:$jupiterVersion"
  }
  pass {
    group = 'org.junit.jupiter'
    module = 'junit-jupiter-engine'
    versions = "[$jupiterVersion,)"
  }
  pass {
    group = 'org.junit.jupiter'
    module = 'junit-jupiter-api'
    versions = "[$jupiterVersion,)"
    extraDependency "org.junit.jupiter:junit-jupiter-engine:$jupiterVersion"
  }
}

addTestSuiteForDir('latestDepTest', 'test')

dependencies {
  implementation project(':dd-java-agent:instrumentation:junit:junit-5.3')

  compileOnly group: 'org.junit.platform', name: 'junit-platform-launcher', version: "$platformVersion"
  compileOnly group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "$jupiterVersion"
  compileOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "$jupiterVersion"

  testImplementation project(':dd-java-agent:agent-ci-visibility:civisibility-instrumentation-test-fixtures')

  // versions used below are not the minimum ones that we support,
  // but the tests need to use them in order to be compliant with Spock 2.x
  testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: libs.versions.junit.platform.get()
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: libs.versions.junit5.get()
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: libs.versions.junit5.get()

  latestDepTestImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: '+'
  latestDepTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '+'
  latestDepTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '+'
}

configurations.matching { it.name.startsWith('test') }.configureEach {
  it.resolutionStrategy {
    force group: 'org.junit.platform', name: 'junit-platform-launcher', version: libs.versions.junit.platform.get()
    force group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: libs.versions.junit5.get()
    force group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: libs.versions.junit5.get()
  }
}

// Reject RC (Release Candidate) and M (Milestone) versions for latestDepTest configurations
configurations.matching { it.name.startsWith('latestDepTest') }.configureEach {
  it.resolutionStrategy.componentSelection.all { ComponentSelection selection ->
    def version = selection.candidate.version
    if ((selection.candidate.group == 'org.junit.platform' || selection.candidate.group == 'org.junit.jupiter') &&
      (version.matches('.*-RC.*') || version.matches('.*-M\\d+.*'))) {
      selection.reject("Skipping pre-release version: ${version}")
    }
  }
}

tasks.named("compileLatestDepTestJava", JavaCompile) {
  configureCompiler(it, 17, JavaVersion.VERSION_1_8)
}

tasks.named("latestDepTest", Test) {
  testJvmConstraints {
    minJavaVersion = JavaVersion.VERSION_17
  }
}
