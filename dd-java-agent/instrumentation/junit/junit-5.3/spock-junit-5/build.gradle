plugins {
  id 'dd-trace-java.instrumentation.testing-framework-tests'
}

apply from: "$rootDir/gradle/java.gradle"

def spockGroovyVersion = '3.0'

muzzle {
  pass {
    group = 'org.spockframework'
    module = 'spock-core'
    versions = "[2.0-groovy-${spockGroovyVersion},)"
    extraDependency "org.junit.platform:junit-platform-launcher:1.12.0"
  }
}

addTestSuiteForDir('latestDepTest', 'test')

dependencies {
  implementation project(':dd-java-agent:instrumentation:junit:junit-5.3')

  compileOnly group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.7.2'
  compileOnly group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.2'
  compileOnly group: 'org.spockframework', name: 'spock-core', version: "2.0-groovy-${spockGroovyVersion}"

  testImplementation project(':dd-java-agent:agent-ci-visibility:civisibility-instrumentation-test-fixtures')

  testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: libs.versions.junit.platform.get()
  testImplementation group: 'org.spockframework', name: 'spock-core', version: "2.2-groovy-${spockGroovyVersion}"

  latestDepTestImplementation group: 'org.spockframework', name: 'spock-core', version: '2.+'
}

configurations.matching({ it.name.startsWith('test') }).configureEach({
  it.resolutionStrategy {
    force group: 'org.junit.platform', name: 'junit-platform-launcher', version: libs.versions.junit.platform.get()
    force group: 'org.spockframework', name: 'spock-core', version: "2.2-groovy-${spockGroovyVersion}"
  }
})

configurations.matching({ it.name.startsWith('latestDepTest') }).configureEach({
  // it.resolutionStrategy.componentSelection.all { ComponentSelection selection ->
  //   if (selection.candidate.group == 'org.spockframework' && selection.candidate.module == 'spock-core' && !selection.candidate.version.endsWith("-groovy-${spockGroovyVersion}")) {
  //     selection.reject('Spock groovy version mismatch')
  //   }
  // TODO: `org.spockframework.lang.SpecInternals` was moved to `org.spockframework.runtime.SpecInternals` in the latest M7 release: https://github.com/spockframework/spock/pull/2209
  // Pinning to 2.4-M6 until we update the instrumentation to use the new class and fall back to the old one.
  it.resolutionStrategy {
    force "org.spockframework:spock-core:2.4-M6-groovy-${spockGroovyVersion}"
  }
})
