plugins {
  id 'dd-trace-java.instrumentation.testing-framework-tests'
}

apply from: "$rootDir/gradle/java.gradle"

// JUnit5 5.3.0+ version is needed because of the fix in the TestInheritance test suite names.
// Previously at that version, the test suite name was the base class.
def jupiterVersion = '5.3.0'
def platformVersion = '1.3.0'

muzzle {
  pass {
    group = 'org.junit.platform'
    module = 'junit-platform-launcher'
    versions = "[$platformVersion,)"
    assertInverse = true
    extraDependency "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
  }
  pass {
    group = 'org.junit.jupiter'
    module = 'junit-jupiter-api'
    versions = "[$jupiterVersion,)"
    assertInverse = true
    extraDependency "org.junit.platform:junit-platform-launcher:$platformVersion"
  }
}

addTestSuiteForDir('v513Test', 'test')
addTestSuiteForDir('latest5Test', 'test')
addTestSuiteForDir('latestDepTest', 'test')

dependencies {
  compileOnly group: 'org.junit.platform', name: 'junit-platform-launcher', version: "$platformVersion"
  compileOnly group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "$jupiterVersion"

  testImplementation project(':dd-java-agent:agent-ci-visibility:civisibility-instrumentation-test-fixtures')

  testImplementation libs.junit4 // This module still needs JUnit4.

  // versions used below are not the minimum ones that we support,
  // but the tests need to use them in order to be compliant with Spock 2.x
  testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: libs.versions.junit.platform.get()
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: libs.versions.junit5.get()
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: libs.versions.junit5.get()

  v513TestImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.13.0'
  v513TestImplementation  group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.13.0'
  v513TestImplementation  group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.13.0'

  latest5TestImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.+'
  latest5TestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.+'
  latest5TestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.+'

  latestDepTestImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: '+'
  latestDepTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '+'
  latestDepTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '+'
}

configurations.matching { it.name.startsWith('test') }.configureEach {
  it.resolutionStrategy {
    force group: 'org.junit.platform', name: 'junit-platform-launcher', version: libs.versions.junit.platform.get()
    force group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: libs.versions.junit5.get()
    force group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: libs.versions.junit5.get()
  }
}

// Reject RC (Release Candidate) and M (Milestone) versions for latest5Test and latestDepTest configurations
configurations.matching { it.name.startsWith('latest5Test') || it.name.startsWith('latestDepTest') }.configureEach {
  it.resolutionStrategy.componentSelection.all { ComponentSelection selection ->
    def version = selection.candidate.version
    if ((selection.candidate.group == 'org.junit.platform' || selection.candidate.group == 'org.junit.jupiter') &&
      (version.matches('.*-RC.*') || version.matches('.*-M\\d+.*'))) {
      selection.reject("Skipping pre-release version: ${version}")
    }
  }
}

tasks.named("compileLatestDepTestJava", JavaCompile) {
  configureCompiler(it, 17, JavaVersion.VERSION_1_8)
}

tasks.named("latestDepTest", Test) {
  testJvmConstraints {
    minJavaVersion = JavaVersion.VERSION_17
  }
}

configureGroovyCompiler(17, "compileLatestDepTestGroovy")
