muzzle {
  // Catalina doesn't reliably depend on coyote, so we use a different dependency that does.
  pass {
    group = "org.apache.tomcat"
    module = 'tomcat-catalina-ha' // includes catalina and coyote (to simplify muzzle check)
    versions = "[7,]"
    assertInverse = true
  }
  pass {
    group = "org.apache.tomcat"
    module = 'catalina-ha' // includes catalina and coyote (to simplify muzzle check)
    versions = "[6,7)" // not all versions are on maven central.
    assertInverse = true
  }
  // Older versions are sparse on maven central, so test what we can.
  pass {
    group = "tomcat"
    module = 'catalina'
    versions = "[5.5,6)"
    extraDependency 'tomcat:tomcat-coyote:5.5.23'
    assertInverse = true
  }
  pass {
    group = "tomcat"
    module = 'tomcat-coyote'
    versions = "[5.5,6)"
    extraDependency 'tomcat:catalina:5.5.23'
    extraDependency 'tomcat:tomcat-util:5.5.23'
    assertInverse = true
  }
  // org.apache.catalina.connector.CoyoteAdapter introduced in Catalina 5.5
}

apply from: "$rootDir/gradle/java.gradle"

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  latestDepTest
}

def tomcatVersion = '5.5.12' // earliest 5.5.x available in maven central (with all needed dependencies).

dependencies {
  compileOnly group: 'tomcat', name: 'catalina', version: tomcatVersion
  compileOnly group: 'tomcat', name: 'tomcat-coyote', version: tomcatVersion
  compileOnly group: 'tomcat', name: 'tomcat-util', version: tomcatVersion

  // Version that corresponds with Tomcat 5.5
  // https://tomcat.apache.org/whichversion.html
  compileOnly group: 'javax.servlet', name: 'servlet-api', version: '2.4'

  testCompile(project(':dd-java-agent:testing')) {
    exclude group: 'org.eclipse.jetty', module: 'jetty-server'
  }

  // Required jars for embedded tomcat:
  testCompile group: 'tomcat', name: 'catalina', version: tomcatVersion
  testCompile group: 'tomcat', name: 'tomcat-coyote', version: tomcatVersion
  testCompile group: 'tomcat', name: 'tomcat-http', version: tomcatVersion
  testCompile group: 'tomcat', name: 'tomcat-util', version: tomcatVersion
  testCompile group: 'tomcat', name: 'naming-resources', version: tomcatVersion
  testCompile group: 'tomcat', name: 'naming-factory', version: tomcatVersion
  testCompile group: 'commons-modeler', name: 'commons-modeler', version: '2.0.1'
  testCompile group: 'javax.servlet', name: 'servlet-api', version: '2.4'

  latestDepTestCompile group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '+'
}

// Exclude all the dependencies from test for latestDepTest since the names are completely different.
configurations {
  latestDepTestCompile {
    exclude group: 'tomcat', module: 'catalina'
    exclude group: 'tomcat', module: 'tomcat-coyote'
    exclude group: 'tomcat', module: 'tomcat-util'
    exclude group: 'tomcat', module: 'tomcat-http'
    exclude group: 'tomcat', module: 'naming-resources'
    exclude group: 'tomcat', module: 'naming-factory'
    exclude group: 'commons-modeler', module: 'commons-modeler'
    exclude group: 'javax.servlet', module: 'servlet-api'
  }
}
