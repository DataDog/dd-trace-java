
muzzle {
  fail {
    group = "javax.ws.rs"
    module = "jsr311-api"
    versions = "[,]"
  }
  pass {
    group = "javax.ws.rs"
    module = "javax.ws.rs-api"
    versions = "[,]"
  }
  pass {
    group = "javax.ws.rs"
    module = "javax.ws.rs-api"
    name = 'javax-message-body-writer'
    versions = "[,]"
  }
}

apply from: "$rootDir/gradle/java.gradle"

addTestSuiteForDir('latestDepTest', 'test')
addTestSuiteForDir('resteasy31Test', 'test')
addTestSuite('nestedTest')

dependencies {
  compileOnly group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0'

  testImplementation project(':dd-java-agent:instrumentation:servlet:request-3')
  testImplementation project(':dd-java-agent:instrumentation:jax-rs-annotations-2:filter-jersey')
  testImplementation project(':dd-java-agent:instrumentation:jax-rs-annotations-2:filter-resteasy-3.0')
  testImplementation project(':dd-java-agent:instrumentation:jax-rs-annotations-2:filter-resteasy-3.1')

  // Jersey
  // First version with DropwizardTestSupport:
  testImplementation group: 'io.dropwizard', name: 'dropwizard-testing', version: '0.8.0'
  testImplementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
  testImplementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-afterburner', version: '2.9.10'

  nestedTestImplementation group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '2.3.5'

  latestDepTestImplementation group: 'io.dropwizard', name: 'dropwizard-testing', version: '1.+'

  // Resteasy
  testImplementation group: 'org.jboss.resteasy', name: 'resteasy-jaxrs', version: '3.0.0.Final'

  resteasy31TestImplementation(group: 'org.jboss.resteasy', name: 'resteasy-jaxrs') {
    version {
      strictly '3.1.0.Final'
    }
  }

  latestDepTestImplementation group: 'org.jboss.resteasy', name: 'resteasy-jaxrs', version: '+'
}

tasks.named("test") {
  dependsOn "resteasy31Test"
  dependsOn "nestedTest"
}
