// Set properties before any plugins get loaded
ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

apply from: "${rootDir}/gradle/java.gradle"

muzzle {
  pass {
    group = "io.netty"
    module = "netty-codec-http"
    versions = "[4.1.0.Final,)"
    assertInverse = true
  }
  pass {
    group = "io.netty"
    module = "netty"
    versions = "[4.1.0.Final,)"
    assertInverse = true
  }
}

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  vertxTest
  latestDepTest {
    dirName = 'test'
  }
}

compileVertxTestJava {
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
}

compileVertxTestGroovy {
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
}

dependencies {
  compileOnly group: 'io.netty', name: 'netty-codec-http', version: '4.1.0.Final'

  compile project(':dd-java-agent:agent-tooling')

  compile deps.bytebuddy
  compile deps.opentracing
  annotationProcessor deps.autoservice
  implementation deps.autoservice

  testCompile project(':dd-java-agent:testing')
  testCompile project(':dd-java-agent:instrumentation:java-concurrent')
  testCompile group: 'io.netty', name: 'netty-codec-http', version: '4.1.0.Final'
  testCompile group: 'org.asynchttpclient', name: 'async-http-client', version: '2.1.0'

  vertxTestCompile project(':dd-java-agent:testing')
  vertxTestCompile project(':dd-java-agent:instrumentation:java-concurrent')
  vertxTestCompile project(':dd-java-agent:instrumentation:trace-annotation')
  vertxTestCompile group: 'io.vertx', name: 'vertx-web', version: '3.5.0'
}

// We need to force the dependency to the earliest supported version because other libraries declare newer versions.
configurations.testCompile {
  resolutionStrategy {
    eachDependency { DependencyResolveDetails details ->
      //specifying a fixed version for all libraries with io.netty' group
      if (details.requested.group == 'io.netty') {
        details.useVersion "4.1.0.Final"
      }
    }
  }
}

configurations.latestDepTestCompile {
  resolutionStrategy {
    force group: 'io.netty', name: 'netty-codec-http', version: '+'
    force group: 'org.asynchttpclient', name: 'async-http-client', version: '+'
  }
}

