apply plugin: 'version-scan'

versionScan {
  group = "io.netty"
  module = "netty-codec-http"
  legacyModule = "netty"
  versions = "[4.1.0.Final,)"
  verifyPresent = [
    "io.netty.handler.codec.http.HttpHeaderValues": null,
  ]
}

apply from: "${rootDir}/gradle/java.gradle"

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  latestDepTest {
    dirName = 'test'
  }
}

dependencies {
  compileOnly group: 'io.netty', name: 'netty-codec-http', version: '4.1.0.Final'

  compile project(':dd-java-agent:agent-tooling')

  compile deps.bytebuddy
  compile deps.opentracing
  annotationProcessor deps.autoservice
  implementation deps.autoservice

  testCompile project(':dd-java-agent:testing')

  testCompile group: 'io.netty', name: 'netty-all', version: '4.1.0.Final'
  testCompile group: 'org.asynchttpclient', name: 'async-http-client', version: '2.0.31'
  // async-http-client:2.0.32+ would require netty:4.1.9.Final

  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-webflux', version: '2.0.0.RELEASE'
  testCompile group: 'org.spockframework', name: 'spock-spring', version: '1.1-groovy-2.4'
  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.0.0.RELEASE'
  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-reactor-netty', version: '2.0.0.RELEASE'

}

// We need to force the dependency to the earliest supported version because other libraries declare newer versions.
configurations.testCompile {
  resolutionStrategy {
    eachDependency { DependencyResolveDetails details ->
      //specifying a fixed version for all libraries with io.netty' group
      if (details.requested.group == 'io.netty') {
        details.useVersion "4.1.0.Final"
      }
    }
  }
}

configurations.latestDepTestCompile {
  resolutionStrategy {
    force group: 'io.netty', name: 'netty-all', version: '+'
    force group: 'org.asynchttpclient', name: 'async-http-client', version: '+'
    force group: 'org.springframework.boot', name: 'spring-boot-starter-webflux', version: '+'
    force group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '+'
    force group: 'org.springframework.boot', name: 'spring-boot-starter-reactor-netty', version: '+'
  }
}

testJava8Only += '**/*Test.class'
testJava8Only += '**/*TestApplication.class'
