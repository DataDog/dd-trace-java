muzzle {
  pass {
    name = 'jetty-websocket-12ee8'
    group = 'org.eclipse.jetty.ee8.websocket'
    module = 'jetty-ee8-websocket-javax-server'
    versions = "[12,12.0.17]"
    javaVersion = "17"
  }
  pass {
    name = 'jetty-websocket-12ee9'
    group = 'org.eclipse.jetty.ee9.websocket'
    module = 'jetty-ee9-websocket-jakarta-server'
    versions = "[12,12.0.17]"
    javaVersion = "17"
  }
  pass {
    name = 'jetty-websocket-12ee10'
    group = 'org.eclipse.jetty.ee10.websocket'
    module = 'jetty-ee10-websocket-jakarta-server'
    versions = "[12,12.0.17]"
    javaVersion = "17"
  }
}

apply from: "$rootDir/gradle/java.gradle"

testJvmConstraints {
  minJavaVersion = JavaVersion.VERSION_17
}

addTestSuiteForDir("latestDepTest", "test")

["compileTestGroovy", "compileLatestDepTestGroovy"].each { name ->
  tasks.named(name, GroovyCompile) {
    configureCompiler(it, 17)
  }
}

dependencies {
  implementation project(":dd-java-agent:instrumentation:websocket:jetty-websocket:jetty-websocket-10")
  testImplementation group: 'org.eclipse.jetty.ee8.websocket', name: 'jetty-ee8-websocket-javax-server', version: '12.0.0'
  testImplementation group: 'org.eclipse.jetty.ee9.websocket', name: 'jetty-ee9-websocket-jakarta-server', version: '12.0.0'
  testImplementation group: 'org.eclipse.jetty.ee10.websocket', name: 'jetty-ee10-websocket-jakarta-server', version: '12.0.0'
  //TODO: jetty-12.1.0 is still alpha but wraps MethodHandle class into a MethodHolder class.
  // Today that is not stable but we'll need to port those advices to support that once the code base will be a bit more stable
  latestDepTestImplementation group: 'org.eclipse.jetty.ee8.websocket', name: 'jetty-ee8-websocket-javax-server', version: '12.0.17'
  latestDepTestImplementation group: 'org.eclipse.jetty.ee9.websocket', name: 'jetty-ee9-websocket-jakarta-server', version: '12.0.17'
  latestDepTestImplementation group: 'org.eclipse.jetty.ee10.websocket', name: 'jetty-ee10-websocket-jakarta-server', version: '12.0.17'

  testRuntimeOnly project(":dd-java-agent:instrumentation:websocket:jetty-websocket:jetty-websocket-10")
  testRuntimeOnly project(":dd-java-agent:instrumentation:websocket:jetty-websocket:jetty-websocket-11")
  testRuntimeOnly project(":dd-java-agent:instrumentation:websocket:javax-websocket-1.0")
  testRuntimeOnly project(":dd-java-agent:instrumentation:websocket:jakarta-websocket-2.0")

  configurations.configureEach {
    it.resolutionStrategy {
      force libs.slf4j
    }
  }
}
