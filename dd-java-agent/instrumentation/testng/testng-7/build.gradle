plugins {
  id 'java-test-fixtures'
}

ext {
  // testng 7.6.0+ requires Java 11 or higher.
  latestDepTestMinJavaVersionForTests = JavaVersion.VERSION_11
}

apply from: "$rootDir/gradle/java.gradle"

muzzle {
  pass {
    group = 'org.testng'
    module = 'testng'
    versions = '[7.0.0,7.6)'
    skipVersions += "7.1.0" //  depends on non-existent guice-4.1.0-no_aop
  }
  // TestNG 7.6+ is compiled with Java 11
  pass {
    group = 'org.testng'
    module = 'testng'
    versions = '[7.6,)'
    javaVersion = 11
  }
}

addTestSuiteForDir('latestDepTest', 'test')
// testng 7.5.1 is the latest version compatible with Java 8
addTestSuiteForDir('testng751Test', 'test')

dependencies {
  compileOnly group: 'org.testng', name: 'testng', version: '7.0.0'

  implementation project(':dd-java-agent:instrumentation:testng')

  testImplementation testFixtures(project(':dd-java-agent:instrumentation:testng'))
  testImplementation group: 'org.testng', name: 'testng', version: '7.0.0'
  testng751TestImplementation group: 'org.testng', name: 'testng', version: '7.5.1'
  latestDepTestImplementation group: 'org.testng', name: 'testng', version: '+'
}

// gradle can't downgrade the testng dependencies with `strictly`
// and IntelliJ IDEA reports an error when importing the project
configurations.matching({ it.name.startsWith('test') }).configureEach({
  it.resolutionStrategy {
    force group: 'org.testng', name: 'testng', version: '7.0.0'
  }
})

