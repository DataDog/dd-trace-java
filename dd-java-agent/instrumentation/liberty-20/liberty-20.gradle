apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  latestDepTest {
    dirName = 'test'
  }
}

configurations {
  zipped
}

dependencies {
  compileOnly group: 'javax.servlet', name: 'javax.servlet-api', version: '3.0.1'
  zipped group: 'io.openliberty', name: 'openliberty-runtime', version: '21.0.0.3', ext: 'zip'
  compileOnly files({ tasks.installOpenLibertyDeps.extractedJars })
}

//unzips the dependencies from the 'zipped' configuration so 'compileOnly' can reference it
task installOpenLibertyDeps(type: Sync) {
  def extractDir = "${buildDir}/openliberty-jars"
  ext.extractedJars = fileTree(extractDir) {
    include "**/*.jar"
    builtBy "installOpenLibertyDeps"
  }
  dependsOn configurations.zipped
  from {
    configurations.zipped.collect { zipTree(it) }
  }
  into extractDir
}


