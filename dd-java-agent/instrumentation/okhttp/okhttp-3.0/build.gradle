muzzle {
  pass {
    group = "com.squareup.okhttp3"
    module = "okhttp"
    versions = "[3.0,)"
    assertInverse = true
  }
}

apply from: "$rootDir/gradle/java.gradle"

addTestSuiteForDir('latestDepTest', 'test')

dependencies {
  compileOnly(group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.0.0')

  testImplementation(group: 'com.squareup.okhttp3', name: 'okhttp') {
    version {
      strictly '[3.0.0,4]'
      prefer '3.0.0'
    }
  }
  testImplementation(group: 'com.squareup.okio', name: 'okio') {
    version {
      strictly '[1.6.0,2]'
      prefer '1.6.0'
    }
  }
  // Downgrade moshi to a previous version as moshi 1.2.0+ requires okio 1.8.0+
  testImplementation('com.squareup.moshi:moshi') {
    version {
      strictly '1.1.0'
    }
  }

  latestDepTestImplementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '[3.11.0, 4)'
  latestDepTestImplementation group: 'com.squareup.okio', name: 'okio', version: '1.+'

  testRuntimeOnly(project(':dd-java-agent:instrumentation:iast-instrumenter'))
  testRuntimeOnly(project(':dd-java-agent:instrumentation:java:java-net:java-net-1.8'))
}
