apply from: "$rootDir/gradle/java.gradle"

muzzle {
  pass {
    group = 'com.intuit.karate'
    module = 'karate-core'
    versions = '[1.0.0,1.4.0)'
  }
  // Karate 1.4.0+ is compiled with Java 11
  pass {
    group = 'com.intuit.karate'
    module = 'karate-core'
    versions = '[1.4.0,)'
    javaVersion = 11
  }
}

addTestSuiteForDir('latestDepTest', 'test')

dependencies {
  compileOnly group: 'com.intuit.karate', name: 'karate-core', version: '1.0.0'

  testImplementation testFixtures(project(':dd-java-agent:agent-ci-visibility'))
  testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.8.2'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.2'

  testImplementation (group: 'com.intuit.karate', name: 'karate-core', version: '1.0.0') {
    // excluding logback to avoid conflicts with deps.testLogging
    exclude group: 'ch.qos.logback', module: 'logback-classic'
  }
  testImplementation (group: 'com.intuit.karate', name: 'karate-junit5', version: '1.0.0') {
    // excluding logback to avoid conflicts with deps.testLogging
    exclude group: 'ch.qos.logback', module: 'logback-classic'
  }

  latestDepTestImplementation (group: 'com.intuit.karate', name: 'karate-core', version: '+') {
    // excluding logback to avoid conflicts with deps.testLogging
    exclude group: 'ch.qos.logback', module: 'logback-classic'
  }
  latestDepTestImplementation (group: 'com.intuit.karate', name: 'karate-junit5', version: '+') {
    // excluding logback to avoid conflicts with deps.testLogging
    exclude group: 'ch.qos.logback', module: 'logback-classic'
  }
}

// Using recommended Karate project layout where Karate feature files sit in same /test/java folders as their java counterparts
sourceSets {
  test {
    resources {
      srcDir file('src/test/java')
      exclude '**/*.java'
    }
  }
  latestDepTest {
    resources {
      srcDir file('src/test/java')
      exclude '**/*.java'
    }
  }
}

configurations.matching({ it.name.startsWith('latestDepTest') }).each({
  it.resolutionStrategy {
    // Karate 1.4.0+ is compiled with Java 11
    force group: 'com.intuit.karate', name: 'karate-core', version: (JavaVersion.current().java11Compatible ? '+' : '1.3.1')
    force group: 'com.intuit.karate', name: 'karate-junit5', version: (JavaVersion.current().java11Compatible ? '+' : '1.3.1')
  }
})
