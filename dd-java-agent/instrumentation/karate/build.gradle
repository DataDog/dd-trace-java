plugins {
  id 'dd-trace-java.instrumentation.testing-framework-tests'
}

apply from: "$rootDir/gradle/java.gradle"

muzzle {
  pass {
    group = 'com.intuit.karate'
    module = 'karate-core'
    versions = '[1.0.0,1.4.0)'
  }
  // Karate 1.4.0+ is compiled with Java 11
  pass {
    group = 'com.intuit.karate'
    module = 'karate-core'
    versions = '[1.4.0,)'
    javaVersion = 11
  }
}

addTestSuiteForDir('latestDepTest', 'test')
// karate 1.3.1 is the last version supporting Java 8.
addTestSuiteForDir('karate131Test', 'test')

dependencies {
  compileOnly group: 'com.intuit.karate', name: 'karate-core', version: '1.0.0'

  testImplementation project(':dd-java-agent:agent-ci-visibility:civisibility-instrumentation-test-fixtures')
  testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: libs.versions.junit.platform.get()
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: libs.versions.junit5.get()

  testImplementation (group: 'com.intuit.karate', name: 'karate-core', version: '1.0.0') {
    // excluding logback to avoid conflicts with libs.bundles.test.logging
    exclude group: 'ch.qos.logback', module: 'logback-classic'
  }
  testImplementation (group: 'com.intuit.karate', name: 'karate-junit5', version: '1.0.0') {
    // excluding logback to avoid conflicts with libs.bundles.test.logging
    exclude group: 'ch.qos.logback', module: 'logback-classic'
  }

  karate131TestImplementation (group: 'com.intuit.karate', name: 'karate-core', version: '1.3.1') {
    // excluding logback to avoid conflicts with libs.bundles.test.logging
    exclude group: 'ch.qos.logback', module: 'logback-classic'
  }
  karate131TestImplementation (group: 'com.intuit.karate', name: 'karate-junit5', version: '1.3.1') {
    // excluding logback to avoid conflicts with libs.bundles.test.logging
    exclude group: 'ch.qos.logback', module: 'logback-classic'
  }

  latestDepTestImplementation (group: 'com.intuit.karate', name: 'karate-core', version: '+') {
    // excluding logback to avoid conflicts with libs.bundles.test.logging
    exclude group: 'ch.qos.logback', module: 'logback-classic'
  }
  latestDepTestImplementation (group: 'com.intuit.karate', name: 'karate-junit5', version: '+') {
    // excluding logback to avoid conflicts with libs.bundles.test.logging
    exclude group: 'ch.qos.logback', module: 'logback-classic'
  }
}

// Using recommended Karate project layout where Karate feature files sit in same /test/java folders as their java counterparts
sourceSets {
  test {
    resources {
      srcDir file('src/test/java')
      exclude '**/*.java'
    }
  }
  karate131Test {
    resources {
      srcDir file('src/test/java')
      exclude '**/*.java'
    }
  }
  latestDepTest {
    resources {
      srcDir file('src/test/java')
      exclude '**/*.java'
    }
  }
}

tasks.named("compileLatestDepTestJava", JavaCompile) {
  configureCompiler(it, 11, JavaVersion.VERSION_1_8)
}

// Latest Karate 1.4.0+ versions require Java 11
tasks.named("latestDepTest", Test) {
  onlyIf { javaVersion.isJava11Compatible() }
}
