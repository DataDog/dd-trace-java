apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'idea'

testJvmConstraints {
  minJavaVersion = JavaVersion.VERSION_17
}

muzzle {
  pass {
    group = 'io.github.resilience4j'
    module = 'resilience4j-reactor'
    versions = '[2.0.0,)'
    assertInverse = true
    javaVersion = "17"
  }
}

idea {
  module {
    jdkName = '17'
  }
}

// Set all compile tasks to use JDK17 but let instrumentation code target 1.8 compatibility
project.tasks.withType(AbstractCompile).configureEach {
  configureCompiler(
    it,
    17,
    JavaVersion.VERSION_1_8,
    "Set all compile tasks to use JDK17 but let instrumentation code target 1.8 compatibility"
    )
}

addTestSuiteForDir('latestDepTest', 'test')

dependencies {
  implementation project(':dd-java-agent:instrumentation:resilience4j:resilience4j-2.0')

  compileOnly group: 'io.github.resilience4j', name: 'resilience4j-all', version: '2.0.0'
  testImplementation group: 'io.github.resilience4j', name: 'resilience4j-all', version: '2.0.0'
  latestDepTestImplementation group: 'io.github.resilience4j', name: 'resilience4j-all', version: '2.+'

  compileOnly group: 'io.github.resilience4j', name: 'resilience4j-reactor', version: '2.0.0'
  testImplementation group: 'io.github.resilience4j', name: 'resilience4j-reactor', version: '2.0.0'
  latestDepTestImplementation group: 'io.github.resilience4j', name: 'resilience4j-reactor', version: '2.+'

  // Include other instruments that we rely on and that must not conflict with each other
  testImplementation project(':dd-java-agent:instrumentation:reactor-core-3.1')
  testImplementation project(':dd-java-agent:instrumentation:reactive-streams-1.0')
  testImplementation project(':dd-java-agent:instrumentation:java:java-concurrent:java-concurrent-1.8')
}
