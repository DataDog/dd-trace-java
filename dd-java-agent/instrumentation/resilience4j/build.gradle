ext {
  minJavaVersionForTests = JavaVersion.VERSION_17 // TODO -PtestJvm=17
}

apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'idea'

muzzle {
  pass {
    group = 'io.github.resilience4j'
    module = 'resilience4j-all'
    versions = '[2.0.0,)' //TODO there are plenty of customers still using 1.x, primarily 1.7. Perhaps main reason is that 2.x is Java 17 only
    assertInverse = false // FIXME make sure muzzle fails for (,2.0.0)
    javaVersion = "17"
  }
}

idea {
  module {
    jdkName = '17'
  }
}

// Set all compile tasks to use JDK17 but let instrumentation code targets 1.8 compatibility
project.tasks.withType(AbstractCompile).configureEach {
  setJavaVersion(it, 17)
}
compileJava.configure {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

addTestSuiteForDir('latestDepTest', 'test')

dependencies {
  compileOnly group: 'io.github.resilience4j', name: 'resilience4j-all', version: '2.0.0'

  testImplementation group: 'io.github.resilience4j', name: 'resilience4j-all', version: '2.0.0'
  latestDepTestImplementation group: 'io.github.resilience4j', name: 'resilience4j-all', version: '2.+'

  // TODO extract r4j-reactor to a separate module
  compileOnly group: 'io.github.resilience4j', name: 'resilience4j-reactor', version: '2.0.0'

  testImplementation group: 'io.projectreactor', name: 'reactor-core', version: '3.1.0.RELEASE'
  testImplementation group: 'io.github.resilience4j', name: 'resilience4j-reactor', version: '2.0.0'

  // include other instrumentations that we rely upon
  testImplementation project(':dd-java-agent:instrumentation:reactor-core-3.1')
  testImplementation project(':dd-java-agent:instrumentation:reactive-streams')
  testImplementation project(':dd-java-agent:instrumentation:java-concurrent') // ???


}
