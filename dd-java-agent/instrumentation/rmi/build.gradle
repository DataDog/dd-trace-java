ext {
  // TODO Java 17: The necessary packages are not opened on Java 17
  maxJavaVersionForTests = JavaVersion.VERSION_15
}

muzzle {
  pass {
    coreJdk()
  }
}

apply from: "$rootDir/gradle/java.gradle"

tasks.withType(JavaCompile).configureEach {
  // need access to sun.rmi package
  setJavaVersion(it, 8)
}

def rmic = tasks.register('rmic', Exec) {
  dependsOn testClasses

  Provider<JavaLauncher> javaLauncher = getJavaLauncherFor(8)
  def toolchainPath = javaLauncher.get().metadata.installationPath

  def clazz = 'rmi.app.ServerLegacy'
  commandLine(
    toolchainPath.file("bin/rmic").toString(),
    "-g",
    "-keep",
    "-classpath", sourceSets.test.output.classesDirs.asPath,
    "-d", layout.buildDirectory.dir("classes/java/test").get().toString(),
    clazz
    )
}

tasks.named("test").configure {
  dependsOn rmic
}
