muzzle {
  pass {
    coreJdk()
  }
}

apply from: "$rootDir/gradle/java.gradle"

testJvmConstraint {
  // TODO Java 17: The necessary packages are not opened on Java 17
  maxJavaVersion = JavaVersion.VERSION_15
}

tasks.withType(JavaCompile).configureEach {
  configureCompiler(it, 8, JavaVersion.VERSION_1_8, "Need access to sun.rmi package")
}

def rmic = tasks.register('rmic', Exec) {
  dependsOn 'testClasses'

  Provider<JavaLauncher> javaLauncher = getJavaLauncherFor(8)
  def toolchainPath = javaLauncher.get().metadata.installationPath

  def clazz = 'rmi.app.ServerLegacy'
  commandLine(
    toolchainPath.file("bin/rmic").toString(),
    "-g",
    "-keep",
    "-classpath", sourceSets.test.output.classesDirs.asPath,
    "-d", layout.buildDirectory.dir("classes/java/test").get().toString(),
    clazz
    )
}

tasks.named("test", Test) {
  dependsOn rmic
}
