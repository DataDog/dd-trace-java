plugins {
  id 'datadog.instrumentation.testing-framework-tests'
}

apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'scala'

muzzle {
  pass {
    group = 'com.disneystreaming'
    module = 'weaver-cats_3'
    versions = '[0.8.4,)'
  }

  pass {
    group = 'org.typelevel'
    module = 'weaver-cats_3'
    versions = '[0.9.0,)'
  }
}

addTestSuiteForDir('latestDepTest', 'latestDepTest')
// weaver 0.8.4 is the earliest supported version before the org migration
addTestSuiteForDir('weaver084Test', 'test')

dependencies {
  compileOnly group: 'org.typelevel', name: 'weaver-cats_3', version: '0.9.0'
  // Weaver is published for Scala > 2.12
  compileOnly group: 'org.scala-lang', name: 'scala-library', version: '2.12.20'

  testImplementation project(':dd-java-agent:agent-ci-visibility:civisibility-instrumentation-test-fixtures')

  testImplementation group: 'org.scala-lang', name: 'scala-library', version: '2.12.20'
  testImplementation group: 'org.typelevel', name: 'weaver-cats_3', version: '0.9.0'
  latestDepTestImplementation group: 'org.typelevel', name: 'weaver-cats_3', version: '+'
  weaver084TestImplementation group: 'com.disneystreaming', name: 'weaver-cats_3', version: '0.8.4'
}

tasks.named("compileTestGroovy", GroovyCompile) {
  dependsOn 'compileTestScala'
  classpath += files(sourceSets.test.scala.destinationDirectory)
}

tasks.named("compileLatestDepTestGroovy", GroovyCompile) {
  dependsOn 'compileLatestDepTestScala'
  classpath += files(sourceSets.latestDepTest.scala.destinationDirectory)
}

tasks.named("compileWeaver084TestGroovy", GroovyCompile) {
  dependsOn 'compileWeaver084TestScala'
  classpath += files(sourceSets.weaver084Test.scala.destinationDirectory)
}
