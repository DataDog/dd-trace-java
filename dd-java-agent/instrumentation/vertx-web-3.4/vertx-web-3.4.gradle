// Set properties before any plugins get loaded
ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

apply from: "$rootDir/gradle/java.gradle"

apply plugin: 'org.unbroken-dome.test-sets'

muzzle {
  pass {
    group = 'io.vertx'
    module = "vertx-web"
    versions = "[3.4.0,4)"
    assertInverse = true
  }
}

testSets {
  latestDepTest {
    dirName = 'test'
  }
}

configurations {
  testArtifacts
}

// Create test artifacts so vertx-rx can reuse the server test instrumentation and base class
artifacts {
  testArtifacts testJar
}

dependencies {
  main_java8CompileOnly group: 'io.vertx', name: 'vertx-web', version: '3.4.0'

  testCompile project(':dd-java-agent:instrumentation:netty-4.1')
  testCompile project(':dd-java-agent:instrumentation:trace-annotation')

  testCompile group: 'io.vertx', name: 'vertx-web', version: '3.4.0'
  testCompile group: 'io.vertx', name: 'vertx-web-client', version: '3.4.0'

  latestDepTestCompile group: 'io.vertx', name: 'vertx-web', version: '3.+'
  latestDepTestCompile group: 'io.vertx', name: 'vertx-web-client', version: '3.+'
}
