// Set properties before any plugins get loaded
ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

// building against 2.3 and testing against 2.4 because JettyHandler is available since 2.4 only
muzzle {
  pass {
    group = "com.sparkjava"
    module = 'spark-core'
    versions = "[2.3,)"
    assertInverse = true
  }
}

apply from: "${rootDir}/gradle/java.gradle"

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  latestDepTest {
    dirName = 'test'
  }
}

latestDepTest {
  sourceCompatibility = "1.8"
  targetCompatibility = "1.8"
}

compileTestJava {
  sourceCompatibility = "1.8"
  targetCompatibility = "1.8"
}

compileJava {
  sourceCompatibility = "1.7"
  targetCompatibility = "1.7"
}

dependencies {
  compileOnly group: 'com.sparkjava', name: 'spark-core', version: '2.3'

  compile project(':dd-java-agent:agent-tooling')
  compile deps.bytebuddy
  compile deps.opentracing
  annotationProcessor deps.autoservice
  implementation deps.autoservice

  testCompile project(':dd-java-agent:instrumentation:jetty-8')
  testCompile project(':dd-java-agent:testing')

  testCompile group: 'com.sparkjava', name: 'spark-core', version: '2.4'

  latestDepTestCompile group: 'com.sparkjava', name: 'spark-core', version: '+'
}
