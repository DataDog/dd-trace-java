
apply from: "$rootDir/gradle/java.gradle"

muzzle {
  pass {
    group = 'io.vertx'
    module = 'vertx-pg-client'
    // 4.1.1 is the first version where PgConnectionFactory is public
    // see https://github.com/eclipse-vertx/vertx-sql-client/pull/986
    versions = '[4.1.1,5)'
    assertInverse = true
  }
}

addTestSuiteForDir('latestDepTest', 'test')
addTestSuiteExtendingForDir('latestDepForkedTest', 'latestDepTest', 'test')

apply from: "$rootDir/gradle/configure_tests.gradle"

latestDepTest {
  finalizedBy 'latestDepForkedTest'
}

dependencies {
  compileOnly group: 'io.vertx', name: 'vertx-pg-client', version: '4.1.1'

  testImplementation group: 'io.vertx', name: 'vertx-pg-client', version: '4.1.1'
  testImplementation project(':dd-java-agent:instrumentation:vertx-sql-common')

  // This is needed for the test container to start
  testImplementation group: 'com.ongres.scram', name: 'client', version: '2.1'
  testImplementation group: 'org.postgresql', name: 'postgresql', version: '42.7.4'
  testImplementation group: 'org.testcontainers', name: 'postgresql', version: libs.versions.testcontainers.get()

  latestDepTestImplementation group: 'io.vertx', name: 'vertx-pg-client', version: '4.+'
}

tasks.withType(Test).configureEach {
  usesService(testcontainersLimit)
}
