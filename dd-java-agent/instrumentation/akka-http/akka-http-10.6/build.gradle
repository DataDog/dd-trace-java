// Set properties before any plugins get loaded
ext {
  minJavaVersionForTests = JavaVersion.VERSION_11
}

apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'scala'
apply plugin: 'call-site-instrumentation'


muzzle {
  pass {
    group = 'com.typesafe.akka'
    module = 'akka-http_2.13'
    versions = "[10.6.0,)"
    javaVersion = "11"
    extraDependency 'com.typesafe.akka:akka-stream_2.13:2.9.0'
    extraRepository('akka', 'https://repo.akka.io/maven')
    assertInverse = true
  }

}

repositories {
  maven {
    url "https://repo.akka.io/maven"
  }
}

addTestSuiteForDir('latestDepTest', 'test')

[compileMain_java11Java, compileTestScala, compileLatestDepTestScala].each {
  it.configure {
    setJavaVersion(it, 11)
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }
}

compileTestGroovy {
  javaLauncher = getJavaLauncherFor(11)
  dependsOn compileTestScala
  classpath += files(compileTestScala.destinationDirectory)
}

compileLatestDepTestGroovy {
  javaLauncher = getJavaLauncherFor(11)
  dependsOn compileLatestDepTestScala
  classpath += files(compileLatestDepTestScala.destinationDirectory)
}

dependencies {
  main_java11CompileOnly libs.scala213
  main_java11CompileOnly group: 'com.typesafe.akka', name: 'akka-http_2.13', version: '10.6.0'
  main_java11CompileOnly group: 'com.typesafe.akka', name: 'akka-actor_2.13', version: '2.9.0'

  //testImplementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.6.0'
  testImplementation group: 'com.typesafe.akka', name: 'akka-http_2.13', version: '10.6.0'
  testImplementation group: 'com.typesafe.akka', name: 'akka-stream_2.13', version: '2.9.0'
  testImplementation group: 'com.typesafe.akka', name: 'akka-http-jackson_2.13', version: '10.6.0'
  testImplementation group: 'com.typesafe.akka', name: 'akka-http-spray-json_2.13', version: '10.6.0'
  testImplementation libs.scala213
  testImplementation project(':dd-java-agent:instrumentation:trace-annotation')
  testImplementation project(':dd-java-agent:instrumentation:akka-concurrent')
  testImplementation project(':dd-java-agent:instrumentation:scala-concurrent')
  testImplementation project(':dd-java-agent:instrumentation:akka-http:akka-http-10.0')
  testImplementation project(':dd-java-agent:instrumentation:scala-promise:scala-promise-2.13')

  latestDepTestImplementation group: 'com.typesafe.akka', name: 'akka-http_2.13', version: '+'
  latestDepTestImplementation group: 'com.typesafe.akka', name: 'akka-stream_2.13', version: '+'
  latestDepTestImplementation group: 'com.typesafe.akka', name: 'akka-pki_2.13', version: '+'
  latestDepTestImplementation group: 'com.typesafe.akka', name: 'akka-protobuf-v3_2.13', version: '+'
  latestDepTestImplementation group: 'com.typesafe.akka', name: 'akka-http-jackson_2.13', version: '+'
  latestDepTestImplementation group: 'com.typesafe.akka', name: 'akka-http-spray-json_2.13', version: '+'
  latestDepTestImplementation group: 'org.scala-lang.modules', name: 'scala-java8-compat_2.13', version: '1.0.+'
}

configurations.getByName("latestDepTestRuntimeClasspath").resolutionStrategy {
  it.force libs.slf4j
}
