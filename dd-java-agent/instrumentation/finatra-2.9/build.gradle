
apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/test-with-scala.gradle"

// Up to 20.7 where the server structure changes
addTestSuiteForDir('latestPre207Test', 'test')
addTestSuite('latestDepTest')

muzzle {
  // There are some weird library issues below 2.9 so can't assert inverse
  pass {
    group = 'com.twitter'
    module = 'finatra-http_2.11'
    versions = '[2.9.0,]'
  }

  pass {
    group = 'com.twitter'
    module = 'finatra-http_2.12'
    versions = '[2.9.0,]'
  }
}

dependencies {
  compileOnly group: 'com.twitter', name: 'finatra-http_2.11', version: '2.9.0'

  testImplementation project(':dd-java-agent:instrumentation:netty:netty-4.1')

  testImplementation group: 'com.twitter', name: 'finatra-http_2.11', version: '19.12.0'
  // Required for older versions of finatra on JDKs >= 11
  testImplementation group: 'com.sun.activation', name: 'javax.activation', version: '1.2.0'

  latestPre207TestImplementation group: 'com.twitter', name: 'finatra-http_2.11', version: '[,20.7.0)'

  latestDepTestImplementation group: 'com.twitter', name: 'finatra-http_2.11', version: '+'
}

tasks.named("compileLatestDepTestGroovy", GroovyCompile) {
  classpath += files(tasks.named('compileLatestDepTestScala').map { it.destinationDirectory })
  dependsOn "compileLatestDepTestScala"
}

tasks.named("compileLatestPre207TestGroovy", GroovyCompile) {
  classpath += files(tasks.named('compileLatestPre207TestScala').map { it.destinationDirectory })
  dependsOn "compileLatestPre207TestScala"
}

tasks.named("latestDepTest", Test) {
  finalizedBy 'latestPre207Test'
}
