
apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/test-with-scala.gradle"

// Up to 20.7 where the server structure changes
addTestSuiteForDir('latestPre207Test', 'test')
addTestSuite('latestDepTest')

muzzle {
  // There are some weird library issues below 2.9 so can't assert inverse
  pass {
    group = 'com.twitter'
    module = 'finatra-http_2.11'
    versions = '[2.9.0,]'
  }

  pass {
    group = 'com.twitter'
    module = 'finatra-http_2.12'
    versions = '[2.9.0,]'
  }
}

dependencies {
  compileOnly group: 'com.twitter', name: 'finatra-http_2.11', version: '2.9.0'

  testImplementation project(':dd-java-agent:instrumentation:netty-4.1')

  testImplementation group: 'com.twitter', name: 'finatra-http_2.11', version: '19.12.0'
  testImplementation(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.10')
  // Required for older versions of finatra on JDKs >= 11
  testImplementation group: 'com.sun.activation', name: 'javax.activation', version: '1.2.0'

  latestPre207TestImplementation group: 'com.twitter', name: 'finatra-http_2.11', version: '[,20.7.0)'

  latestDepTestImplementation group: 'com.twitter', name: 'finatra-http_2.11', version: '+'
  latestDepTestImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.0'
}

tasks.named("compileLatestDepTestGroovy").configure {
  classpath = classpath.plus(files(compileLatestDepTestScala.destinationDirectory))
  dependsOn "compileLatestDepTestScala"
}

tasks.named("compileLatestPre207TestGroovy").configure {
  classpath = classpath.plus(files(compileLatestPre207TestScala.destinationDirectory))
  dependsOn "compileLatestPre207TestScala"
}

tasks.named("latestDepTest").configure {
  finalizedBy latestPre207Test
}
