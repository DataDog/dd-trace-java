ext {
  // Play doesn't work with Java 9+ until 2.6.12
  maxJavaVersionForTests = JavaVersion.VERSION_1_8
}

def scalaVersion = '2.11'
def playVersion = '2.6.0'

muzzle {
  pass {
    group = 'com.typesafe.play'
    module = "play_$scalaVersion"
    versions = "[$playVersion,)"
    assertInverse = true
  }
  pass {
    group = 'com.typesafe.play'
    module = 'play_2.12'
    versions = "[$playVersion,)"
    assertInverse = true
  }
  pass {
    group = 'com.typesafe.play'
    module = 'play_2.13'
    versions = "[$playVersion,)"
    assertInverse = true
  }
}

apply from: "$rootDir/gradle/java.gradle"

repositories {
  maven {
    // for muzzle because of play-2.3
    // only place that has 'com.typesafe.netty:netty-http-pipelining:1.1.2' publicly accessible
    name 'typesafe'
    url 'https://repo.typesafe.com/typesafe/maven-releases/'
  }
}

addTestSuiteForDir('latestDepTest', 'test')

dependencies {
  compileOnly group: 'com.typesafe.play', name: "play_$scalaVersion", version: playVersion

  testImplementation project(':dd-java-agent:instrumentation:netty-4.0')
  testImplementation project(':dd-java-agent:instrumentation:netty-4.1')
  testImplementation project(':dd-java-agent:instrumentation:akka-http-10.0')
  testImplementation project(':dd-java-agent:instrumentation:akka-concurrent')
  testImplementation project(':dd-java-agent:instrumentation:akka-init')
  testImplementation project(':dd-java-agent:instrumentation:scala-concurrent')
  testImplementation project(':dd-java-agent:instrumentation:scala-promise:scala-promise-2.10')
  testImplementation project(':dd-java-agent:instrumentation:scala-promise:scala-promise-2.13')
  testImplementation(project(path: ':dd-java-agent:instrumentation:akka-http-10.0', configuration: 'testArtifacts'))

  testImplementation group: 'com.typesafe.play', name: "play-java_$scalaVersion", version: playVersion
  // TODO: Play WS is a separately versioned library starting with 2.6 and needs separate instrumentation.
  testImplementation(group: 'com.typesafe.play', name: "play-test_$scalaVersion", version: playVersion) {
    exclude group: 'org.eclipse.jetty.websocket', module: 'websocket-client'
  }

  // TODO: This should be changed to the latest in scala 2.13 instead of 2.11 since its ahead
  latestDepTestImplementation group: 'com.typesafe.play', name: "play-java_$scalaVersion", version: '2.+'
  latestDepTestImplementation(group: 'com.typesafe.play', name: "play-test_$scalaVersion", version: '2.+') {
    exclude group: 'org.eclipse.jetty.websocket', module: 'websocket-client'
  }
}
