muzzle {
  pass {
    name = "since-0.11"
    group = "org.apache.kafka"
    module = "kafka-clients"
    versions = "[0.11.0.0,)"
    assertInverse = true
  }
  pass {
    name = "before-3.8"
    group = "org.apache.kafka"
    module = "kafka-clients"
    versions = "[0.11.0.0,3.8.0),[5,7.8.0)"
    assertInverse = true
  }
}

apply from: "$rootDir/gradle/java.gradle"

addTestSuite('latestDepTest')
addTestSuite('iastLatestDepTest3')

dependencies {
  compileOnly group: 'org.apache.kafka', name: 'kafka-clients', version: '0.11.0.0'
  implementation project(':dd-java-agent:instrumentation:kafka:kafka-common')

  testImplementation group: 'org.apache.kafka', name: 'kafka-clients', version: '0.11.0.0'
  testImplementation group: 'org.springframework.kafka', name: 'spring-kafka', version: '1.3.3.RELEASE'
  testImplementation group: 'org.springframework.kafka', name: 'spring-kafka-test', version: '1.3.3.RELEASE'
  testImplementation group: 'io.projectreactor.kafka', name: 'reactor-kafka', version: '1.0.0.RELEASE'
  testImplementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.2.3'
  testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.19.0'
  testRuntimeOnly group: 'org.assertj', name: 'assertj-core', version: '2.9.+'
  testRuntimeOnly project(':dd-java-agent:instrumentation:spring:spring-scheduling-3.1')
  testRuntimeOnly project(':dd-java-agent:instrumentation:reactor-core-3.1')
  testRuntimeOnly project(':dd-java-agent:instrumentation:reactive-streams')
  testImplementation project(':dd-java-agent:agent-iast:iast-test-fixtures')

  // IAST testing dependencies
  testRuntimeOnly project(':dd-java-agent:instrumentation:iast-instrumenter')
  testRuntimeOnly project(':dd-java-agent:instrumentation:java:java-lang:java-lang-1.8')
  testRuntimeOnly project(':dd-java-agent:instrumentation:java:java-io-1.8')
  testRuntimeOnly project(':dd-java-agent:instrumentation:jackson-core')
  testRuntimeOnly project(':dd-java-agent:instrumentation:jackson-core:jackson-core-2.8')
  testImplementation(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.4')

  // This module still needs Spock with JUnit4.
  testImplementation(libs.spock.junit4.groovy4)

  // Include latest version of kafka itself along with latest version of client libs.
  latestDepTestImplementation group: 'org.apache.kafka', name: 'kafka-clients', version: '2.+'
  // latest depending to kafka client 2.x -> to be fixed when this instrumentation will test 3.x as well
  latestDepTestImplementation group: 'org.springframework.kafka', name: 'spring-kafka', version: '2.+'
  latestDepTestImplementation group: 'org.springframework.kafka', name: 'spring-kafka-test', version: '2.+'
  latestDepTestImplementation libs.guava

  // Add kafka version 3.x for IAST
  iastLatestDepTest3Implementation group: 'org.apache.kafka', name: 'kafka-clients', version: '3.+'
  iastLatestDepTest3Implementation group: 'org.springframework.kafka', name: 'spring-kafka', version: '3.+'
  iastLatestDepTest3RuntimeOnly project(':dd-java-agent:instrumentation:iast-instrumenter')
  iastLatestDepTest3RuntimeOnly project(':dd-java-agent:instrumentation:java:java-lang:java-lang-1.8')
  iastLatestDepTest3RuntimeOnly project(':dd-java-agent:instrumentation:java:java-io-1.8')
  iastLatestDepTest3RuntimeOnly project(':dd-java-agent:instrumentation:jackson-core')
  iastLatestDepTest3RuntimeOnly project(':dd-java-agent:instrumentation:jackson-core:jackson-core-2.12')
  iastLatestDepTest3Implementation(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.15.4')
  iastLatestDepTest3Implementation project(':dd-java-agent:agent-iast:iast-test-fixtures')
}

tasks.named("iastLatestDepTest3", Test) {
  javaLauncher = getJavaLauncherFor(17)
  jvmArgs = ['--add-opens', 'java.base/java.util=ALL-UNNAMED']
}


