ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
  // Play doesn't work with Java 9+ until 2.6.12
  maxJavaVersionForTests = JavaVersion.VERSION_1_8
}

muzzle {
  pass {
    group = 'com.typesafe.play'
    module = 'play_2.11'
    versions = '[2.4.0,2.6)'
    assertInverse = true
  }
  fail {
    group = 'com.typesafe.play'
    module = 'play_2.12'
    versions = '[,]'
  }
  fail {
    group = 'com.typesafe.play'
    module = 'play_2.13'
    versions = '[,]'
  }
}

apply from: "$rootDir/gradle/java.gradle"
repositories {
  jcenter() // only place that has 'com.typesafe.netty:netty-http-pipelining:1.1.2' publically accessible (muzzle)
}

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  latestDepTest {
    dirName = 'test'
  }
}

dependencies {
  main_java8Implementation group: 'com.typesafe.play', name: 'play_2.11', version: '2.4.0'

  testImplementation project(':dd-java-agent:instrumentation:netty-4.0')
  testImplementation project(':dd-java-agent:instrumentation:netty-4.1')
  testImplementation project(':dd-java-agent:instrumentation:akka-http-10.0')
  testImplementation project(':dd-java-agent:instrumentation:akka-concurrent')
  testImplementation project(':dd-java-agent:instrumentation:akka-init')
  testImplementation project(':dd-java-agent:instrumentation:scala-concurrent')
  testImplementation project(':dd-java-agent:instrumentation:scala-promise:scala-promise-2.10')
  testImplementation project(':dd-java-agent:instrumentation:scala-promise:scala-promise-2.13')

  // Before 2.5, play used netty 3.x which isn't supported, so for better test consistency, we test with just 2.5
  testImplementation group: 'com.typesafe.play', name: 'play-java_2.11', version: '2.5.0'
  testImplementation group: 'com.typesafe.play', name: 'play-java-ws_2.11', version: '2.5.0'
  testImplementation(group: 'com.typesafe.play', name: 'play-test_2.11', version: '2.5.0') {
    exclude group: 'org.eclipse.jetty.websocket', module: 'websocket-client'
  }

  latestDepTestImplementation group: 'com.typesafe.play', name: 'play-java_2.11', version: '2.5.+'
  latestDepTestImplementation group: 'com.typesafe.play', name: 'play-java-ws_2.11', version: '2.5.+'
  latestDepTestImplementation(group: 'com.typesafe.play', name: 'play-test_2.11', version: '2.5.+') {
    exclude group: 'org.eclipse.jetty.websocket', module: 'websocket-client'
  }
}
