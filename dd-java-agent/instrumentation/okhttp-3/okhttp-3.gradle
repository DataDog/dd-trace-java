apply plugin: 'version-scan'

versionScan {
  group = "com.squareup.okhttp3"
  module = "okhttp"
  versions = "[3.0,)"
  legacyGroup = "com.squareup.okhttp"
  verifyPresent = [
    "okhttp3.Cookie"        : null,
    "okhttp3.ConnectionPool": null,
    "okhttp3.Headers"       : null,
  ]
}

apply from: "${rootDir}/gradle/java.gradle"

if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
  sourceSets {
    test {
      groovy {
        // These classes use Ratpack which requires Java 8. (Currently also incompatible with Java 9.)
        exclude '**/OkHttp3Test.groovy'
      }
    }
  }
}

dependencies {
  compileOnly group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.0.0'
  compile('io.opentracing.contrib:opentracing-okhttp3:0.1.0') {
    transitive = false
  }

  compile project(':dd-trace-ot')
  compile project(':dd-java-agent:agent-tooling')

  compile deps.bytebuddy
  compile deps.opentracing

  testCompile project(':dd-java-agent:testing')
  testCompile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.0.0'
}
