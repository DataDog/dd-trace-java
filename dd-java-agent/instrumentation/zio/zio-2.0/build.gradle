def zioVersion = '2.0.0'
def scalaVersion = '2.12'

muzzle {
  pass {
    group = 'dev.zio'
    module = "zio_2.12"
    versions = "[$zioVersion,)"
    assertInverse = true
    javaVersion = "11"
  }
  pass {
    group = 'dev.zio'
    module = "zio_2.13"
    versions = "[$zioVersion,)"
    assertInverse = true
    javaVersion = "11"
  }
  pass {
    group = 'dev.zio'
    module = "zio_3"
    versions = "[$zioVersion,)"
    assertInverse = true
    javaVersion = "11"
  }
}

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/test-with-scala.gradle"

addTestSuiteForDir('latestDepTest', 'test')

tasks.named("compileLatestDepTestGroovy", GroovyCompile) {
  classpath += files(tasks.named('compileLatestDepTestScala').map { it.destinationDirectory })
}

tasks.withType(ScalaCompile).configureEach {
  // Despite the compatibility matrix, scala compiler doesn't properly
  // get the right compiler flags with JDK11 toolchain. Using 8 works.
  // * https://docs.scala-lang.org/overviews/jdk-compatibility/overview.html
  // * https://github.com/gradle/gradle/issues/19456
  configureCompiler(it, 8)
}

dependencies {
  compileOnly group: 'dev.zio', name: "zio_$scalaVersion", version: zioVersion

  testImplementation libs.scala
  testImplementation group: 'dev.zio', name: "zio_$scalaVersion", version: zioVersion

  // Fails from 2.1: https://datadoghq.atlassian.net/browse/AIDM-156
  latestDepTestImplementation group: 'dev.zio', name: "zio_$scalaVersion", version: '2.0.+'
}
