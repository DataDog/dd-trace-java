ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

muzzle {
  pass {
    group = 'org.scala-lang'
    module = "scala-library"
    versions = "[2.8,2.12)"
    assertInverse = true
  }
}

apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'scala' // Don't use test-with-scala since we want to pick our own version.

apply plugin: 'org.unbroken-dome.test-sets'

/**
 * This project has instrumentation to support Scala's copy of ForkJoinPool which was added in 2.8 and removed in 2.11.
 * It also has additional generic tests for scala context propagation support beyond 2.11 which are covered in latest12Test and latestDepTest.
 */
testSets {
  latestDepTest
  latest12Test {
    dirName = 'latestDepTest'
  }
  latest11Test {
    dirName = 'test'
  }
  slickTest
}

compileTestGroovy {
  classpath += files(sourceSets.test.scala.classesDirectory)
}
compileLatest12TestGroovy {
  classpath += files(sourceSets.latest12Test.scala.classesDirectory)
}
compileLatest11TestGroovy {
  classpath += files(sourceSets.latest11Test.scala.classesDirectory)
}
compileLatestDepTestGroovy {
  classpath += files(sourceSets.latestDepTest.scala.classesDirectory)
}
compileSlickTestGroovy {
  classpath += files(sourceSets.slickTest.scala.classesDirectory)
}

dependencies {
  compileOnly group: 'org.scala-lang', name: 'scala-library', version: '2.8.0'

  // first version that works with the tests (without dependency problems):
  testCompile group: 'org.scala-lang', name: 'scala-library', version: '2.10.2'
  testCompile project(':dd-java-agent:instrumentation:trace-annotation')

  latest11TestCompile group: 'org.scala-lang', name: 'scala-library', version: '2.11.+'
  latest12TestCompile group: 'org.scala-lang', name: 'scala-library', version: '2.12.+'
  latestDepTestCompile group: 'org.scala-lang', name: 'scala-library', version: '+'
  latestDepTestCompile project(':dd-java-agent:instrumentation:scala-promise:scala-promise-2.13')

  slickTestCompile project(':dd-java-agent:instrumentation:java-concurrent')
  slickTestCompile project(':dd-java-agent:instrumentation:java-concurrent:java-completablefuture')
  slickTestCompile project(':dd-java-agent:instrumentation:trace-annotation')
  slickTestCompile project(':dd-java-agent:instrumentation:jdbc')
  slickTestCompile deps.scala
  slickTestCompile group: 'com.typesafe.slick', name: 'slick_2.11', version: '3.2.0'
  slickTestCompile group: 'com.h2database', name: 'h2', version: '1.4.197'
}

// Run extra tests along with the rest of unit tests
test.dependsOn slickTest
test.dependsOn latest11Test
test.dependsOn latest12Test
