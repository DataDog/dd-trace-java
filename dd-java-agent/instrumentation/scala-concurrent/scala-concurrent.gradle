ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

muzzle {
  pass {
    group = 'org.scala-lang'
    module = "scala-library"
    versions = "[2.3.0,]"
    assertInverse = true
  }
}

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/test-with-scala.gradle"

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  latestDepTest {
    dirName = 'test'
  }
  slickTest
}

compileLatestDepTestGroovy {
  classpath += files(sourceSets.latestDepTest.scala.classesDirectory)
}
compileSlickTestGroovy {
  classpath += files(sourceSets.slickTest.scala.classesDirectory)
}

dependencies {
  compileOnly group: 'org.scala-lang', name: 'scala-library', version: '2.3.0'

  testCompile group: 'org.scala-lang', name: 'scala-library', version: '2.3.0'
  testCompile project(':dd-java-agent:instrumentation:trace-annotation')

  latestDepTestCompile group: 'org.scala-lang', name: 'scala-library', version: '+'

  slickTestCompile project(':dd-java-agent:instrumentation:java-concurrent')
  slickTestCompile project(':dd-java-agent:instrumentation:trace-annotation')
  slickTestCompile project(':dd-java-agent:instrumentation:jdbc')
  slickTestCompile deps.scala
  slickTestCompile group: 'com.typesafe.slick', name: 'slick_2.11', version: '3.2.0'
  slickTestCompile group: 'com.h2database', name: 'h2', version: '1.4.197'
}

[
  configurations.latestDepTestCompile,
  configurations.latestDepTestRuntime
].each {
  it.resolutionStrategy {
    force group: 'org.scala-lang', name: 'scala-library', version: '+'
  }
}

// Run slickTest along with the rest of unit tests
test.dependsOn slickTest
