muzzle {
  // Catalina doesn't reliably depend on coyote, so we use a different dependency that does.
  pass {
    group = "org.apache.tomcat"
    module = 'tomcat-catalina-ha' // includes catalina and coyote (to simplify muzzle check)
    versions = "[7,]"
    assertInverse = true
  }
  pass {
    group = "org.apache.tomcat"
    module = 'catalina-ha' // includes catalina and coyote (to simplify muzzle check)
    versions = "[6,7)" // not all versions are on maven central.
    assertInverse = true
  }
  // Older versions are sparse on maven central, so test what we can.
  pass {
    group = "tomcat"
    module = 'catalina'
    versions = "[5.5,6)"
    extraDependency 'tomcat:tomcat-coyote:5.5.23'
    assertInverse = true
  }
  pass {
    group = "tomcat"
    module = 'tomcat-coyote'
    versions = "[5.5,6)"
    extraDependency 'tomcat:catalina:5.5.23'
    extraDependency 'tomcat:tomcat-util:5.5.23'
    assertInverse = true
  }
  pass {
    name = "tomcat-websocket"
    group = "org.apache.tomcat"
    module = 'tomcat-websocket'
    versions = "[8.0.1,]"
    extraDependency 'org.apache.tomcat:tomcat-catalina-ha:8.0.1'
    assertInverse = true
  }
  // org.apache.catalina.connector.CoyoteAdapter introduced in Catalina 5.5
  pass {
    name = "tomcat-processtags"
    group = "org.apache.tomcat"
    module = 'tomcat-catalina-ha'
    versions = "[,]"
  }
}

apply from: "$rootDir/gradle/java.gradle"

addTestSuite('latestDepTest')
addTestSuiteExtendingForDir('latestDepForkedTest', 'latestDepTest', 'latestDepTest')

addTestSuite("tomcat9Test")

addTestSuiteForDir('latest10Test', 'latestDepTest')
addTestSuiteExtendingForDir('latest10ForkedTest', 'latest10Test', 'latestDepTest')

def tomcatVersion = '5.5.12' // earliest 5.5.x available in maven central (with all needed dependencies).

configurations.configureEach {
  // shut up about broken xml-api pom relocation
  resolutionStrategy {
    force 'xml-apis:xml-apis:1.4.01'
  }
}

["compileLatest10TestGroovy", "compileLatest10ForkedTestGroovy"].each { name ->
  tasks.named(name, GroovyCompile) {
    configureCompiler(it, 11)
  }
}

["compileLatestDepTestGroovy", "compileLatestDepForkedTestGroovy"].each { name ->
  tasks.named(name, GroovyCompile) {
    configureCompiler(it, 17)
  }
}

dependencies {
  compileOnly group: 'tomcat', name: 'catalina', version: tomcatVersion
  compileOnly group: 'tomcat', name: 'tomcat-coyote', version: tomcatVersion
  compileOnly group: 'tomcat', name: 'tomcat-util', version: tomcatVersion
  compileOnly group: 'org.apache.tomcat', name: 'tomcat-websocket', version: '8.0.1', transitive: false
  compileOnly group: 'org.apache.tomcat', name: 'tomcat-websocket-api', version: '8.0.1'


  // Version that corresponds with Tomcat 5.5
  // https://tomcat.apache.org/whichversion.html
  compileOnly group: 'javax.servlet', name: 'servlet-api', version: '2.4'

  implementation project(':dd-java-agent:instrumentation:tomcat:tomcat-common')

  testImplementation(project(':dd-java-agent:instrumentation-testing')) {
    exclude group: 'org.eclipse.jetty', module: 'jetty-server'
  }

  // Required jars for embedded tomcat:
  testImplementation group: 'tomcat', name: 'catalina', version: tomcatVersion
  testImplementation group: 'tomcat', name: 'tomcat-coyote', version: tomcatVersion
  testImplementation group: 'tomcat', name: 'tomcat-http', version: tomcatVersion
  testImplementation group: 'tomcat', name: 'tomcat-util', version: tomcatVersion
  testImplementation group: 'tomcat', name: 'naming-resources', version: tomcatVersion
  testImplementation group: 'tomcat', name: 'naming-factory', version: tomcatVersion
  testImplementation group: 'commons-modeler', name: 'commons-modeler', version: '2.0.1'
  testImplementation group: 'javax.servlet', name: 'servlet-api', version: '2.4'
  testImplementation testFixtures(project(':dd-java-agent:instrumentation:servlet:javax-servlet:javax-servlet-3.0'))

  testRuntimeOnly project(':dd-java-agent:instrumentation:tomcat:tomcat-appsec:tomcat-appsec-5.5')
  testRuntimeOnly project(':dd-java-agent:instrumentation:tomcat:tomcat-appsec:tomcat-appsec-6.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:servlet:javax-servlet:javax-servlet-common')
  testRuntimeOnly project(':dd-java-agent:instrumentation:servlet:javax-servlet:javax-servlet-2.2')
  testRuntimeOnly project(':dd-java-agent:instrumentation:servlet:javax-servlet:javax-servlet-3.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:servlet:jakarta-servlet-5.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:websocket:javax-websocket-1.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:websocket:jakarta-websocket-2.0')

  testImplementation testFixtures(project(':dd-java-agent:instrumentation:servlet:jakarta-servlet-5.0'))

  tomcat9TestImplementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '9.+'
  tomcat9TestImplementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-websocket', version: '9.+'

  latest10TestImplementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '10.+'
  latest10TestImplementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-websocket', version: '10.+'
  latest10TestImplementation group: 'org.apache.tomcat', name: 'jakartaee-migration', version: '1.+'
  latest10TestImplementation project(":dd-java-agent:appsec:appsec-test-fixtures")

  latest10TestImplementation(project(':dd-java-agent:instrumentation:tomcat:tomcat-appsec:tomcat-appsec-6.0'))
  latest10TestImplementation(project(':dd-java-agent:instrumentation:tomcat:tomcat-appsec:tomcat-appsec-7.0'))
  latest10TestImplementation(project(':dd-java-agent:instrumentation:tomcat:tomcat-9.0'))

  latestDepTestImplementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '11.+'
  latestDepTestImplementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-websocket', version: '11.+'
  latestDepTestImplementation group: 'org.apache.tomcat', name: 'jakartaee-migration', version: '1.+'
  latestDepTestImplementation project(":dd-java-agent:appsec:appsec-test-fixtures")

  latestDepTestRuntimeOnly(project(':dd-java-agent:instrumentation:tomcat:tomcat-appsec:tomcat-appsec-6.0'))
  latestDepTestRuntimeOnly(project(':dd-java-agent:instrumentation:tomcat:tomcat-appsec:tomcat-appsec-7.0'))
  latestDepTestRuntimeOnly(project(':dd-java-agent:instrumentation:tomcat:tomcat-9.0'))
}

tasks.withType(Test).configureEach {
  // to avoid java.lang.IllegalAccessException: class org.apache.tomcat.util.compat.JreCompat cannot access a member of class java.io.FileSystem (in module java.base) with modifiers "static final"
  conditionalJvmArgs(
    it,
    JavaVersion.VERSION_16,
    ['--add-opens', 'java.base/java.io=ALL-UNNAMED']
    )
}

// Exclude all the dependencies from test for latestDepTest since the names are completely different.
["latestDepTestImplementation", "tomcat9TestImplementation", "latest10TestImplementation"].each {
  configurations.named(it) {
    it.exclude group: 'tomcat', module: 'catalina'
    it.exclude group: 'tomcat', module: 'tomcat-coyote'
    it.exclude group: 'tomcat', module: 'tomcat-util'
    it.exclude group: 'tomcat', module: 'tomcat-http'
    it.exclude group: 'tomcat', module: 'naming-resources'
    it.exclude group: 'tomcat', module: 'naming-factory'
    it.exclude group: 'commons-modeler', module: 'commons-modeler'
    it.exclude group: 'javax.servlet', module: 'servlet-api'
  }
}

tasks.named("latestDepTest", Test) {
  testJvmConstraints {
    minJavaVersion = JavaVersion.VERSION_17
  }
}

tasks.named("latestDepForkedTest", Test) {
  testJvmConstraints {
    minJavaVersion = JavaVersion.VERSION_17
  }
}

tasks.named("latest10Test", Test) {
  testJvmConstraints {
    minJavaVersion = JavaVersion.VERSION_11
  }
}

tasks.named("latest10ForkedTest", Test) {
  testJvmConstraints {
    minJavaVersion = JavaVersion.VERSION_11
  }
}
