muzzle {
  pass {
    group = "org.apache.axis2"
    module = "axis2-kernel"
    versions = "[1.3,)"
  }
}

apply from: "$rootDir/gradle/java.gradle"

addTestSuiteForDir('latestDepTest', 'test')
addTestSuiteForDir('latestDepForkedTest', 'test')

tasks.named("latestDepTest", Test) {
  jvmConstraint {
    minJavaVersionForTests = JavaVersion.VERSION_11
  }
}

tasks.named("latestDepForkedTest", Test) {
  jvmConstraint {
    minJavaVersionForTests = JavaVersion.VERSION_11
  }
}

configurations.configureEach {
  // the version used by axis2 isn't available in a public repository - we don't need it, so exclude it
  exclude group: 'org.apache.woden', module: 'woden'
  // shut up about broken xml-api pom relocation
  resolutionStrategy {
    force 'xml-apis:xml-apis:1.4.01'
  }
}

configureGroovyCompiler(11, "compileLatestDepForkedTestGroovy", "compileLatestDepTestGroovy")

["compileLatestDepForkedTestJava", "compileLatestDepTestJava"].each {
  tasks.named(it, JavaCompile) {
    configureCompiler(it, 11)
  }
}

dependencies {
  compileOnly group: 'org.apache.axis2', name: 'axis2-kernel', version: '1.3'

  testImplementation group: 'org.apache.axis2', name: 'axis2-kernel', version: '1.3'

  latestDepTestImplementation group: 'org.apache.axis2', name: 'axis2-kernel', version: '+'
  latestDepTestImplementation group: 'org.apache.axis2', name: 'axis2-transport-local', version: '+'
  latestDepTestImplementation group: 'org.apache.axis2', name: 'axis2-transport-http', version: '+'
  latestDepTestImplementation group: 'org.apache.axis2', name: 'axis2-adb', version: '+'
}
