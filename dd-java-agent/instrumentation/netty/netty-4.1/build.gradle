apply from: "$rootDir/gradle/java.gradle"

muzzle {
  pass {
    group = "io.netty"
    module = "netty-codec-http"
    versions = "[4.1.0.Final,)"
    assertInverse = true
  }
  pass {
    group = "io.netty"
    module = "netty-codec-http2"
    versions = "[4.1.0.Final,)"
    assertInverse = true
  }
  pass {
    group = "io.netty"
    module = "netty-all"
    versions = "[4.1.0.Final,)"
    assertInverse = true
    excludeDependency 'io.netty:netty-tcnative'
  }
  fail {
    group = "io.netty"
    module = "netty"
    versions = "[,]"
  }
  pass {
    group = "io.vertx"
    module = "vertx-core"
    versions = "[3.3.0,)"
    assertInverse = true
  }
}

addTestSuiteForDir('latestDepTest', 'test')

dependencies {
  compileOnly group: 'io.netty', name: 'netty-codec-http', version: '4.1.0.Final'
  compileOnly group: 'io.netty', name: 'netty-codec-http2', version: '4.1.0.Final'

  api project(':dd-java-agent:instrumentation:netty:netty-common')

  testImplementation project(':dd-java-agent:instrumentation:netty:netty-promise-4.0')
  testImplementation project(':dd-java-agent:instrumentation:reactor-core-3.1')
  testImplementation project(':dd-java-agent:instrumentation:reactive-streams-1.0')
  testImplementation group: 'io.netty', name: 'netty-codec-http', version: '4.1.0.Final'
  testImplementation group: 'io.projectreactor.netty', name: 'reactor-netty', version: '0.8.0.RELEASE'
  testImplementation group: 'org.asynchttpclient', name: 'async-http-client', version: '2.1.0'

  // latest async-http-client incompatable with 5.0+ netty
  latestDepTestImplementation group: 'io.netty', name: 'netty-codec', version: '4.+'
  latestDepTestImplementation group: 'io.netty', name: 'netty-codec-http', version: '4.+'
  latestDepTestImplementation group: 'io.netty', name: 'netty-codec-http2', version: '4.+'
  latestDepTestImplementation group: 'io.netty', name: 'netty-codec-socks', version: '4.+'

  latestDepTestImplementation group: 'org.asynchttpclient', name: 'async-http-client', version: '2.+'
}

// We need to force the dependency to the earliest supported version because other libraries declare newer versions.
configurations.named('testImplementation') {
  resolutionStrategy {
    eachDependency { DependencyResolveDetails details ->
      //specifying a fixed version for all libraries with io.netty' group
      if (details.requested.group == 'io.netty') {
        details.useVersion "4.1.0.Final"
      }
    }
  }
}
