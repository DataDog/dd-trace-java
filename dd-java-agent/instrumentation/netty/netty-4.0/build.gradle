// Set properties before any plugins get loaded
// asynchttpclient is not compatible with Java 11
ext {
  maxJavaVersionForTests = JavaVersion.VERSION_1_8
}

apply from: "$rootDir/gradle/java.gradle"

muzzle {
  pass {
    group = "io.netty"
    module = "netty-codec-http"
    versions = "[4.0.0.Final,4.1.0.Final)"
    assertInverse = true
  }
  pass {
    group = "io.netty"
    module = "netty-all"
    versions = "[4.0.0.Final,4.1.0.Final)"
    assertInverse = true
    excludeDependency 'io.netty:netty-tcnative'
  }
  fail {
    group = "io.netty"
    module = "netty"
    versions = "[,]"
  }
  pass {
    group = "io.vertx"
    module = "vertx-core"
    versions = "[2.0.0,3.3.0)"
    assertInverse = true
  }
}

addTestSuiteForDir('latestDepTest', 'test')

dependencies {
  compileOnly instrumentedLibs.netty.codec.http.v4

  testImplementation project(':dd-java-agent:instrumentation:netty:netty-promise-4.0')
  testImplementation instrumentedLibs.netty.codec.http.v4
  testImplementation instrumentedLibs.async.http.client.v2

  latestDepTestImplementation instrumentedLibs.netty.codec.http.latest.v4x
  latestDepTestImplementation instrumentedLibs.async.http.client.v2x
}

// We need to force the dependency to the earliest supported version because other libraries declare newer versions.
configurations.named('testImplementation') {
  resolutionStrategy {
    eachDependency { DependencyResolveDetails details ->
      //specifying a fixed version for all libraries with io.netty' group
      if (details.requested.group == 'io.netty') {
        details.useVersion "4.0.0.Final"
      }
    }
  }
}
