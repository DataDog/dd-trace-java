plugins {
  id 'com.google.protobuf' version '0.9.6'
}

muzzle {
  pass {
    group = "com.linecorp.armeria"
    module = "armeria-grpc"
    versions = "[0.84.0,)"
    assertInverse = true
    skipVersions += "1.3.0" // com.linecorp.armeria.common.grpc.protocol.ArmeriaMessageDeframer is missing in this one version
  }
}

apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'idea'

// First version with Mac M1 support
def protocVersion = '3.17.3'
def grpcVersion = '1.42.2'
protobuf {
  protoc {
    // Download compiler rather than using locally installed version:
    // First version with Mac M1 support
    artifact = "com.google.protobuf:protoc:${protocVersion}"
  }
  plugins {
    // First version with aarch support
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
    }
  }
  generateProtoTasks {
    ofSourceSet("test").configureEach {
      plugins { grpc {} }
    }
  }
}

addTestSuiteForDir('latestDepTest', 'test')
addTestSuiteExtendingForDir('latestDepForkedTest', 'latestDepTest', 'test')

tasks.named("latestDepTest", Test) {
  finalizedBy 'latestDepForkedTest'
}

dependencies {
  compileOnly group: 'com.linecorp.armeria', name: 'armeria-grpc', version: '0.84.0'
  compileOnly group: 'com.linecorp.armeria', name: 'armeria-grpc-protocol', version: '0.84.0'

  testImplementation group: 'com.linecorp.armeria', name: 'armeria-grpc', version: '1.0.0'
  testImplementation group: 'com.linecorp.armeria', name: 'armeria-junit4', version: '1.0.0'
  testImplementation group: 'com.google.protobuf', name: 'protobuf-java', version: protocVersion
  testImplementation group: 'io.grpc', name: 'grpc-stub', version: grpcVersion
  testImplementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
  testImplementation project(':dd-java-agent:instrumentation:grpc-1.5')
  testImplementation project(':dd-java-agent:instrumentation:netty:netty-3.8')
  testImplementation project(':dd-java-agent:instrumentation:netty:netty-4.0')
  testImplementation project(':dd-java-agent:instrumentation:netty:netty-4.1')

  latestDepTestImplementation sourceSets.test.output // include the protobuf generated classes
  latestDepTestImplementation group: 'com.linecorp.armeria', name: 'armeria-grpc', version: '1.+'
}
