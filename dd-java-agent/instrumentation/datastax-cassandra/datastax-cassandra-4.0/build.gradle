muzzle {
  pass {
    group = "com.datastax.oss"
    module = "java-driver-core"
    versions = "[4.0,]"
    assertInverse = true
  }
}

apply from: "$rootDir/gradle/java.gradle"

testJvmConstraint {
  // TODO Java 17: The embedded cassandra deadlocks on start every time on Java 17
  //  This can be changed to use test-containers
  maxJavaVersion = JavaVersion.VERSION_15
}

addTestSuiteForDir('latestDepTest', 'test')

dependencies {
  compileOnly group: 'com.datastax.oss', name: 'java-driver-core', version: '4.0.0'

  // ProgrammaticConfig, required to set the timeout, wasn't added until 4.0.1
  testImplementation group: 'com.datastax.oss', name: 'java-driver-core', version: '4.0.1'
  testImplementation group: 'org.testcontainers', name: 'cassandra', version: libs.versions.testcontainers.get()

  // Force 0.3.3 because 0.3.0 has a version parsing bug that fails on jdk 15
  testImplementation group: 'com.github.jbellis', name: "jamm", version: '0.3.3'

  testImplementation project(':dd-java-agent:instrumentation:guava-10.0')

  latestDepTestImplementation group: 'com.datastax.oss', name: 'java-driver-core', version: '+'
}
