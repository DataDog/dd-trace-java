ext {
  cassandraDriverTestVersions = "[3.0,4.0)"
}

muzzle {
  pass {
    group = "com.datastax.cassandra"
    module = "cassandra-driver-core"
    versions = cassandraDriverTestVersions
    assertInverse = true
    // Older versions of cassandra-driver-core require an older guava dependency (0.16.0). guava >20.0 is not
    // compatible with Java 7, so we declare the dependency on 20.0 in our top level dependencies.gradle.
    // Ideally our muzzle plugin should take into account those versions declaration, instead it does not so we would
    // end up with testing compatibility with guava 0.16 which lacks the method invocation added to be compatible with
    // most recent versions of guava (27+). While the long term solution is to make the muzzle plugin aware of upstream
    // declared dependencies, for now we just make sure that we use the proper ones.
    extraDependency "com.google.guava:guava:20.0"
  }

  // Making sure that instrumentation works with recent versions of Guava which removed method
  // Futures::transform(input, function) in favor of Futures::transform(input, function, executor)
  pass {
    name = "Newest versions of Guava"
    group = "com.datastax.cassandra"
    module = "cassandra-driver-core"
    versions = cassandraDriverTestVersions
    // While com.datastax.cassandra uses old versions of Guava, users may depends themselves on newer versions of Guava
    extraDependency "com.google.guava:guava:27.0-jre"
  }
  pass {
    name = "cluster"
    group = "com.datastax.cassandra"
    module = "cassandra-driver-core"
    versions = "[3.0,3.8)"
    extraDependency "com.google.guava:guava:27.0-jre"
  }
}

apply from: "$rootDir/gradle/java.gradle"


testJvmConstraints {
  // Test use Cassandra 3 which requires Java 8. (Currently incompatible with Java 9.)
  maxJavaVersion = JavaVersion.VERSION_1_8
}

addTestSuiteForDir('latestDepTest', 'test')

dependencies {
  compileOnly group: 'com.datastax.cassandra', name: 'cassandra-driver-core', version: '3.0.0'
  compileOnly group: 'com.google.guava', name: 'guava', version: '18.0'


  testImplementation group: 'com.datastax.cassandra', name: 'cassandra-driver-core', version: '3.2.0'
  testImplementation group: 'com.github.jbellis', name: 'jamm', version: '0.3.3'
  testImplementation (group: 'org.testcontainers', name: 'cassandra', version: libs.versions.testcontainers.get()) {
    exclude group: 'com.datastax.cassandra', module: 'cassandra-driver-core'
  }


  testImplementation project(':dd-java-agent:instrumentation:guava-10.0')

  latestDepTestImplementation group: 'com.datastax.cassandra', name: 'cassandra-driver-core', version: '3.7+'
}
