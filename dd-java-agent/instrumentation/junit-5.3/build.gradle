apply from: "$rootDir/gradle/java.gradle"

// JUnit5 5.3.0+ version is needed because of the fix in the TestInheritance test suite names.
// Previously at that version, the test suite name was the base class.
def jupiterVersion = '5.3.0'
def platformVersion = '1.3.0'

muzzle {
  pass {
    group = 'org.junit.platform'
    module = 'junit-platform-launcher'
    versions = "[$platformVersion,)"
    assertInverse = true
    extraDependency "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
  }
  pass {
    group = 'org.junit.jupiter'
    module = 'junit-jupiter-api'
    versions = "[$jupiterVersion,)"
    assertInverse = true
    extraDependency "org.junit.platform:junit-platform-launcher:$platformVersion"
  }
}

addTestSuiteForDir('latestDepTest', 'test')

dependencies {
  compileOnly group: 'org.junit.platform', name: 'junit-platform-launcher', version: "$platformVersion"
  compileOnly group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "$jupiterVersion"

  testImplementation project(':dd-java-agent:agent-ci-visibility:civisibility-instrumentation-testing')

  // versions used below are not the minimum ones that we support,
  // but the tests need to use them in order to be compliant with Spock 2.x
  testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.12.0'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.12.0'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.12.0'

  latestDepTestImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: '+'
  latestDepTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '+'
  latestDepTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '+'
}

configurations.matching({ it.name.startsWith('test') }).each({
  it.resolutionStrategy {
    force group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.12.0'
    force group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.12.0'
    force group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.12.0'
  }
})
