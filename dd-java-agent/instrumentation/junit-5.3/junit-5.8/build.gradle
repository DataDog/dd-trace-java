apply from: "$rootDir/gradle/java.gradle"

def jupiterVersion = '5.8.0'
def platformVersion = '1.8.0'

muzzle {
  pass {
    group = 'org.junit.platform'
    module = 'junit-platform-launcher'
    versions = "[$platformVersion,)"
    extraDependency "org.junit.jupiter:junit-jupiter-engine:$jupiterVersion"
  }
  pass {
    group = 'org.junit.jupiter'
    module = 'junit-jupiter-engine'
    versions = "[$jupiterVersion,)"
  }
  pass {
    group = 'org.junit.jupiter'
    module = 'junit-jupiter-api'
    versions = "[$jupiterVersion,)"
    extraDependency "org.junit.jupiter:junit-jupiter-engine:$jupiterVersion"
  }
}

addTestSuiteForDir('latestDepTest', 'test')

dependencies {
  implementation project(':dd-java-agent:instrumentation:junit-5.3')

  compileOnly group: 'org.junit.platform', name: 'junit-platform-launcher', version: "$platformVersion"
  compileOnly group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "$jupiterVersion"
  compileOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "$jupiterVersion"

  testImplementation testFixtures(project(':dd-java-agent:agent-ci-visibility'))

  // versions used below are not the minimum ones that we support,
  // but the tests need to use them in order to be compliant with Spock 2.x
  testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.13.1'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.13.1'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.13.1'

  latestDepTestImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: '+'
  latestDepTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '+'
  latestDepTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '+'
}

configurations.matching({ it.name.startsWith('test') }).each({
  it.resolutionStrategy {
    force group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.13.1'
    force group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.13.1'
    force group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.13.1'
  }
})
