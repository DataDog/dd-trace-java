// Set properties before any plugins get loaded
ext {
  // JUnit5 requires Java 1.8+
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

apply from: "$rootDir/gradle/java.gradle"

// JUnit5 5.3.0+ version is needed because of the fix in the TestInheritance test suite names.
// Previously at that version, the test suite name was the base class.
def jupiterVersion = '5.3.0'
def platformVersion = '1.3.0'

muzzle {
  pass {
    group = 'org.junit.platform'
    module = 'junit-platform-launcher'
    versions = "[$platformVersion,)"
    assertInverse = true
    extraDependency "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
  }
}

dependencies {
  implementation(project(':dd-java-agent:agent-civisibility')) {
    exclude module: ':dd-java-agent:agent-tooling'
  }

  main_java8CompileOnly group: 'org.junit.platform', name: 'junit-platform-launcher', version: "$platformVersion"
  main_java8CompileOnly group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "$jupiterVersion"

  testImplementation(group: 'org.junit.platform', name: 'junit-platform-engine') {
    version {
      strictly platformVersion
    }
  }
  testImplementation(group: 'org.junit.jupiter', name: 'junit-jupiter-engine') {
    version {
      strictly jupiterVersion
    }
  }
  testImplementation(group: 'org.junit.jupiter', name: 'junit-jupiter-params') {
    version {
      strictly jupiterVersion
    }
  }
}
