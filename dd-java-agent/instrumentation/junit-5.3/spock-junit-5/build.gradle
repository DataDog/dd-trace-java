apply from: "$rootDir/gradle/java.gradle"

def spockGroovyVersion = '3.0'

muzzle {
  pass {
    group = 'org.spockframework'
    module = 'spock-core'
    versions = "[2.0-groovy-${spockGroovyVersion},)"
    extraDependency "org.junit.platform:junit-platform-launcher:1.9.2"
  }
}

addTestSuiteForDir('latestDepTest', 'test')

dependencies {
  implementation project(':dd-java-agent:instrumentation:junit-5.3')
  compileOnly group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.7.2'
  compileOnly group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.2'
  compileOnly group: 'org.spockframework', name: 'spock-core', version: "2.0-groovy-${spockGroovyVersion}"

  testImplementation testFixtures(project(':dd-java-agent:agent-ci-visibility'))

  testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.9.2'
  testImplementation group: 'org.spockframework', name: 'spock-core', version: "2.2-groovy-${spockGroovyVersion}"

  latestDepTestImplementation group: 'org.spockframework', name: 'spock-core', version: '2.+'
}

configurations.matching({ it.name.startsWith('test') }).each({
  it.resolutionStrategy {
    force group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.9.2'
    force group: 'org.spockframework', name: 'spock-core', version: "2.2-groovy-${spockGroovyVersion}"
  }
})

configurations.matching({ it.name.startsWith('latestDepTest') }).each({
  it.resolutionStrategy.componentSelection.all { ComponentSelection selection ->
    if (selection.candidate.group == 'org.spockframework' && selection.candidate.module == 'spock-core' && !selection.candidate.version.endsWith("-groovy-${spockGroovyVersion}")) {
      selection.reject('Spock groovy version mismatch')
    }
  }
})
