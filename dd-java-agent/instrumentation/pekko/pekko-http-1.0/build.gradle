apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'scala'
apply plugin: 'dd-trace-java.call-site-instrumentation'

// Since we are using different scala versions for different test sets,
// we put the test classes in the baseTest test set so that the scala
// version is not inherited
addTestSuite('baseTest')
addTestSuite('latestDepTest')
addTestSuiteForDir('latestPekko10Test', 'latestDepTest')
addTestSuite('iastTest')
addTestSuiteForDir 'latestDepIastTest', 'iastTest'

muzzle {
  pass {
    group = 'org.apache.pekko'
    module = 'pekko-http_2.12'
    versions = "[1.0.0,)"
    assertInverse = true

    extraDependency 'org.apache.pekko:pekko-stream_2.12:1.0.1'
  }
  pass {
    group = 'org.apache.pekko'
    module = 'pekko-http_2.13'
    versions = "[1.0.0,)"
    assertInverse = true

    extraDependency 'org.apache.pekko:pekko-stream_2.13:1.0.1'
    extraDependency 'org.scala-lang.modules:scala-java8-compat_2.13:1.0.2'
  }
}

configurations {
  iastTestImplementation.extendsFrom(baseTestImplementation)
  latestDepIastTestRuntimeOnly.extendsFrom(iastTestRuntimeOnly)
}

sourceSets {
  latestDepTest.groovy.srcDir sourceSets.baseTest.groovy
  latestDepTest.scala.srcDir sourceSets.baseTest.scala
  latestPekko10Test.groovy.srcDir sourceSets.baseTest.groovy
  latestPekko10Test.scala.srcDir sourceSets.baseTest.scala
}

dependencies {
  compileOnly libs.scala212
  compileOnly group: 'org.apache.pekko', name: 'pekko-http-core_2.12', version: '1.0.0'
  compileOnly group: 'org.apache.pekko', name: 'pekko-http_2.12', version: '1.0.0'
  compileOnly group: 'org.apache.pekko', name: 'pekko-stream_2.12', version: '1.0.1'

  // These are the common dependencies that are inherited by the other test sets
  testImplementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.6.0'
  testImplementation project(':dd-java-agent:instrumentation:datadog:tracing:trace-annotation')
  testImplementation project(':dd-java-agent:instrumentation:pekko:pekko-concurrent-1.0')
  testImplementation project(':dd-java-agent:instrumentation:scala:scala-concurrent-2.8')
  testImplementation project(':dd-java-agent:instrumentation:scala:scala-promise:scala-promise-2.10')
  testImplementation project(':dd-java-agent:instrumentation:scala:scala-promise:scala-promise-2.13')

  baseTestImplementation libs.scala212
  baseTestImplementation group: 'org.apache.pekko', name: 'pekko-http_2.12', version: '1.0.0'
  baseTestImplementation group: 'org.apache.pekko', name: 'pekko-stream_2.12', version: '1.0.1'

  iastTestImplementation group: 'com.github.pjfanning', name: 'pekko-http-jackson_2.12', version: '2.1.0'
  iastTestImplementation project(':dd-java-agent:agent-iast:iast-test-fixtures')
  iastTestCompileOnly libs.forbiddenapis
  iastTestRuntimeOnly project(':dd-java-agent:instrumentation:jackson-core:jackson-core-common')
  iastTestRuntimeOnly project(':dd-java-agent:instrumentation:jackson-core:jackson-core-2.12')
  iastTestRuntimeOnly project(':dd-java-agent:instrumentation:datadog:asm:iast-instrumenter')

  latestPekko10TestImplementation libs.scala213
  latestPekko10TestImplementation group: 'org.apache.pekko', name: 'pekko-http_2.13', version: '1.0.+'
  latestPekko10TestImplementation group: 'org.apache.pekko', name: 'pekko-stream_2.13', version: '1.0.+'
  latestPekko10TestImplementation group: 'org.scala-lang.modules', name: 'scala-java8-compat_2.13', version: '1.+'

  latestDepTestImplementation libs.scala213
  latestDepTestImplementation group: 'org.apache.pekko', name: 'pekko-http_2.13', version: '1.+'
  latestDepTestImplementation group: 'org.apache.pekko', name: 'pekko-stream_2.13', version: '1.+'
  latestDepTestImplementation group: 'org.scala-lang.modules', name: 'scala-java8-compat_2.13', version: '1.+'

  latestDepIastTestImplementation libs.scala213
  // Limit version to 1.0+: https://datadoghq.atlassian.net/browse/APPSEC-53615
  latestDepIastTestImplementation group: 'org.apache.pekko', name: 'pekko-http_2.13', version: '1.0.+'
  latestDepIastTestImplementation group: 'org.apache.pekko', name: 'pekko-stream_2.13', version: '1.0.+'
  latestDepIastTestImplementation group: 'com.github.pjfanning', name: 'pekko-http-jackson_2.13', version: '2.+'
  latestDepIastTestImplementation group: 'org.scala-lang.modules', name: 'scala-java8-compat_2.13', version: '1.0.+'
  latestDepIastTestImplementation project(':dd-java-agent:agent-iast:iast-test-fixtures')
  latestDepIastTestImplementation project(':dd-java-agent:instrumentation:jackson-core:jackson-core-2.16')
}

tasks.named("iastTest", Test) {
  filter {
    // This class must be excluded from scanning because it references class from "org.apache.pekko.http" package.
    // When JUnit 5 scans this class, it loads every other class that is present in its method signatures (arguments, return types, throws).
    // As the result, some classes from the datadog.trace.instrumentation.pekkohttp.iast.MakeTaintableInstrumentation#knownMatchingTypes list are loaded.
    // JUnit scanning (and class loading that it triggers) happens before instrumentations are applied.
    // Later when MakeTaintableInstrumentation is applied (in InstrumentationSpecification#setupSpec),
    // it fails to retransform the already-loaded org.apache.pekko.http classes,
    // because it needs to change the set of implemented interfaces, which is not possible for already loaded classes.
    excludeTestsMatching("*PekkoIastTestWebServer*")
  }
}

tasks.named("latestDepIastTest", Test) {
  filter {
    // Exclude the same problematic class as for iastTest to avoid class loading issues.
    excludeTestsMatching("*PekkoIastTestWebServer*")
  }
}

tasks.named("test", Test) {
  dependsOn "baseTest"
  dependsOn "iastTest"
}

tasks.named("latestDepTest", Test) {
  dependsOn "latestDepIastTest"
}

tasks.named("compileBaseTestGroovy", GroovyCompile) {
  classpath += files(tasks.named('compileBaseTestScala').map { it.destinationDirectory })
}

tasks.named("compileLatestDepTestGroovy", GroovyCompile) {
  classpath += files(tasks.named('compileLatestDepTestScala').map { it.destinationDirectory })
}

tasks.named("compileLatestPekko10TestGroovy", GroovyCompile) {
  classpath += files(tasks.named('compileLatestDepTestScala').map { it.destinationDirectory })
}
