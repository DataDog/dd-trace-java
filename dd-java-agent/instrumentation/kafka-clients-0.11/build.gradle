muzzle {
  pass {
    group = "org.apache.kafka"
    module = "kafka-clients"
    versions = "[0.11.0.0,)"
    assertInverse = true
  }
}

apply from: "$rootDir/gradle/java.gradle"

addTestSuite('latestDepTest')
addTestSuite('iastLatestDepTest3')

dependencies {
  compileOnly group: 'org.apache.kafka', name: 'kafka-clients', version: '0.11.0.0'
  implementation project(':dd-java-agent:instrumentation:kafka-common')

  testImplementation group: 'org.apache.kafka', name: 'kafka-clients', version: '0.11.0.0'
  testImplementation group: 'org.springframework.kafka', name: 'spring-kafka', version: '1.3.3.RELEASE'
  testImplementation group: 'org.springframework.kafka', name: 'spring-kafka-test', version: '1.3.3.RELEASE'
  testImplementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.2.3'
  testImplementation group: 'org.assertj', name: 'assertj-core', version: '2.9.+'
  testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.19.0'
  testRuntimeOnly project(':dd-java-agent:instrumentation:spring-scheduling-3.1')
  testImplementation(testFixtures(project(':dd-java-agent:agent-iast')))


  // IAST testing dependencies
  testRuntimeOnly project(':dd-java-agent:instrumentation:iast-instrumenter')
  testRuntimeOnly project(':dd-java-agent:instrumentation:java-lang')
  testRuntimeOnly project(':dd-java-agent:instrumentation:java-io')
  testRuntimeOnly project(':dd-java-agent:instrumentation:jackson-core')
  testImplementation(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.10')

  // Include latest version of kafka itself along with latest version of client libs.
  // This seems to help with jar compatibility hell.
  latestDepTestImplementation group: 'org.apache.kafka', name: 'kafka_2.13', version: '2.+'
  latestDepTestImplementation group: 'org.apache.kafka', name: 'kafka-clients', version: '2.+'
  latestDepTestImplementation group: 'org.springframework.kafka', name: 'spring-kafka', version: '2.+'
  latestDepTestImplementation group: 'org.springframework.kafka', name: 'spring-kafka-test', version: '2.+'
  latestDepTestImplementation group: 'org.assertj', name: 'assertj-core', version: '3.19.+'
  latestDepTestImplementation deps.guava

  // Add kafka version 3.x for IAST
  iastLatestDepTest3Implementation group: 'org.apache.kafka', name: 'kafka-clients', version: '3.+'
  iastLatestDepTest3Implementation group: 'org.springframework.kafka', name: 'spring-kafka', version: '3.+'
  iastLatestDepTest3RuntimeOnly project(':dd-java-agent:instrumentation:iast-instrumenter')
  iastLatestDepTest3RuntimeOnly project(':dd-java-agent:instrumentation:java-lang')
  iastLatestDepTest3RuntimeOnly project(':dd-java-agent:instrumentation:java-io')
  iastLatestDepTest3RuntimeOnly project(':dd-java-agent:instrumentation:jackson-core')
  iastLatestDepTest3Implementation(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.15.3')
  iastLatestDepTest3Implementation(testFixtures(project(':dd-java-agent:agent-iast')))

}

configurations.testRuntimeClasspath {
  // spock-core depends on assertj version that is not compatible with kafka-clients
  resolutionStrategy.force 'org.assertj:assertj-core:2.9.1'
}

iastLatestDepTest3.configure {
  javaLauncher = getJavaLauncherFor(17)
  jvmArgs = ['--add-opens', 'java.base/java.util=ALL-UNNAMED']
}


