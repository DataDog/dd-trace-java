plugins {
  id 'java-test-fixtures'
  id 'com.github.johnrengelman.shadow'
}

def sparkVersion = '2.4.0'
def scalaVersion = '2.12'

muzzle {
  pass {
    group = "org.apache.spark"
    module = "spark-sql_$scalaVersion"
    versions = "[$sparkVersion,)"
    assertInverse = true
  }
}
configurations.all {
  resolutionStrategy.deactivateDependencyLocking()
}
apply from: "$rootDir/gradle/java.gradle"

addTestSuiteForDir('latestDepTest', 'test')
addTestSuite('test_spark24')
addTestSuite('test_spark32')

ext {
  // Hadoop does not behave correctly with OpenJ9 https://issues.apache.org/jira/browse/HADOOP-18174
  excludeJdk = ['SEMERU8', 'SEMERU11']

  // Spark does not support Java > 11 until 3.3.0 https://issues.apache.org/jira/browse/SPARK-33772
  maxJavaVersionForTests = JavaVersion.VERSION_11
}

dependencies {
  implementation project(':dd-java-agent:instrumentation:spark')

  compileOnly group: 'org.apache.spark', name: "spark-core_$scalaVersion", version: "$sparkVersion"
  compileOnly group: 'org.apache.spark', name: "spark-sql_$scalaVersion", version: "$sparkVersion"
  compileOnly group: 'io.openlineage', name: "openlineage-java", version: '1.29.0-SNAPSHOT'

  testImplementation(testFixtures(project(":dd-java-agent:instrumentation:spark")))
  testImplementation group: 'org.apache.spark', name: "spark-core_$scalaVersion", version: "$sparkVersion"
  testImplementation group: 'org.apache.spark', name: "spark-sql_$scalaVersion", version: "$sparkVersion"
  testImplementation group: 'org.apache.spark', name: "spark-yarn_$scalaVersion", version: "$sparkVersion"
  testImplementation group: 'io.openlineage', name: "openlineage-java", version: '1.29.0-SNAPSHOT'

  test_spark24Implementation group: 'org.apache.spark', name: "spark-core_$scalaVersion", version: "2.4.8"
  test_spark24Implementation group: 'org.apache.spark', name: "spark-sql_$scalaVersion", version: "2.4.8"
  test_spark24Implementation group: 'org.apache.spark', name: "spark-yarn_$scalaVersion", version: "2.4.8"
  test_spark24Implementation group: 'io.openlineage', name: "openlineage-java", version: '1.29.0-SNAPSHOT'

  test_spark32Implementation group: 'org.apache.spark', name: "spark-core_$scalaVersion", version: "3.2.4"
  test_spark32Implementation group: 'org.apache.spark', name: "spark-sql_$scalaVersion", version: "3.2.4"
  test_spark32Implementation group: 'org.apache.spark', name: "spark-yarn_$scalaVersion", version: "3.2.4"
  // We do not support netty versions older than this because of a change to the number of parameters to the
  // PooledByteBufAllocator constructor. See this PR where the new constructor (the only one we support) was introduced:
  // https://github.com/netty/netty/pull/10267
  test_spark32Implementation group: 'io.netty', name: 'netty-buffer', version: '4.1.52.Final'
  test_spark32Implementation group: 'io.openlineage', name: "openlineage-java", version: '1.29.0-SNAPSHOT'

  latestDepTestImplementation group: 'org.apache.spark', name: "spark-core_$scalaVersion", version: '+'
  latestDepTestImplementation group: 'org.apache.spark', name: "spark-sql_$scalaVersion", version: '+'
  latestDepTestImplementation group: 'org.apache.spark', name: "spark-yarn_$scalaVersion", version: '+'
  latestDepTestImplementation group: 'io.openlineage', name: "openlineage-java", version: '1.29.0-SNAPSHOT'
}

tasks.named("test").configure {
  dependsOn "test_spark24"
  dependsOn "test_spark32"
}

shadowJar {
  relocate "io.openlineage.spark", "shared.io.openlineage.spark_${scalaVersion}"
}
