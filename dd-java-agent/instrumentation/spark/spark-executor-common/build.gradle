muzzle {
  pass {
    group = "org.apache.spark"
    module = "spark-sql_2.12"
    versions = "[2.4.0,)"
    assertInverse = true
  }
  pass {
    group = "org.apache.spark"
    module = "spark-sql_2.13"
    versions = "[3.2.0,)"
    assertInverse = true
  }
}

apply from: "$rootDir/gradle/java.gradle"

// Since we are using different scala versions for different test sets,
// we put the test classes in the baseTest test set so that the scala
// version is not inherited
addTestSuite('baseTest')
addTestSuiteForDir('latest212DepTest', 'baseTest')
addTestSuiteForDir('latest213DepTest', 'baseTest')

testJvmConstraints {
  // Hadoop does not behave correctly with OpenJ9 https://issues.apache.org/jira/browse/HADOOP-18174
  excludeJdk = ['SEMERU8', 'SEMERU11']

  // Spark does not support Java > 11 until 3.3.0 https://issues.apache.org/jira/browse/SPARK-33772
  maxJavaVersion = JavaVersion.VERSION_11
}

dependencies {
  compileOnly group: 'org.apache.spark', name: 'spark-core_2.12', version: '2.4.0'
  compileOnly group: 'org.apache.spark', name: 'spark-sql_2.12', version: '2.4.0'

  baseTestImplementation group: 'org.apache.spark', name: "spark-core_2.12", version: "2.4.0"
  baseTestImplementation group: 'org.apache.spark', name: "spark-sql_2.12", version: "2.4.0"

  latest212DepTestImplementation group: 'org.apache.spark', name: "spark-core_2.12", version: '3.+'
  latest212DepTestImplementation group: 'org.apache.spark', name: "spark-sql_2.12", version: '3.+'

  latest213DepTestImplementation group: 'org.apache.spark', name: "spark-core_2.13", version: '3.+'
  latest213DepTestImplementation group: 'org.apache.spark', name: "spark-sql_2.13", version: '3.+'
}
