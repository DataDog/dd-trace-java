ext {
  minJavaVersionForTests = JavaVersion.VERSION_17
}

muzzle {
  pass {
    group = "org.eclipse.jetty"
    module = 'jetty-server'
    versions = "[12,]"
    additionalDependencies = ['org.eclipse.jetty:jetty-session:12.0.0']
  }
  pass {
    name = 'jetty-session'
    group = "org.eclipse.jetty"
    module = 'jetty-session'
    versions = "[12,]"
  }
}

apply from: "$rootDir/gradle/java.gradle"

addTestSuiteForDir('latestDepTest', 'test')

// ee8
addTestSuiteForDir('ee8Test', 'test/ee8')
addTestSuiteExtendingForDir('ee8LatestDepTest', 'latestDepTest', 'test/ee8')
// ee9
addTestSuiteForDir('ee9Test', 'test/ee9')
addTestSuiteExtendingForDir('ee9LatestDepTest', 'latestDepTest', 'test/ee9')
// ee10
addTestSuiteForDir('ee10Test', 'test/ee10')
addTestSuiteExtendingForDir('ee10ForkedTest', 'ee10Test', 'test/ee10')
addTestSuiteExtendingForDir('ee10LatestDepTest', 'latestDepTest', 'test/ee10')
addTestSuiteExtendingForDir('ee10LatestDepForkedTest', 'ee10LatestDepTest', 'test/ee10')

[compileMain_java17Java, compileTestJava].each {
  it.configure {
    setJavaVersion(it, 17)
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }
}
[ee8LatestDepTest, ee9LatestDepTest, ee10LatestDepTest].each {
  it.configure {
    it.jvmArgs += ['-Dtest.dd.latestDepTest=true']
  }
}
forbiddenApisMain_java17 {
  failOnMissingClasses = false
}
tasks.withType(GroovyCompile).configureEach {
  javaLauncher = getJavaLauncherFor(17)
}

dependencies {
  main_java17CompileOnly ("org.eclipse.jetty:jetty-server:12.0.0") {
    exclude group: 'org.slf4j', module: 'slf4j-api'
  }
  main_java17CompileOnly ("org.eclipse.jetty:jetty-session:12.0.0") {
    exclude group: 'org.slf4j', module: 'slf4j-api'
  }
  main_java17Implementation project(':dd-java-agent:instrumentation:jetty-common')
  implementation project(':dd-java-agent:instrumentation:jetty-common')

  // Don't want to conflict with jetty from the test server.
  testImplementation(project(':dd-java-agent:instrumentation-testing')) {
    exclude group: 'org.eclipse.jetty', module: 'jetty-server'
  }
  testImplementation ("org.eclipse.jetty:jetty-server:12.0.0") {
    exclude group: 'org.slf4j', module: 'slf4j-api'
  }

  testImplementation(project(':dd-java-agent:instrumentation:jetty-appsec-9.3'))
  testImplementation testFixtures(project(':dd-java-agent:instrumentation:servlet:request-5'))
  testImplementation testFixtures(project(':dd-java-agent:instrumentation:servlet:request-3'))
  testRuntimeOnly project(':dd-java-agent:instrumentation:websocket:javax-websocket-1.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:websocket:jakarta-websocket-2.0')
  testRuntimeOnly project(":dd-java-agent:instrumentation:websocket:jetty-websocket:jetty-websocket-10")
  testRuntimeOnly project(":dd-java-agent:instrumentation:websocket:jetty-websocket:jetty-websocket-11")
  testRuntimeOnly project(":dd-java-agent:instrumentation:websocket:jetty-websocket:jetty-websocket-12")
  testImplementation testFixtures(project(':dd-java-agent:appsec'))
  testRuntimeOnly project(':dd-java-agent:instrumentation:jetty-9')
  testRuntimeOnly project(':dd-java-agent:instrumentation:servlet:request-5')
  testRuntimeOnly(project(':dd-java-agent:instrumentation:jetty-11'))
  testRuntimeOnly(project(':dd-java-agent:instrumentation:jetty-util'))

  latestDepTestImplementation ("org.eclipse.jetty:jetty-server:12.+") {
    exclude group: 'org.slf4j', module: 'slf4j-api'
  }
  latestDepTestImplementation ("org.eclipse.jetty:jetty-session:12.+") {
    exclude group: 'org.slf4j', module: 'slf4j-api'
  }

  ee8TestImplementation group: 'org.eclipse.jetty.ee8', name: 'jetty-ee8-servlet', version: '12.0.0' , {
    exclude group: 'org.slf4j', module: 'slf4j-api'
  }
  ee8TestImplementation group: 'org.eclipse.jetty.ee8.websocket', name: 'jetty-ee8-websocket-javax-server', version: '12.0.0' , {
    exclude group: 'org.slf4j', module: 'slf4j-api'
  }

  ee8LatestDepTestImplementation group: 'org.eclipse.jetty.ee8', name: 'jetty-ee8-servlet', version: '12.+' , {
    exclude group: 'org.slf4j', module: 'slf4j-api'
  }
  ee8LatestDepTestImplementation group: 'org.eclipse.jetty.ee8.websocket', name: 'jetty-ee8-websocket-javax-server', version: '12.+' , {
    exclude group: 'org.slf4j', module: 'slf4j-api'
  }

  ee9TestImplementation group: 'org.eclipse.jetty.ee9', name: 'jetty-ee9-servlet', version: '12.0.0' , {
    exclude group: 'org.slf4j', module: 'slf4j-api'
  }
  ee9TestImplementation group: 'org.eclipse.jetty.ee9.websocket', name: 'jetty-ee9-websocket-jakarta-server', version: '12.0.0' , {
    exclude group: 'org.slf4j', module: 'slf4j-api'
  }
  ee9LatestDepTestImplementation group: 'org.eclipse.jetty.ee9', name: 'jetty-ee9-servlet', version: '12.+' , {
    exclude group: 'org.slf4j', module: 'slf4j-api'
  }
  ee9LatestDepTestImplementation group: 'org.eclipse.jetty.ee9.websocket', name: 'jetty-ee9-websocket-jakarta-server', version: '12.+' , {
    exclude group: 'org.slf4j', module: 'slf4j-api'
  }

  ee10TestImplementation group: 'org.eclipse.jetty.ee10', name: 'jetty-ee10-servlet', version: '12.0.0' , {
    exclude group: 'org.slf4j', module: 'slf4j-api'
  }
  ee10TestImplementation group: 'org.eclipse.jetty.ee10.websocket', name: 'jetty-ee10-websocket-jakarta-server', version: '12.0.0' , {
    exclude group: 'org.slf4j', module: 'slf4j-api'
  }
  ee10LatestDepTestImplementation group: 'org.eclipse.jetty.ee10', name: 'jetty-ee10-servlet', version: '12.+' , {
    exclude group: 'org.slf4j', module: 'slf4j-api'
  }
  ee10LatestDepTestImplementation group: 'org.eclipse.jetty.ee10.websocket', name: 'jetty-ee10-websocket-jakarta-server', version: '12.+' , {
    exclude group: 'org.slf4j', module: 'slf4j-api'
  }
}
