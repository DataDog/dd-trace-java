ext {
  minJavaVersionForTests = JavaVersion.VERSION_17
}

muzzle {
  pass {
    group = 'org.springframework'
    module = 'spring-webflux'
    versions = "[6,)"
    javaVersion = "17"
  }
}

apply from: "$rootDir/gradle/java.gradle"

[compileMain_java17Java, compileTestJava].each {
  it.configure {
    setJavaVersion(it, 17)
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }
}

// test that webflux5 instrumentation works for webflux6 too
addTestSuite('iastTest')

[compileTestGroovy, compileIastTestGroovy].each {
  it.javaLauncher = getJavaLauncherFor(17)
}

iastTest {
  jvmArgs '--add-opens', 'java.base/java.util=ALL-UNNAMED'
}

dependencies {
  main_java17CompileOnly group: 'org.springframework', name: 'spring-webflux', version: '6.0.0'

  testImplementation project(':dd-java-agent:instrumentation:trace-annotation')
  testImplementation project(':dd-java-agent:instrumentation:netty-4.1')
  testImplementation project(':dd-java-agent:instrumentation:netty-promise-4')
  testImplementation project(':dd-java-agent:instrumentation:reactor-core-3.1')
  testImplementation group: 'org.spockframework', name: 'spock-spring', version: '1.1-groovy-2.4'

  testImplementation group: 'org.springframework', name: 'spring-webflux', version: '6.0.0'
  testImplementation group: 'io.projectreactor.netty', name: 'reactor-netty', version: '1.1.3'
  testImplementation group: 'org.springframework', name: 'spring-test', version: '6.0.0'

  iastTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-reactor-netty', version: '3.0.0'
  iastTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-json', version: '3.0.0'
  iastTestImplementation(testFixtures(project(':dd-java-agent:agent-iast')))
  iastTestImplementation project(':dd-java-agent:instrumentation:spring-webflux-5')
  iastTestImplementation project(':dd-java-agent:instrumentation:jackson-core')

  iastTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-webflux', version: '3.0.0'
  iastTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '3.0.0', {
    exclude group: 'org.hamcrest', module: 'hamcrest-core'
    exclude group: 'org.hamcrest', module: 'hamcrest-library'
  }
  iastTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-reactor-netty', version: '3.0.0'
  iastTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-json', version: '3.0.0'

  // our default version of spock is too old
  iastTestImplementation group: 'org.spockframework', name: 'spock-core', version: '2.4-M1-groovy-3.0'
  iastTestImplementation group: 'org.spockframework', name: 'spock-junit4', version: '2.4-M1-groovy-3.0'
  iastTestImplementation group: 'org.spockframework', name: 'spock-spring', version: '2.4-M1-groovy-3.0'
}

