muzzle {
  pass {
    group = "org.restlet.jse"
    module = "org.restlet"
    versions = "[2.2.0,)"
    assertInverse = false
  }
}

apply from: "$rootDir/gradle/java.gradle"

repositories {
  maven {
    // only place that has 'org.restlet.jse:org.restlet' publicly accessible
    name 'talend-restlet'
    url 'https://maven.restlet.talend.com/'
  }
}

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  // Header classes have moved around versions, so we need to split out parts of the test code
  baseTest

  latestDepTest
}

tasks.named("test").configure {
  dependsOn "baseTest"
}

dependencies {
  compileOnly group: 'org.restlet.jse', name: 'org.restlet', version: '2.2.0'

  testImplementation group: 'org.restlet.jse', name: 'org.restlet', version: '2.2.0'

  baseTestImplementation sourceSets.test.output

  latestDepTestImplementation group: 'org.restlet.jse', name: 'org.restlet', version: '2.4+'
  latestDepTestImplementation sourceSets.test.output
}
