// This results in download a LOT of modules.  commenting out for now...

apply plugin: 'version-scan'

versionScan {
  group = "com.amazonaws"
  module = "aws-java-sdk-core"
  versions = "[1.11.0,)"
  verifyPresent = [
    "com.amazonaws.http.client.HttpClientFactory"  : null,
    "com.amazonaws.http.apache.utils.ApacheUtils"  : null,
    "com.amazonaws.http.request.HttpRequestFactory": null,
  ]
}

apply from: "${rootDir}/gradle/java.gradle"

if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
  sourceSets {
    test {
      groovy {
        // These classes use Ratpack which requires Java 8. (Currently also incompatible with Java 9.)
        exclude '**/AWSClientTest.groovy'
      }
    }
  }
}

dependencies {
  compileOnly group: 'com.amazonaws', name: 'aws-java-sdk-core', version: '1.11.119'

  compile project(':dd-trace-ot')
  compile project(':dd-java-agent:agent-tooling')

  compile deps.bytebuddy
  compile deps.opentracing
  compile deps.autoservice

  testCompile project(':dd-java-agent:testing')
  // Include httpclient instrumentation for testing because it is a dependency for aws-sdk.
  testCompile project(':dd-java-agent:instrumentation:apache-httpclient-4.3')
  testCompile group: 'com.amazonaws', name: 'aws-java-sdk', version: '1.11.119'
}
