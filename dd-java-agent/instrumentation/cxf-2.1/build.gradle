ext {
  cxf3LatestDepTestMinJavaVersionForTests = JavaVersion.VERSION_11
  latestDepTestMinJavaVersionForTests = JavaVersion.VERSION_17
}
muzzle {
  pass {
    group = "org.apache.cxf"
    module = "cxf-api"
    versions = "[2.0.6,)"
    extraDependency "javax.servlet:servlet-api:2.5"
  }
  pass {
    group = "org.apache.cxf"
    module = "cxf-core"
    versions = "[3,)"
    javaVersion = "11"
    extraDependency "jakarta.servlet:jakarta.servlet-api:5.0.0"
  }
}

apply from: "${rootDir}/gradle/java.gradle"

addTestSuite('latestDepTest')
addTestSuiteForDir('cxf3LatestDepTest', 'test')

[compileCxf3LatestDepTestJava].each {
  it.configure {
    setJavaVersion(it, 11)
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }
}

[compileLatestDepTestJava].each {
  it.configure {
    setJavaVersion(it, 17)
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }
}

[compileLatestDepTestGroovy, latestDepTest].each {
  it.configure {
    javaLauncher = getJavaLauncherFor(17)
  }
}

[compileCxf3LatestDepTestGroovy, cxf3LatestDepTest].each {
  it.configure {
    javaLauncher = getJavaLauncherFor(11)
  }
}

dependencies {
  compileOnly group: 'org.apache.cxf', name: 'cxf-api', version: '2.1'
  testImplementation(project(':dd-java-agent:instrumentation-testing')) {
    exclude group: 'org.eclipse.jetty', module: 'jetty-server'
  }
  testImplementation group: 'org.apache.cxf', name: 'cxf-rt-frontend-jaxrs', version: '3.0.0'
  testImplementation group: 'org.apache.cxf', name:'cxf-rt-transports-http-jetty', version: '3.0.0'
  testCompileOnly group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.1'
  testRuntimeOnly group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
  testRuntimeOnly group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
  testRuntimeOnly project(':dd-java-agent:instrumentation:jetty:jetty-server:jetty-server-7.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:jetty:jetty-server:jetty-server-7.6')
  testRuntimeOnly project(':dd-java-agent:instrumentation:jetty:jetty-server:jetty-server-9.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:jetty:jetty-server:jetty-server-11.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:jetty:jetty-server:jetty-server-12.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:trace-annotation')
  testRuntimeOnly project(':dd-java-agent:instrumentation:jax-rs-annotations-2')
  testRuntimeOnly project(':dd-java-agent:instrumentation:jakarta-rs-annotations-3')
  testRuntimeOnly project(':dd-java-agent:instrumentation:jakarta-rs-annotations-3')

  cxf3LatestDepTestImplementation group: 'org.apache.cxf', name: 'cxf-rt-frontend-jaxrs', version: '3.+'
  cxf3LatestDepTestImplementation group: 'org.apache.cxf', name:'cxf-rt-transports-http-jetty', version: '3.+'
  cxf3LatestDepTestCompileOnly group: 'jakarta.ws.rs', name: 'jakarta.ws.rs-api', version: '2.+'

  latestDepTestCompileOnly group: 'jakarta.ws.rs', name: 'jakarta.ws.rs-api', version: '3.+'
  latestDepTestImplementation group: 'org.apache.cxf', name: 'cxf-rt-frontend-jaxrs', version: '+'
  latestDepTestImplementation group: 'org.apache.cxf', name:'cxf-rt-transports-http-jetty', version: '+'

  configurations.findAll {it.name in ['latestDepTestRuntimeClasspath', 'cxf3LatestDepTestRuntimeClasspath'] }.each {
    it.resolutionStrategy {
      force libs.slf4j
    }
  }
}
