ext {
  cxf3LatestDepTestMinJavaVersionForTests = JavaVersion.VERSION_11
  latestDepTestMinJavaVersionForTests = JavaVersion.VERSION_17
}
muzzle {
  pass {
    group = "org.apache.cxf"
    module = "cxf-api"
    versions = "[2.0.6,)"
    extraDependency "javax.servlet:servlet-api:2.5"
  }
  pass {
    group = "org.apache.cxf"
    module = "cxf-core"
    versions = "[3,)"
    javaVersion = "11"
    extraDependency "jakarta.servlet:jakarta.servlet-api:5.0.0"
  }
}

apply from: "${rootDir}/gradle/java.gradle"

addTestSuite('latestDepTest')
addTestSuiteForDir('cxf3LatestDepTest', 'test')

tasks.named("compileCxf3LatestDepTestJava", JavaCompile) {
  configureCompiler(it, 11, JavaVersion.VERSION_1_8)
}

tasks.named("compileLatestDepTestJava", JavaCompile) {
  configureCompiler(it, 17, JavaVersion.VERSION_1_8)
}

tasks.named("compileLatestDepTestGroovy", GroovyCompile) {
  configureCompiler(it, 17)
}

tasks.named("latestDepTest", Test) {
  javaLauncher = getJavaLauncherFor(17)
}

tasks.named("compileCxf3LatestDepTestGroovy", GroovyCompile) {
  configureCompiler(it, 11)
}

tasks.named("cxf3LatestDepTest", Test) {
  javaLauncher = getJavaLauncherFor(11)
}

dependencies {
  compileOnly group: 'org.apache.cxf', name: 'cxf-api', version: '2.1'
  testImplementation(project(':dd-java-agent:instrumentation-testing')) {
    exclude group: 'org.eclipse.jetty', module: 'jetty-server'
  }
  testImplementation group: 'org.apache.cxf', name: 'cxf-rt-frontend-jaxrs', version: '3.0.0'
  testImplementation group: 'org.apache.cxf', name:'cxf-rt-transports-http-jetty', version: '3.0.0'
  testCompileOnly group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.1'
  testRuntimeOnly group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
  testRuntimeOnly group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
  testRuntimeOnly project(':dd-java-agent:instrumentation:jetty:jetty-server:jetty-server-7.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:jetty:jetty-server:jetty-server-7.6')
  testRuntimeOnly project(':dd-java-agent:instrumentation:jetty:jetty-server:jetty-server-9.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:jetty:jetty-server:jetty-server-10.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:jetty:jetty-server:jetty-server-11.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:jetty:jetty-server:jetty-server-12.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:trace-annotation')
  testRuntimeOnly project(':dd-java-agent:instrumentation:jax-rs-annotations-2')
  testRuntimeOnly project(':dd-java-agent:instrumentation:jakarta-rs-annotations-3')
  testRuntimeOnly project(':dd-java-agent:instrumentation:jakarta-rs-annotations-3')

  cxf3LatestDepTestImplementation group: 'org.apache.cxf', name: 'cxf-rt-frontend-jaxrs', version: '3.+'
  cxf3LatestDepTestImplementation group: 'org.apache.cxf', name:'cxf-rt-transports-http-jetty', version: '3.+'
  cxf3LatestDepTestCompileOnly group: 'jakarta.ws.rs', name: 'jakarta.ws.rs-api', version: '2.+'

  latestDepTestCompileOnly group: 'jakarta.ws.rs', name: 'jakarta.ws.rs-api', version: '3.+'
  latestDepTestImplementation group: 'org.apache.cxf', name: 'cxf-rt-frontend-jaxrs', version: '+'
  latestDepTestImplementation group: 'org.apache.cxf', name:'cxf-rt-transports-http-jetty', version: '+'
}

configurations.named('latestDepTestRuntimeClasspath') {
  resolutionStrategy {
    force libs.slf4j
  }
}

configurations.named('cxf3LatestDepTestRuntimeClasspath') {
  resolutionStrategy {
    force libs.slf4j
  }
}
