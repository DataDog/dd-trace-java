// this project will run in isolation under the agent's classloader
buildscript {

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "net.bytebuddy:byte-buddy-gradle-plugin:1.10.14" // 1.10.15 gradle plugin has breaking changes
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
  }
}
plugins {
  id "com.github.johnrengelman.shadow"
}
apply from: "$rootDir/gradle/java.gradle"

tasks.register("latestDepTest")

Project parent_project = project
subprojects { Project subProj ->
  apply plugin: "net.bytebuddy.byte-buddy"
  apply plugin: 'muzzle'

  subProj.tasks.withType(Javadoc).configureEach { enabled = false }

  // needs fetching this project's configurations.instrumentationMuzzle
  evaluationDependsOn ':dd-java-agent:agent-tooling'

  subProj.byteBuddy {
    transformation {
      // Applying NoOp optimizes build by applying bytebuddy plugin to only compileJava task
      tasks = ['compileJava', 'compileScala', 'compileKotlin']
      plugin = 'net.bytebuddy.build.Plugin$NoOp'
    }
  }

  subProj.afterEvaluate {
    subProj.byteBuddy {
      transformation {
        tasks = ['compileJava', 'compileScala', 'compileKotlin']
        plugin = 'datadog.trace.agent.tooling.bytebuddy.AgentGradlePlugin'
        classPath = project(':dd-java-agent:agent-tooling').configurations.instrumentationMuzzle + subProj.configurations.compileClasspath + subProj.sourceSets.main.output
      }
    }

    String jdkCompile = null
    if (project.hasProperty('minJavaVersionForTests') && project.getProperty('minJavaVersionForTests') != JavaVersion.VERSION_1_7) {
      def version = JavaVersion.toVersion(project.getProperty('minJavaVersionForTests'))
      def name = "java$version.majorVersion"
      jdkCompile = "main_${name}Implementation"
    }
    dependencies {
      // Apply common dependencies for instrumentation.
      implementation project(':dd-trace-api')
      implementation project(':dd-java-agent:agent-tooling')
      implementation deps.bytebuddy
      if (jdkCompile) {
        "$jdkCompile" project(':dd-trace-api')
        "$jdkCompile" project(':dd-java-agent:agent-tooling')
        "$jdkCompile" deps.bytebuddy
      }

      annotationProcessor deps.autoserviceProcessor
      compileOnly deps.autoserviceAnnotation

      // Include instrumentations instrumenting core JDK classes to ensure interoperability with other instrumentation
      testImplementation project(':dd-java-agent:instrumentation:java-concurrent')
      testImplementation project(':dd-java-agent:instrumentation:java-concurrent:java-completablefuture')
      // FIXME: we should enable this, but currently this fails tests for google http client
      //testImplementation project(':dd-java-agent:instrumentation:http-url-connection')
      testImplementation project(':dd-java-agent:instrumentation:classloading')

      testImplementation project(':dd-java-agent:testing')
      testAnnotationProcessor deps.autoserviceProcessor
      testCompileOnly deps.autoserviceAnnotation
    }

    subProj.tasks.withType(Test).configureEach { subTask ->
      onlyIf { !project.rootProject.hasProperty("skipInstTests") }

      // Make it so all instrumentation subproject tests can be run with a single command.
      if (parent_project.hasProperty(subTask.name)) {
        parent_project.tasks.named(subTask.name).configure {
          dependsOn(subTask)
        }
      }
    }
  }

  parent_project.dependencies {
    implementation project(subProj.getPath())
  }
}

dependencies {
  implementation(project(':dd-java-agent:agent-tooling')) {
    exclude module: ':dd-java-agent:agent-bootstrap'
  }
}

tasks.named('shadowJar').configure {
  duplicatesStrategy = DuplicatesStrategy.FAIL
  dependencies deps.sharedInverse
  dependencies {
    exclude(project(':dd-java-agent:agent-bootstrap'))
    exclude(project(':dd-java-agent:agent-logging'))
    exclude(project(':dd-trace-api'))
    exclude(project(':internal-api'))
    exclude(project(':internal-api:internal-api-8'))
    exclude(dependency('org.slf4j::'))
  }
}

