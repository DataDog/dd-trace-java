ext {
  minJavaVersionForTests = JavaVersion.VERSION_17
}

apply from: "$rootDir/gradle/java.gradle"

// test that webflux5 instrumentation works for webflux6 too
addTestSuite('iastTest')
addTestSuite('bootTest')
addTestSuiteForDir('latestDepTest', 'test')
addTestSuiteExtendingForDir('latestDepForkedTest', 'latestDepTest', 'test')
addTestSuiteExtendingForDir('latestDepBootTest', 'latestDepTest', 'bootTest')

tasks.withType(JavaCompile).each {
  it.configure {
    setJavaVersion(it, 17)
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }
}

tasks.withType(GroovyCompile).each {
  it.javaLauncher = getJavaLauncherFor(17)
}

tasks.named("iastTest", Test) {
  jvmArgs '--add-opens', 'java.base/java.util=ALL-UNNAMED'
}

dependencies {
  testImplementation project(':dd-java-agent:instrumentation:spring:spring-webflux:spring-webflux-5.0')
  testImplementation project(':dd-java-agent:instrumentation:trace-annotation')
  testImplementation project(':dd-java-agent:instrumentation:netty:netty-4.1')
  testImplementation project(':dd-java-agent:instrumentation:netty:netty-promise-4.0')
  testImplementation project(':dd-java-agent:instrumentation:reactor-core-3.1')
  testImplementation project(':dd-java-agent:instrumentation:reactive-streams')
  testImplementation project(':dd-java-agent:instrumentation:reactor-netty-1')

  testImplementation libs.spock.spring
  testImplementation group: 'org.springframework', name: 'spring-webflux', version: '6.0.0'
  testImplementation group: 'io.projectreactor.netty', name: 'reactor-netty', version: '1.1.3'
  testImplementation group: 'org.springframework', name: 'spring-test', version: '6.0.0'

  latestDepTestImplementation group: 'org.springframework', name: 'spring-webflux', version: '6.+'
  latestDepTestImplementation group: 'org.springframework', name: 'spring-context', version: '6.+'
  latestDepTestImplementation group: 'io.projectreactor.netty', name: 'reactor-netty', version: '1.+'
  latestDepTestImplementation group: 'org.springframework', name: 'spring-test', version: '6.+'

  bootTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-webflux', version: '3.0.0'
  bootTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '3.0.0'
  bootTestImplementation project(':dd-java-agent:instrumentation:spring:spring-webflux:spring-webflux-5.0')
  bootTestImplementation libs.spock.spring

  latestDepBootTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-webflux', version: '3.+'
  latestDepBootTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '3.+'
  latestDepBootTestImplementation project(':dd-java-agent:instrumentation:spring:spring-webflux:spring-webflux-5.0')
  latestDepBootTestImplementation libs.spock.spring

  iastTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-reactor-netty', version: '3.0.0'
  iastTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-json', version: '3.0.0'
  iastTestImplementation project(':dd-java-agent:agent-iast:iast-test-fixtures')
  iastTestImplementation project(':dd-java-agent:instrumentation:spring:spring-webflux:spring-webflux-5.0')
  iastTestImplementation project(':dd-java-agent:instrumentation:jackson-core')
  iastTestImplementation project(':dd-java-agent:instrumentation:jackson-core:jackson-core-2.12')

  iastTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-webflux', version: '3.0.0'
  iastTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '3.0.0'
  iastTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-reactor-netty', version: '3.0.0'
  iastTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-json', version: '3.0.0'
  iastTestImplementation libs.spock.spring
}

