muzzle {
  pass {
    group = 'org.springframework'
    module = 'spring-messaging'
    versions = "[4.0.0.RELEASE,)"
    assertInverse = true
  }
}

apply from: "$rootDir/gradle/java.gradle"

testJvmConstraints {
  minJavaVersion = JavaVersion.VERSION_17
}

addTestSuiteForDir('latestDepTest', 'test')

["compileTestGroovy", "compileLatestDepTestGroovy"].each { name ->
  tasks.named(name, GroovyCompile) {
    configureCompiler(it, 17)
  }
}

dependencies {
  compileOnly group: 'org.springframework', name: 'spring-messaging', version: '4.0.0.RELEASE'
  testImplementation project(':dd-java-agent:instrumentation:aws-java:aws-java-common')

  // capture SQS send and receive spans, propagate trace details in messages
  testImplementation project(':dd-java-agent:instrumentation:apache-httpclient:apache-httpclient-4.0')
  testImplementation project(':dd-java-agent:instrumentation:aws-java:aws-java-sdk-2.2')
  testImplementation project(':dd-java-agent:instrumentation:aws-java:aws-java-sqs-2.0')

  testImplementation group: 'jakarta.annotation', name: 'jakarta.annotation-api', version: '2.0.0'
  testImplementation group: 'io.awspring.cloud', name: 'spring-cloud-aws-sqs', version: '3.0.1', {
    exclude group: 'org.slf4j', module: 'slf4j-api'
  }
  testImplementation group: 'org.elasticmq', name: 'elasticmq-rest-sqs_2.13', version: '1.2.3'

  latestDepTestImplementation group: 'org.springframework', name: 'spring-messaging', version: '6.+', {
    exclude group: 'org.slf4j', module: 'slf4j-api'
  }

}
