
muzzle {
  pass {
    group = 'org.springframework.cloud'
    module = 'spring-cloud-netflix-zuul'
    versions = "[,]"
    extraDependency "com.netflix.zuul:zuul-core:1.3.1"
    extraDependency "javax.servlet:javax.servlet-api:3.1.0"
    assertInverse = true
  }
}

apply from: "$rootDir/gradle/java.gradle"

tasks.named("compileJava", JavaCompile) {
  // TODO
  // public class HeaderUtils {
  //        ^
  //   ZipException opening "aspectjweaver-1.8.13.jar": Invalid CEN header (invalid zip64 extra data field size)
  configureCompiler(it, 17, JavaVersion.VERSION_1_8)
  doFirst {
    println it.javaCompiler.get().executablePath
  }
}

dependencies {
  compileOnly group: 'org.springframework.cloud', name: 'spring-cloud-netflix-zuul', version: '2.0.0.RELEASE'
  compileOnly group: 'com.netflix.zuul', name: 'zuul-core', version: '1.3.1' // "optional" dependency of ^^
  compileOnly group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'

  testImplementation project(':dd-java-agent:instrumentation:servlet:javax-servlet:javax-servlet-common')
  testImplementation project(':dd-java-agent:instrumentation:servlet:javax-servlet:javax-servlet-3.0')
  testImplementation project(':dd-java-agent:instrumentation:spring:spring-webmvc:spring-webmvc-3.1')

  // These two are used need for spans to propagate correctly:
  testImplementation project(':dd-java-agent:instrumentation:apache-httpclient:apache-httpclient-4.0')
  testImplementation project(':dd-java-agent:instrumentation:java:java-net:java-net-1.8')

  // These two version sets are compatible together:
  testImplementation enforcedPlatform('org.springframework.boot:spring-boot-dependencies:2.3.5.RELEASE') {
    exclude group: 'net.bytebuddy', module: 'byte-buddy' // we don't want this shared dependency pinned
    // exclude transitive Groovy and JUnit since they're too old
    // and conflict with the ones that Spock uses
    exclude group: 'org.codehaus.groovy'
    exclude group: 'org.junit'
    exclude group: 'org.junit.jupiter'
    exclude group: 'org.junit.platform'
    exclude group: 'org.junit.vintage'
  }
  testImplementation enforcedPlatform('org.springframework.cloud:spring-cloud-netflix-dependencies:2.2.6.RELEASE')

  testImplementation(group: 'org.springframework.boot', name: 'spring-boot-starter-test') {
    // exclude transitive Groovy and JUnit since they're too old
    // and conflict with the ones that Spock uses
    exclude group: 'org.codehaus.groovy'
    exclude group: 'org.junit'
    exclude group: 'org.junit.jupiter'
    exclude group: 'org.junit.platform'
    exclude group: 'org.junit.vintage'
  }
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  testImplementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-zuul'
  testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind'

  testImplementation(libs.spock.spring)
}
