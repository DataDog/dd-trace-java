// This file includes software developed at SignalFx

muzzle {
  // We have two independent covariants, so we have to test them independently.
  pass {
    group = 'org.springframework.data'
    module = 'spring-data-commons'
    versions = "[1.8.0.RELEASE,3)"
    extraDependency "org.springframework:spring-aop:1.2"
    assertInverse = true
  }

  pass {
    group = 'org.springframework'
    module = 'spring-aop'
    versions = "[1.2,6)"
    extraDependency "org.springframework.data:spring-data-commons:1.8.0.RELEASE"
    assertInverse = true
    skipFromReport = true
  }
}

apply from: "$rootDir/gradle/java.gradle"

testJvmConstraint {
  // TODO Java 17: This version of spring-data doesn't support Java 17
  maxJavaVersionForTests = JavaVersion.VERSION_15
}

addTestSuiteForDir('latestDepTest', 'test')

// DQH - API changes that impact instrumentation occurred in spring-data-commons in March 2014.
// For now, that limits support to spring-data-commons 1.9.0 (maybe 1.8.0).
// For testing, chose a couple spring-data modules that are old enough to work with 1.9.0.
dependencies {
  compileOnly(group: 'org.springframework.data', name: 'spring-data-commons', version: '1.8.0.RELEASE')
  compileOnly(group: 'org.springframework', name: 'spring-aop', version: '1.2')
  implementation project(':dd-java-agent:agent-debugger:debugger-bootstrap')

  testImplementation group: 'org.spockframework', name: 'spock-spring', version: libs.versions.spock.get()
  testImplementation group: 'org.springframework', name: 'spring-test', version: '3.0.0.RELEASE'

  // JPA dependencies
  testImplementation project(':dd-java-agent:instrumentation:jdbc')
  testImplementation group: 'org.springframework.data', name: 'spring-data-jpa', version: '1.8.0.RELEASE'
  testImplementation group: 'com.mysema.querydsl', name: 'querydsl-jpa', version: '3.7.4'
  testImplementation group: 'org.hsqldb', name: 'hsqldb', version: '2.0.0'
  testImplementation group: 'org.hibernate', name: 'hibernate-entitymanager', version: '4.3.0.Final'

  latestDepTestImplementation group: 'org.springframework', name: 'spring-test', version: '5.+'
  latestDepTestImplementation group: 'org.springframework', name: 'spring-context', version: '5.+'

  latestDepTestImplementation group: 'org.springframework.data', name: 'spring-data-commons', version: '2.+'
  latestDepTestImplementation group: 'org.springframework.data', name: 'spring-data-jpa', version: '2.3.+'
}

