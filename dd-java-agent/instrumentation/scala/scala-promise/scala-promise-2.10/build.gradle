
muzzle {
  pass {
    group = 'org.scala-lang'
    module = "scala-library"
    versions = "[2.9.3,2.13)"
    assertInverse = true
  }
}

// We need to set up the spotless targets here before we apply the standard settings to avoid
// having all the scala files in the common test directories being added as well, making spotless
// fail because the files are outside this project directory
apply plugin: 'com.diffplug.spotless'
spotless {
  groovy {
    target('src/**/*.groovy')
  }
  scala {
    target('src/**/*.scala')
  }
}
project.ext.groovySkipJavaExclude = true

apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'scala' // Don't use test-with-scala since we want to pick our own version.

// Ensure the common project is evaluated before we access its source sets
evaluationDependsOn(':dd-java-agent:instrumentation:scala:scala-promise:scala-promise-common')

addTestSuiteForDir('latestDepTest', 'test')
addTestSuiteExtendingForDir('latestDepForkedTest', 'latestDepTest', 'forkedTest')

tasks.named("latestDepTest", Test) {
  finalizedBy 'latestDepForkedTest'
}

sourceSets {
  test.groovy.srcDir project(':dd-java-agent:instrumentation:scala:scala-promise:scala-promise-common').sourceSets.test.groovy
  test.groovy.srcDir sourceSets.latestDepForkedTest.groovy
  latestDepTest.groovy.srcDir project(':dd-java-agent:instrumentation:scala:scala-promise:scala-promise-common').sourceSets.test.groovy
  latestDepForkedTest.groovy.srcDir project(':dd-java-agent:instrumentation:scala:scala-promise:scala-promise-common').sourceSets.test.groovy

  test.scala.srcDir project(':dd-java-agent:instrumentation:scala:scala-promise:scala-promise-common').sourceSets.test.scala
  latestDepTest.scala.srcDir project(':dd-java-agent:instrumentation:scala:scala-promise:scala-promise-common').sourceSets.test.scala
  latestDepForkedTest.scala.srcDir project(':dd-java-agent:instrumentation:scala:scala-promise:scala-promise-common').sourceSets.test.scala
}

tasks.named("compileTestGroovy", GroovyCompile) {
  classpath += files(sourceSets.test.scala.classesDirectory)
  dependsOn "compileTestScala"
}

tasks.named("compileLatestDepTestGroovy", GroovyCompile) {
  classpath += files(sourceSets.latestDepTest.scala.classesDirectory)
  dependsOn "compileLatestDepTestScala"
}

tasks.named("compileLatestDepForkedTestGroovy", GroovyCompile) {
  classpath += files(sourceSets.latestDepForkedTest.scala.classesDirectory)
  dependsOn "compileLatestDepForkedTestScala"
}

tasks.withType(ScalaCompile).configureEach {
  // Despite the compatibility matrix, scala compiler doesn't properly
  // get the right compiler flags with JDK11 toolchain. Using 8 works.
  // * https://docs.scala-lang.org/overviews/jdk-compatibility/overview.html
  // * https://github.com/gradle/gradle/issues/19456
  configureCompiler(it, 8)
}


dependencies {
  // first version that works with the tests on JDK11 (without dependency problems):
  compileOnly group: 'org.scala-lang', name: 'scala-library', version: '2.10.7'
  implementation project(':dd-java-agent:instrumentation:scala:scala-promise:scala-promise-common')

  testImplementation group: 'org.scala-lang', name: 'scala-library', version: '2.10.7'
  testImplementation project(':dd-java-agent:instrumentation:scala:scala-promise:scala-promise-common')

  latestDepTestImplementation group: 'org.scala-lang', name: 'scala-library', version: '2.12.+'
  latestDepTestImplementation project(':dd-java-agent:instrumentation:scala:scala-promise:scala-promise-common')
}
