
muzzle {
  pass {
    group = 'org.scala-lang'
    module = "scala-library"
    versions = "[2.8,2.12)"
    assertInverse = true
  }
}

apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'scala' // Don't use test-with-scala since we want to pick our own version.

/**
 * This project has instrumentation to support Scala's copy of ForkJoinPool which was added in 2.8 and removed in 2.11.
 * It also has additional generic tests for scala context propagation support beyond 2.11 which are covered in latest12Test and latestDepTest.
 */
addTestSuite('latestDepTest')
addTestSuiteForDir('latest12Test', 'latestDepTest')
addTestSuiteForDir('latest11Test', 'test')

tasks.named("compileTestGroovy", GroovyCompile) {
  classpath += files(sourceSets.test.scala.classesDirectory)
}
tasks.named("compileLatest12TestGroovy", GroovyCompile) {
  classpath += files(sourceSets.latest12Test.scala.classesDirectory)
}
tasks.named("compileLatest11TestGroovy", GroovyCompile) {
  classpath += files(sourceSets.latest11Test.scala.classesDirectory)
}
tasks.named("compileLatestDepTestGroovy", GroovyCompile) {
  classpath += files(sourceSets.latestDepTest.scala.classesDirectory)
}

dependencies {
  // first version that works with the tests and can build on JDK11 (without dependency problems):
  compileOnly group: 'org.scala-lang', name: 'scala-library', version: '2.10.7'
  testImplementation group: 'org.scala-lang', name: 'scala-library', version: '2.10.7'

  testImplementation project(':dd-java-agent:instrumentation:trace-annotation')

  latest11TestImplementation group: 'org.scala-lang', name: 'scala-library', version: '2.11.+'
  latest12TestImplementation group: 'org.scala-lang', name: 'scala-library', version: '2.12.+'
  latestDepTestImplementation group: 'org.scala-lang', name: 'scala-library', version: '2.+' // scala-lang 3.x requires scala 3.x compiler
  latestDepTestImplementation project(':dd-java-agent:instrumentation:scala:scala-promise:scala-promise-2.13')
}

tasks.named("latestDepTest", Test) {
  dependsOn "latest11Test"
  dependsOn "latest12Test"
}
