// Set properties before any plugins get loaded
ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

apply from: "$rootDir/gradle/java.gradle"

muzzle {
  pass {
    group = 'io.vertx'
    module = 'vertx-mysql-client'
    versions = '[3.9.0,4)'
    assertInverse = true
  }
  fail {
    group = 'io.vertx'
    module = 'vertx-pg-client'
    versions = '[,]'
  }
}

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  latestDepTest {
    dirName = 'test'
  }

  latestDepForkedTest {
    extendsFrom latestDepTest
    dirName = 'test'
  }
}

apply from: "$rootDir/gradle/configure_tests.gradle"

latestDepTest {
  finalizedBy 'latestDepForkedTest'
}

dependencies {
  main_java8CompileOnly group: 'io.vertx', name: 'vertx-mysql-client', version: '3.9.0'

  testImplementation group: 'io.vertx', name: 'vertx-mysql-client', version: '3.9.0'

  // This is needed for the test container to start
  testImplementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.23'
  testImplementation "org.testcontainers:mysql:${versions.testcontainers}"

  latestDepTestImplementation group: 'io.vertx', name: 'vertx-mysql-client', version: '3.+'
}

tasks.withType(Test).configureEach {
  usesService(testcontainersLimit)
}
