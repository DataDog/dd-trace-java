// Set properties before any plugins get loaded
ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

// We need to set up the spotless targets here before we apply the standard settings to avoid
// having all the scala files in the common test directories being added as well, making spotless
// fail because the files are outside this project directory
apply plugin: 'com.diffplug.spotless'
spotless {
  groovy {
    target('src/**/*.groovy')
  }
  scala {
    target('src/**/*.scala')
  }
}
project.ext.groovySkipJavaExclude = true

apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'scala'

apply plugin: 'org.unbroken-dome.test-sets'
testSets {
  latestDepTest {
    dirName = 'test'
  }
}

sourceSets {
  test.groovy.srcDir project(':dd-java-agent:instrumentation:http4s-blaze-client-0.23').sourceSets.test.groovy
  latestDepTest.groovy.srcDir project(':dd-java-agent:instrumentation:http4s-blaze-client-0.23').sourceSets.test.groovy

  test.scala.srcDir project(':dd-java-agent:instrumentation:http4s-blaze-client-0.23').sourceSets.test.scala
  latestDepTest.scala.srcDir project(':dd-java-agent:instrumentation:http4s-blaze-client-0.23').sourceSets.test.scala
}

tasks.named('compileScala').configure {
  // Scala only depends declared dependencies
  classpath = sourceSets.main.compileClasspath
}

tasks.named('compileJava').configure {
  // Java depends on Scala
  classpath += files(sourceSets.main.scala.classesDirectory)
}

tasks.named('compileTestGroovy').configure {
  // Groovy tests depend on Scala tests
  classpath += files(sourceSets.test.scala.classesDirectory)
}

tasks.named('compileLatestDepTestGroovy').configure {
  // Groovy tests depend on Scala tests
  classpath += files(sourceSets.latestDepTest.scala.classesDirectory)
}

tasks.withType(ScalaCompile) {
  scalaCompileOptions.with {
    force = true
  }
}
