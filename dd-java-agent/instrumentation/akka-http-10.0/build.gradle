// Set properties before any plugins get loaded
ext {
  lagomTestMaxJavaVersionForTests = JavaVersion.VERSION_1_8
}

apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'scala'
apply plugin: 'call-site-instrumentation'

// Since we are using different scala versions for different test sets,
// we put the test classes in the baseTest test set so that the scala
// version is not inherited
addTestSuite('baseTest')
addTestSuiteForDir('version101Test', 'baseTest')
addTestSuite('latestDepTest')
addTestSuite('lagomTest')
addTestSuite('iastTest')
addTestSuiteForDir 'version101IastTest', 'iastTest'
addTestSuiteForDir 'version102IastTest', 'iastTest'
addTestSuiteForDir 'latestDepIastTest', 'iastTest'

compileLagomTestJava {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

compileLagomTestGroovy {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

muzzle {
  pass {
    group = 'com.typesafe.akka'
    module = 'akka-http_2.11'
    versions = "[10.0.0,10.1.0)"
    // later versions of akka-http expect streams to be provided
    extraDependency 'com.typesafe.akka:akka-stream_2.11:2.4.14'
  }
  pass {
    group = 'com.typesafe.akka'
    module = 'akka-http_2.12'
    versions = "[10.0.0,10.1.0)"
    // later versions of akka-http expect streams to be provided
    extraDependency 'com.typesafe.akka:akka-stream_2.12:2.4.14'
  }
  pass {
    group = 'com.typesafe.akka'
    module = 'akka-http_2.11'
    versions = "[10.1.0,)"
    // later versions of akka-http expect streams to be provided
    extraDependency 'com.typesafe.akka:akka-stream_2.11:2.5.11'
  }
  pass {
    group = 'com.typesafe.akka'
    module = 'akka-http_2.12'
    versions = "[10.1.0,)"
    // later versions of akka-http expect streams to be provided
    extraDependency 'com.typesafe.akka:akka-stream_2.12:2.5.11'
  }
  //There is no akka-http 10.0.x series for scala 2.13
  pass {
    group = 'com.typesafe.akka'
    module = 'akka-http_2.13'
    versions = "[10.1.8,)"
    // later versions of akka-http expect streams to be provided
    extraDependency 'com.typesafe.akka:akka-stream_2.13:2.5.23'
  }
}

configurations {
  testArtifacts

  iastTestImplementation.extendsFrom(baseTestImplementation)

  version101IastTestImplementation.extendsFrom(version101TestImplementation)
  version101IastTestRuntimeOnly.extendsFrom(iastTestRuntimeOnly)

  version102IastTestRuntimeOnly.extendsFrom(iastTestRuntimeOnly)

  latestDepIastTestRuntimeOnly.extendsFrom(iastTestRuntimeOnly)
}

// Create test artifacts so play-2.6 can reuse the akka-http test instrumentation
artifacts {
  testArtifacts baseTestJar
}

sourceSets {
  latestDepTest.groovy.srcDir sourceSets.baseTest.groovy
  latestDepTest.scala.srcDir sourceSets.baseTest.scala
}

dependencies {
  compileOnly deps.scala211
  compileOnly group: 'com.typesafe.akka', name: 'akka-http_2.11', version: '10.0.0'

  // These are the common dependencies that are inherited by the other test sets
  testImplementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.6.0'
  testImplementation project(':dd-java-agent:instrumentation:trace-annotation')
  testImplementation project(':dd-java-agent:instrumentation:akka-concurrent')
  testImplementation project(':dd-java-agent:instrumentation:scala-concurrent')
  testImplementation project(':dd-java-agent:instrumentation:scala-promise:scala-promise-2.10')
  testImplementation project(':dd-java-agent:instrumentation:scala-promise:scala-promise-2.13')

  baseTestImplementation deps.scala211
  // First 10.0.x version with a convenient way to test http2 support
  baseTestImplementation group: 'com.typesafe.akka', name: 'akka-http_2.11', version: '10.0.10'
  baseTestImplementation group: 'com.typesafe.akka', name: 'akka-http2-support_2.11', version: '10.0.10'

  iastTestImplementation group: 'com.typesafe.akka', name: 'akka-http-jackson_2.11', version: '10.0.10'
  iastTestImplementation(testFixtures(project(':dd-java-agent:agent-iast')))
  iastTestCompileOnly group: 'de.thetaphi', name: 'forbiddenapis', version: '3.4'
  iastTestRuntimeOnly project(':dd-java-agent:instrumentation:jackson-core')
  iastTestRuntimeOnly project(':dd-java-agent:instrumentation:iast-instrumenter')
  iastTestRuntimeOnly project(':dd-java-agent:instrumentation:akka-http-10.2-iast')

  // There are some internal API changes in 10.1 that we would like to test separately for
  version101TestImplementation deps.scala212
  version101TestImplementation group: 'com.typesafe.akka', name: 'akka-http_2.12', version: '10.1.+'
  version101TestImplementation group: 'com.typesafe.akka', name: 'akka-http2-support_2.12', version: '10.1.+'
  version101TestImplementation group: 'com.typesafe.akka', name: 'akka-stream_2.12', version: '2.5.+'

  version101IastTestImplementation group: 'com.typesafe.akka', name: 'akka-http-jackson_2.12', version: '10.1.+'
  version101IastTestImplementation(testFixtures(project(':dd-java-agent:agent-iast')))

  version102IastTestImplementation deps.scala212
  version102IastTestImplementation group: 'com.typesafe.akka', name: 'akka-http_2.12', version: '10.2.+'
  version102IastTestImplementation group: 'com.typesafe.akka', name: 'akka-stream_2.12', version: '2.6.+'
  version102IastTestImplementation group: 'com.typesafe.akka', name: 'akka-http-jackson_2.12', version: '10.2.+'
  version102IastTestImplementation(testFixtures(project(':dd-java-agent:agent-iast')))

  latestDepTestImplementation deps.scala213
  latestDepTestImplementation group: 'com.typesafe.akka', name: 'akka-http_2.13', version: '10.2.+'
  // http2 support is included in akka-http since 10.2.x
  latestDepTestImplementation group: 'com.typesafe.akka', name: 'akka-stream_2.13', version: '2.6.+'

  // TODO: test with Scala 3
  latestDepIastTestImplementation deps.scala213
  // Use akka-2.8.+ since latest akka-http release is still not compatible with akka-2.9.0-M1+.
  latestDepIastTestImplementation group: 'com.typesafe.akka', name: 'akka-http_2.13', version: '10.+'
  latestDepIastTestImplementation group: 'com.typesafe.akka', name: 'akka-stream_2.13', version: '2.8.+'
  latestDepIastTestImplementation group: 'com.typesafe.akka', name: 'akka-actor_2.13', version: '2.8.+'
  latestDepIastTestImplementation group: 'com.typesafe.akka', name: 'akka-http-jackson_2.13', version: '10.+'
  latestDepIastTestImplementation(testFixtures(project(':dd-java-agent:agent-iast')))

  lagomTestImplementation deps.scala211
  lagomTestImplementation group: 'com.typesafe.akka', name: 'akka-http_2.11', version: '10.0.0'
  lagomTestImplementation project(':dd-java-agent:instrumentation:akka-init')
  lagomTestImplementation group: 'com.lightbend.lagom', name: 'lagom-javadsl-testkit_2.11', version: '1.4.0'
}

tasks.named("test").configure {
  dependsOn "baseTest"
  dependsOn "version101Test"
  dependsOn "lagomTest"
  dependsOn "iastTest"
  dependsOn "version101IastTest"
  dependsOn "version102IastTest"
}

tasks.named('latestDepTest').configure {
  dependsOn "latestDepIastTest"
}

compileBaseTestGroovy {
  classpath = classpath.plus(files(compileBaseTestScala.destinationDirectory))
  dependsOn "compileBaseTestScala"
}

compileVersion101TestGroovy {
  classpath = classpath.plus(files(compileVersion101TestScala.destinationDirectory))
  dependsOn "compileVersion101TestScala"
}

compileLatestDepTestGroovy {
  classpath = classpath.plus(files(compileLatestDepTestScala.destinationDirectory))
  dependsOn "compileLatestDepTestScala"
}
