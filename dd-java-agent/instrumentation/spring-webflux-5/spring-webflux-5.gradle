// Set properties before any plugins get loaded
ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

muzzle {
  pass {
    name = "webflux_5.0.0+_with_netty_0.8.0"
    group = "org.springframework"
    module = "spring-webflux"
    versions = "[5.0.0.RELEASE,)"
    assertInverse = true
    extraDependency "io.projectreactor.netty:reactor-netty:0.8.0.RELEASE"
  }

  pass {
    name = "webflux_5.0.0_with_ipc_0.7.0"
    group = "org.springframework"
    module = "spring-webflux"
    versions = "[5.0.0.RELEASE,)"
    assertInverse = true
    extraDependency "io.projectreactor.ipc:reactor-netty:0.7.0.RELEASE"
  }

  pass {
    name = "netty_0.8.0+_with_spring-webflux:5.1.0"
    group = "io.projectreactor.netty"
    module = "reactor-netty"
    versions = "[0.8.0.RELEASE,)"
    extraDependency "org.springframework:spring-webflux:5.1.0.RELEASE"
  }

  pass {
    name = "ipc_0.7.0+_with_spring-webflux:5.0.0"
    group = "io.projectreactor.ipc"
    module = "reactor-netty"
    versions = "[0.7.0.RELEASE,)"
    extraDependency "org.springframework:spring-webflux:5.0.0.RELEASE"
  }
}

apply from: "$rootDir/gradle/java.gradle"

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  latestDepTest {
    dirName = 'test'
  }

  boot20Test {}
  latestBoot20Test {
    dirName = 'boot20Test'
  }

  boot24Test {}
  latestBoot24Test {
    dirName = 'boot24Test'
  }
}

dependencies {
  main_java8CompileOnly group: 'org.springframework', name: 'spring-webflux', version: '5.0.0.RELEASE'

  testCompile project(':dd-java-agent:instrumentation:trace-annotation')
  testCompile project(':dd-java-agent:instrumentation:netty-4.1')
  testCompile project(':dd-java-agent:instrumentation:netty-promise-4')
  testCompile project(':dd-java-agent:instrumentation:reactor-core-3.1')
  testCompile group: 'org.spockframework', name: 'spock-spring', version: '1.1-groovy-2.4'

  testCompile group: 'org.springframework', name: 'spring-webflux', version: '5.0.0.RELEASE'
  testCompile group: 'io.projectreactor.ipc', name: 'reactor-netty', version: '0.7.0.RELEASE'
  testCompile group: 'org.springframework', name: 'spring-test', version: '5.0.0.RELEASE'

  latestDepTestCompile group: 'org.springframework', name: 'spring-webflux', version: '5.+'
  // This is the key dep that breaks things, 1.x doesn't work
  latestDepTestCompile group: 'io.projectreactor.netty', name: 'reactor-netty', version: '0.+'
  latestDepTestCompile group: 'org.springframework', name: 'spring-test', version: '5.+'

  boot20TestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-webflux', version: '2.0.0.RELEASE'
  boot20TestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.0.0.RELEASE'
  boot20TestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-reactor-netty', version: '2.0.0.RELEASE'

  latestBoot20TestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-webflux', version: '2.0.+'
  latestBoot20TestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.0.+'
  latestBoot20TestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-reactor-netty', version: '2.0.+'

  boot24TestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-webflux', version: '2.4.0'
  boot24TestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.4.0'
  boot24TestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-reactor-netty', version: '2.4.0'

  latestBoot24TestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-webflux', version: '2.4.+'
  latestBoot24TestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.4.+'
  latestBoot24TestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-reactor-netty', version: '2.4.+'
}

test.dependsOn boot20Test
test.dependsOn latestBoot20Test
test.dependsOn boot24Test
test.dependsOn latestBoot24Test
