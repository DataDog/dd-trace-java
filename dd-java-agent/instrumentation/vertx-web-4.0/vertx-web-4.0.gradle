// Set properties before any plugins get loaded
ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
  // TODO Java 17: This version of vertx-web doesn't support Java 17
  maxJavaVersionForTests = JavaVersion.VERSION_15
}

apply from: "$rootDir/gradle/java.gradle"

apply plugin: 'org.unbroken-dome.test-sets'

muzzle {
  pass {
    group = 'io.vertx'
    module = "vertx-web"
    versions = "[4.0.0,5)"
    assertInverse = true
  }
}

testSets {
  latestDepTest {
    dirName = 'test'
  }
}

configurations {
  testArtifacts
}

// Create test artifacts so vertx-rx can reuse the server test instrumentation and base class
artifacts {
  testArtifacts testJar
}

dependencies {
  main_java8CompileOnly group: 'io.vertx', name: 'vertx-web', version: '4.2.7'

  testImplementation project(':dd-java-agent:instrumentation:netty-4.1')
  testImplementation project(':dd-java-agent:instrumentation:trace-annotation')

  testImplementation group: 'io.vertx', name: 'vertx-web', version: '4.0.0'
  testImplementation group: 'io.vertx', name: 'vertx-web-client', version: '4.0.0'

  latestDepTestImplementation group: 'io.vertx', name: 'vertx-web', version: '4.+'
  latestDepTestImplementation group: 'io.vertx', name: 'vertx-web-client', version: '4.+'
}
