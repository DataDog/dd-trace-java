// Set properties before any plugins get loaded
ext {
  // vertx-web doesn't support Java 17 until v4.2
  maxJavaVersionForTests = JavaVersion.VERSION_15
  // unbound it for latest
  latestDepTestMinJavaVersionForTests = JavaVersion.VERSION_11
  latestDepForkedTestMinJavaVersionForTests = JavaVersion.VERSION_11
  latest4xTestMaxJavaVersionForTests = JavaVersion.VERSION_25
  latest4xForkedTestMaxJavaVersionForTests = JavaVersion.VERSION_25
  latestDepTestMaxJavaVersionForTests = JavaVersion.VERSION_25
  latestDepForkedTestMaxJavaVersionForTests = JavaVersion.VERSION_25
}

apply from: "$rootDir/gradle/java.gradle"

muzzle {
  pass {
    group = 'io.vertx'
    module = "vertx-web"
    versions = "[4.0.0,)"
    assertInverse = true
  }
}

addTestSuiteForDir('latestDepTest', 'latestDepTest')
addTestSuiteExtendingForDir('latestDepForkedTest', 'latestDepTest', 'latestDepTest')
addTestSuiteForDir('latest4xTest', 'test')
addTestSuiteExtendingForDir('latest4xForkedTest', 'latest4xTest', 'test')

configurations {
  testArtifacts
}

// Create test artifacts so vertx-rx can reuse the server test instrumentation and base class
artifacts {
  testArtifacts testJar
}

dependencies {
  api project(':dd-java-agent:instrumentation:netty-4.1-shared')

  compileOnly group: 'io.vertx', name: 'vertx-web', version: '4.0.0'

  testImplementation project(':dd-java-agent:instrumentation:netty-4.1')
  testImplementation project(':dd-java-agent:instrumentation:trace-annotation')
  testImplementation(testFixtures(project(':dd-java-agent:agent-iast')))
  testRuntimeOnly project(':dd-java-agent:instrumentation:iast-instrumenter')

  testImplementation group: 'io.vertx', name: 'vertx-web', version: '4.0.0'
  testImplementation group: 'io.vertx', name: 'vertx-web-client', version: '4.0.0'

  testImplementation testFixtures(project(':dd-java-agent:appsec'))

  testRuntimeOnly project(':dd-java-agent:instrumentation:jackson-core')
  testRuntimeOnly project(':dd-java-agent:instrumentation:netty-buffer-4')

  latest4xTestImplementation group: 'io.vertx', name: 'vertx-web', version: '4.+'
  latest4xTestImplementation group: 'io.vertx', name: 'vertx-web-client', version: '4.+'

  latestDepTestImplementation group: 'io.vertx', name: 'vertx-web', version: '+'
  latestDepTestImplementation group: 'io.vertx', name: 'vertx-web-client', version: '+'
}

[compileLatestDepTestJava, compileLatestDepForkedTestJava].each {
  setJavaVersion(it, 11)
}
[compileLatestDepForkedTestGroovy, compileLatestDepTestGroovy].each {
  it.javaLauncher = getJavaLauncherFor(11)
}
[latest4xForkedTest, latest4xTest].each {
  it.jvmArgs += '-Dtest.dd.latest4xTest=true'
}
