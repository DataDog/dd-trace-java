// Set properties before any plugins get loaded
ext {
  // vertx-web doesn't support Java 17 until v4.2
  maxJavaVersionForTests = JavaVersion.VERSION_15
  latestDepTestMaxJavaVersionForTests = JavaVersion.VERSION_17
}

apply from: "$rootDir/gradle/java.gradle"

muzzle {
  pass {
    group = 'io.vertx'
    module = "vertx-web"
    versions = "[4.0.0,5)"
    assertInverse = true
  }
}

addTestSuiteForDir('latestDepTest', 'test')

configurations {
  testArtifacts
}

// Create test artifacts so vertx-rx can reuse the server test instrumentation and base class
artifacts {
  testArtifacts testJar
}

dependencies {
  api project(':dd-java-agent:instrumentation:netty-4.1-shared')

  compileOnly group: 'io.vertx', name: 'vertx-web', version: '4.0.0'

  testImplementation project(':dd-java-agent:instrumentation:netty-4.1')
  testImplementation project(':dd-java-agent:instrumentation:trace-annotation')
  testImplementation(testFixtures(project(':dd-java-agent:agent-iast')))
  testRuntimeOnly project(':dd-java-agent:instrumentation:iast-instrumenter')

  testImplementation group: 'io.vertx', name: 'vertx-web', version: '4.0.0'
  testImplementation group: 'io.vertx', name: 'vertx-web-client', version: '4.0.0'

  testImplementation testFixtures(project(':dd-java-agent:appsec'))

  testRuntimeOnly project(':dd-java-agent:instrumentation:jackson-core')
  testRuntimeOnly project(':dd-java-agent:instrumentation:netty-buffer-4')

  // TODO support v>=4.5
  latestDepTestImplementation group: 'io.vertx', name: 'vertx-web', version: '4.4.+'
  latestDepTestImplementation group: 'io.vertx', name: 'vertx-web-client', version: '4.4.+'
}
