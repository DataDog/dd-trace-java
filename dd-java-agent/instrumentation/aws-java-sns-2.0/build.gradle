muzzle {
  pass {
    group = "software.amazon.awssdk"
    module = "sns"
    versions = "[2.17.84,3)"
    assertInverse = true
  }
}

apply from: "$rootDir/gradle/java.gradle"

addTestSuiteForDir('latestDepTest', 'test')
addTestSuiteExtendingForDir('latestDepForkedTest', 'latestDepTest', 'test')

dependencies {
  compileOnly group: 'software.amazon.awssdk', name: 'sns', version: '2.25.40'
  testImplementation project(':dd-java-agent:instrumentation:aws-common')

  // Include httpclient instrumentation for testing because it is a dependency for aws-sdk.
  testImplementation project(':dd-java-agent:instrumentation:apache-httpclient-4')
  testImplementation project(':dd-java-agent:instrumentation:aws-java-sdk-2.2')
  testImplementation 'software.amazon.awssdk:sns:2.25.40'
  // SQS is used to act as the "Subscriber" of the SNS topic.
  testImplementation 'software.amazon.awssdk:sqs:2.25.40'
  testImplementation group: 'org.testcontainers', name: 'localstack', version: libs.versions.testcontainers.get()

  latestDepTestImplementation group: 'software.amazon.awssdk', name: 'sns', version: '+'
}

tasks.withType(Test).configureEach {
  usesService(testcontainersLimit)
}
