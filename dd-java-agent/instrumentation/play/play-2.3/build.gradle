muzzle {
  extraRepository("typesafe", "https://repo.typesafe.com/typesafe/maven-releases/")

  pass {
    group = 'com.typesafe.play'
    module = 'play_2.11'
    versions = '[2.3.0,2.4)'
    assertInverse = true
  }
  fail {
    group = 'com.typesafe.play'
    module = 'play_2.12'
    versions = '[,]'
  }
  fail {
    group = 'com.typesafe.play'
    module = 'play_2.13'
    versions = '[,]'
  }
}

apply from: "$rootDir/gradle/java.gradle"

testJvmConstraints {
  // Play doesn't work with Java 9+ until 2.6.12
  maxJavaVersion = JavaVersion.VERSION_1_8
}

repositories {
  maven {
    // only place that has 'com.typesafe.netty:netty-http-pipelining:1.1.2' publicly accessible
    name = 'typesafe'
    url = 'https://repo.typesafe.com/typesafe/maven-releases/'
  }
}

apply from: "$rootDir/gradle/test-with-scala.gradle"

addTestSuiteForDir('latestDepTest', 'test')

dependencies {
  compileOnly group: 'com.typesafe.play', name: 'play_2.11', version: '2.3.9'

  testImplementation project(':dd-java-agent:instrumentation:netty:netty-3.8')
  testImplementation project(':dd-java-agent:instrumentation:akka:akka-actor-2.5')
  testImplementation project(':dd-java-agent:instrumentation:scala:scala-concurrent-2.8')
  testImplementation project(':dd-java-agent:instrumentation:scala:scala-promise:scala-promise-2.10')
  testImplementation project(':dd-java-agent:instrumentation:scala:scala-promise:scala-promise-2.13')
  testImplementation group: 'com.typesafe.play', name: 'play-java_2.11', version: '2.3.9'
  testImplementation group: 'com.typesafe.play', name: 'play-java-ws_2.11', version: '2.3.9'
  testImplementation(group: 'com.typesafe.play', name: 'play-test_2.11', version: '2.3.9') {
    exclude group: 'org.eclipse.jetty', module: 'jetty-websocket'
  }

  latestDepTestImplementation group: 'com.typesafe.play', name: 'play-java_2.11', version: '2.3.+'
  latestDepTestImplementation group: 'com.typesafe.play', name: 'play-java-ws_2.11', version: '2.3.+'
  latestDepTestImplementation(group: 'com.typesafe.play', name: 'play-test_2.11', version: '2.3.+') {
    exclude group: 'org.eclipse.jetty', module: 'jetty-websocket'
  }
}

tasks.named("compileLatestDepTestGroovy", GroovyCompile) {
  classpath += files(tasks.named('compileLatestDepTestScala').map { it.destinationDirectory })
}
