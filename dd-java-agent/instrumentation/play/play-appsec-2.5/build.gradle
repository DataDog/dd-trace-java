muzzle {
  extraRepository("typesafe", "https://repo.typesafe.com/typesafe/maven-releases/")

  pass {
    group = 'com.typesafe.play'
    module = 'play_2.11'
    versions = '[2.5.0,2.6)'
    assertInverse = true
  }
}

apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'scala'

def buildTestRoutes = tasks.register('buildTestRoutes', JavaExec)

sourceSets {
  test {
    scala {
      srcDir buildTestRoutes
    }
  }
  routeGenerator {
    scala {
      srcDir 'src/routeGenerator/scala'
    }
  }
}

testJvmConstraints {
  // Play doesn't work with Java 9+ until 2.6.12
  maxJavaVersion = JavaVersion.VERSION_1_8
}

repositories {
  maven {
    // for muzzle because of play-2.3
    // only place that has 'com.typesafe.netty:netty-http-pipelining:1.1.2' publicly accessible
    name = 'typesafe'
    url = 'https://repo.typesafe.com/typesafe/maven-releases/'
  }
}

tasks.withType(ScalaCompile).configureEach {
  configureCompiler(it, 8)
}

addTestSuiteForDir('latestDepTest', 'test')
def buildLatestDepTestRoutes = tasks.register('buildLatestDepTestRoutes', JavaExec)
sourceSets {
  latestDepTest {
    scala {
      srcDir buildLatestDepTestRoutes
    }
  }
}

dependencies {
  compileOnly group: 'com.typesafe.play', name: 'play_2.11', version: '2.5.0'
  implementation(project(':dd-java-agent:instrumentation:play:play-appsec-common'))

  routeGeneratorImplementation libs.scala211
  routeGeneratorImplementation group: 'com.typesafe.play', name: "routes-compiler_2.11", version: '2.5.0'

  testRuntimeOnly project(':dd-java-agent:instrumentation:netty:netty-4.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:netty:netty-4.1')
  testRuntimeOnly project(':dd-java-agent:instrumentation:akka:akka-http:akka-http-10.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:akka:akka-actor-2.5')
  testRuntimeOnly project(':dd-java-agent:instrumentation:scala:scala-concurrent-2.8')
  testRuntimeOnly project(':dd-java-agent:instrumentation:scala:scala-promise:scala-promise-2.10')
  testRuntimeOnly project(':dd-java-agent:instrumentation:scala:scala-promise:scala-promise-2.13')

  // All play modules for testing
  testImplementation project(':dd-java-agent:instrumentation:play:play-2.4')

  testImplementation group: 'com.typesafe.play', name: 'play-java_2.11', version: '2.5.0'
  testImplementation group: 'com.typesafe.play', name: 'play-java-ws_2.11', version: '2.5.0'
  testImplementation(group: 'com.typesafe.play', name: 'play-test_2.11', version: '2.5.0') {
    exclude group: 'org.eclipse.jetty.websocket', module: 'websocket-client'
  }

  latestDepTestImplementation group: 'com.typesafe.play', name: 'play-java_2.11', version: '2.5.+'
  latestDepTestImplementation group: 'com.typesafe.play', name: 'play-java-ws_2.11', version: '2.5.+'
  latestDepTestImplementation(group: 'com.typesafe.play', name: 'play-test_2.11', version: '2.5.+') {
    exclude group: 'org.eclipse.jetty.websocket', module: 'websocket-client'
  }
}

buildTestRoutes.configure {
  String routesFile = "${project.projectDir}/src/test/routes/conf/routes"
  def outputDir = layout.buildDirectory.dir('generated/sources/testRoutes/scala')

  it.inputs.file routesFile
  it.outputs.dir outputDir

  it.mainClass.set 'generator.CompileRoutes'
  it.args routesFile, outputDir.get().asFile.absolutePath

  it.classpath configurations.named('routeGeneratorRuntimeClasspath')
  it.classpath tasks.named('compileRouteGeneratorScala')
}

buildLatestDepTestRoutes.configure {
  String routesFile = "${project.projectDir}/src/test/routes/conf/routes"
  def outputDir = layout.buildDirectory.dir('generated/sources/latestDepTestRoutes/scala')

  it.inputs.file routesFile
  it.outputs.dir outputDir

  it.mainClass.set 'generator.CompileRoutes'
  it.args routesFile, outputDir.get().asFile.absolutePath

  it.classpath configurations.named('routeGeneratorRuntimeClasspath')
  it.classpath tasks.named('compileRouteGeneratorScala')
}

tasks.named("compileTestGroovy", GroovyCompile) {
  // Groovy tests need access to generated routes compiled by Scala
  classpath += files(tasks.named('compileTestScala'))
}

tasks.named("compileLatestDepTestGroovy", GroovyCompile) {
  // Groovy tests need access to generated routes compiled by Scala
  classpath += files(tasks.named('compileLatestDepTestScala'))
}
