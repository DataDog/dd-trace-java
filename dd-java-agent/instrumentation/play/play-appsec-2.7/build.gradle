def scalaVersion = '2.11'
def playVersion = '2.7.0'

muzzle {
  extraRepository("typesafe", "https://repo.typesafe.com/typesafe/maven-releases/")

  pass {
    name = 'play27'
    group = 'com.typesafe.play'
    module = 'play-java_2.11'
    versions = "[2.7.0,)"
    assertInverse = true
    javaVersion = 11
  }
  pass {
    name = 'play27'
    group = 'com.typesafe.play'
    module = 'play-java_2.12'
    versions = "[2.7.0,)"
    assertInverse = true
    javaVersion = 11
  }
  pass {
    name = 'play27'
    group = 'com.typesafe.play'
    module = 'play-java_2.13'
    versions = "[2.7.0,)"
    assertInverse = true
    javaVersion = 11
  }
}

apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'scala'

testJvmConstraints {
  minJavaVersion = JavaVersion.VERSION_11
}

repositories {
  maven {
    // for muzzle because of play-2.3
    // only place that has 'com.typesafe.netty:netty-http-pipelining:1.1.2' publicly accessible
    name = 'typesafe'
    url = 'https://repo.typesafe.com/typesafe/maven-releases/'
  }
}

dependencies {
  compileOnly group: 'com.typesafe.play', name: "play-java_$scalaVersion", version: playVersion
  implementation project(':dd-java-agent:instrumentation:play:play-2.6')
  implementation(project(':dd-java-agent:instrumentation:play:play-appsec-common'))

  // TODO
  //  * [x] replace 'latestDepTest' by 'test'
  //  * [x] ensure test fixture is used in 'test' instead of 'latestDepTest'
  //  * [x] use the introduced 'Abstract*Test' classes
  //  * [ ] router.Routes is generated code, find a way to generate it for tests

  testImplementation libs.scala213
  testImplementation project(':dd-java-agent:instrumentation:play:play-2.6')
  // Inherited tests needs play-appsec-2.6 instrumentation
  testImplementation project(':dd-java-agent:instrumentation:play:play-appsec-2.6')
  testImplementation testFixtures(project(':dd-java-agent:instrumentation:play:play-2.6'))

  testImplementation group: 'com.typesafe.play', name: "play-java_2.13", version: '2.+'
  testImplementation(group: 'com.typesafe.play', name: "play-test_2.13", version: '2.+') {
    exclude group: 'org.eclipse.jetty.websocket', module: 'websocket-client'
  }
  testImplementation group: 'com.typesafe.play', name: 'play-akka-http-server_2.13', version: '2.+'


  testRuntimeOnly project(':dd-java-agent:instrumentation:netty:netty-4.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:netty:netty-4.1')
  testRuntimeOnly project(':dd-java-agent:instrumentation:akka:akka-http:akka-http-10.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:akka:akka-actor-2.5')
  testRuntimeOnly project(':dd-java-agent:instrumentation:scala:scala-concurrent-2.8')
  testRuntimeOnly project(':dd-java-agent:instrumentation:scala:scala-promise:scala-promise-2.10')
  testRuntimeOnly project(':dd-java-agent:instrumentation:scala:scala-promise:scala-promise-2.13')
}

configurations.matching({ it.name.startsWith('test') }).configureEach({
  it.resolutionStrategy {
    // logback-classic 1.4.11 doesn't like being loaded in the bootstrap classloader (NPE)
    force group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.5'
  }
})

tasks.named("compileTestJava", JavaCompile) {
  configureCompiler(it, 11)
}

tasks.named("compileTestScala", ScalaCompile) {
  configureCompiler(it, 11)
}

// tasks.named("latestDepTest", Test) {
//   javaLauncher = getJavaLauncherFor(11)
// }

final generatedRoutes = layout.buildDirectory.dir('generated/sources/testRoutes/scala')
sourceSets {
  routeGenerator {
    scala {
      srcDir "${project.projectDir}/src/routeGenerator/scala"
    }
  }
  testGenerated {
    scala {
      srcDir generatedRoutes
    }
  }
}

dependencies {
  routeGeneratorImplementation libs.scala213
  routeGeneratorImplementation group: 'com.typesafe.play', name: "routes-compiler_2.13", version: '2.+'
}

configurations {
  testGeneratedCompileClasspath.extendsFrom(testCompileClasspath)
}

tasks.register('buildTestRoutes', JavaExec) {
  String routesFile = "${project.projectDir}/src/test/routes/conf/routes"
  def outputDir = generatedRoutes

  it.inputs.file routesFile
  it.outputs.dir outputDir

  it.mainClass = 'generator.CompileRoutes'
  it.args routesFile, outputDir.get().asFile.absolutePath

  it.classpath configurations.named('routeGeneratorRuntimeClasspath')
  it.classpath tasks.named('compileRouteGeneratorScala').map { it.destinationDirectory }
  it.classpath tasks.named('compileTestScala').map { it.destinationDirectory }

  it.javaLauncher = getJavaLauncherFor(11)
}

tasks.named("compileTestGeneratedScala", ScalaCompile) {
  configureCompiler(it, 11)
  classpath = classpath + files(tasks.named('compileTestScala').map { it.destinationDirectory })
  dependsOn 'buildTestRoutes'
}

tasks.named("forbiddenApisTestGenerated") {
  enabled = false
}

tasks.named("compileTestGroovy", GroovyCompile) {
  configureCompiler(it, 11)
  classpath = classpath +
    files(tasks.named('compileTestScala').map { it.destinationDirectory }) +
    files(tasks.named('compileTestGeneratedScala').map { it.destinationDirectory })
}

dependencies {
  testRuntimeOnly sourceSets.testGenerated.output
}
