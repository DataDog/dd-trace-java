muzzle {
  extraRepository("typesafe", "https://repo.typesafe.com/typesafe/maven-releases/")

  pass {
    name = 'play27'
    group = 'com.typesafe.play'
    module = 'play-java_2.11'
    versions = "[2.7.0,)"
    assertInverse = true
    javaVersion = 11
  }
  pass {
    name = 'play27'
    group = 'com.typesafe.play'
    module = 'play-java_2.12'
    versions = "[2.7.0,)"
    assertInverse = true
    javaVersion = 11
  }
  pass {
    name = 'play27'
    group = 'com.typesafe.play'
    module = 'play-java_2.13'
    versions = "[2.7.0,)"
    assertInverse = true
    javaVersion = 11
  }
}

apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'scala'

def configureRoutesCompilation(String sourceSetName) {
  def capitalizedName = sourceSetName.capitalize()
  def buildRoutesTask = tasks.register("build${capitalizedName}Routes", JavaExec) {
    def routesFile = "${project.projectDir}/src/test/routes/conf/routes"
    def outputDir = layout.buildDirectory.dir("generated/sources/${sourceSetName}Routes/scala")

    inputs.file routesFile
    outputs.dir outputDir

    mainClass = 'generator.CompileRoutes'
    args routesFile, outputDir.get().asFile.absolutePath

    classpath configurations.named('routeGeneratorRuntimeClasspath')
    classpath tasks.named('compileRouteGeneratorScala')

    javaLauncher = getJavaLauncherFor(11)
  }

  sourceSets.named(sourceSetName) {
    scala {
      srcDir buildRoutesTask
    }
  }

  tasks.named("compile${capitalizedName}Java", JavaCompile) {
    configureCompiler(it, 11)
  }

  tasks.named("compile${capitalizedName}Scala", ScalaCompile) {
    configureCompiler(it, 11)
    dependsOn buildRoutesTask
  }

  tasks.named("compile${capitalizedName}Groovy", GroovyCompile) {
    configureCompiler(it, 11)
    classpath += files(tasks.named("compile${capitalizedName}Scala"))
  }
}

sourceSets {
  routeGenerator {
    scala {
      srcDir 'src/routeGenerator/scala'
    }
  }
}

testJvmConstraints {
  minJavaVersion = JavaVersion.VERSION_11
}

repositories {
  maven {
    // for muzzle because of play-2.3
    // only place that has 'com.typesafe.netty:netty-http-pipelining:1.1.2' publicly accessible
    name = 'typesafe'
    url = 'https://repo.typesafe.com/typesafe/maven-releases/'
  }
}

addTestSuiteForDir('latestDepTest', 'test')

dependencies {
  compileOnly group: 'com.typesafe.play', name: "play-java_2.11", version: '2.7.0'
  implementation project(':dd-java-agent:instrumentation:play:play-2.6')
  implementation(project(':dd-java-agent:instrumentation:play:play-appsec-common'))

  routeGeneratorImplementation libs.scala213
  routeGeneratorImplementation group: 'com.typesafe.play', name: "routes-compiler_2.13", version: '2.+'

  testImplementation libs.scala213
  testImplementation project(':dd-java-agent:instrumentation:play:play-2.6')
  // Inherited tests needs play-appsec-2.6 instrumentation
  testImplementation project(':dd-java-agent:instrumentation:play:play-appsec-2.6')
  testImplementation testFixtures(project(':dd-java-agent:instrumentation:play:play-2.6'))

  testImplementation group: 'com.typesafe.play', name: "play-java_2.13", version: '2.7.+'
  testImplementation(group: 'com.typesafe.play', name: "play-test_2.13", version: '2.7.+') {
    exclude group: 'org.eclipse.jetty.websocket', module: 'websocket-client'
  }
  testImplementation group: 'com.typesafe.play', name: 'play-akka-http-server_2.13', version: '2.+'

  testRuntimeOnly project(':dd-java-agent:instrumentation:netty:netty-4.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:netty:netty-4.1')
  testRuntimeOnly project(':dd-java-agent:instrumentation:akka:akka-http:akka-http-10.0')
  testRuntimeOnly project(':dd-java-agent:instrumentation:akka:akka-actor-2.5')
  testRuntimeOnly project(':dd-java-agent:instrumentation:scala:scala-concurrent-2.8')
  testRuntimeOnly project(':dd-java-agent:instrumentation:scala:scala-promise:scala-promise-2.10')
  testRuntimeOnly project(':dd-java-agent:instrumentation:scala:scala-promise:scala-promise-2.13')

  latestDepTestImplementation libs.scala213
  latestDepTestImplementation group: 'com.typesafe.play', name: "play-java_2.13", version: '2.+'
  latestDepTestImplementation(group: 'com.typesafe.play', name: "play-test_2.13", version: '2.+') {
    exclude group: 'org.eclipse.jetty.websocket', module: 'websocket-client'
  }
  latestDepTestImplementation group: 'com.typesafe.play', name: 'play-akka-http-server_2.13', version: '2.+'
}

configurations.matching({ it.name.startsWith('test') }).configureEach({
  it.resolutionStrategy {
    // logback-classic 1.4.11 doesn't like being loaded in the bootstrap classloader (NPE)
    force group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.5'
  }
})

configureRoutesCompilation('test')
configureRoutesCompilation('latestDepTest')
