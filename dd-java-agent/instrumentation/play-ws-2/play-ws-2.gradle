// Set properties before any plugins get loaded
ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

apply from: "${rootDir}/gradle/java.gradle"

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  latestDepTest {
    dirName = 'test'
  }
}

muzzle {
  // 2.0.5 was a bad release

  fail {
    group = 'com.typesafe.play'
    module = 'play-ahc-ws-standalone_2.11'
    versions = '[,2.0.0)'
  }
  pass {
    group = 'com.typesafe.play'
    module = 'play-ahc-ws-standalone_2.11'
    versions = '[2.0.0,2.0.4]'
  }
  pass {
    group = 'com.typesafe.play'
    module = 'play-ahc-ws-standalone_2.11'
    versions = '[2.0.6,]'
  }

  fail {
    group = 'com.typesafe.play'
    module = 'play-ahc-ws-standalone_2.12'
    versions = '[,2.0.0)'
  }
  pass {
    group = 'com.typesafe.play'
    module = 'play-ahc-ws-standalone_2.12'
    versions = '[2.0.0,2.0.4]'
  }
  pass {
    group = 'com.typesafe.play'
    module = 'play-ahc-ws-standalone_2.12'
    versions = '[2.0.6,]'
  }

  // No Scala 2.13 versions below 2.0.6 exist
  pass {
    group = 'com.typesafe.play'
    module = 'play-ahc-ws-standalone_2.13'
    versions = '[2.0.6,]'
  }
}

def scalaVersion = '2.12'

dependencies {
  compileOnly group: 'com.typesafe.play', name: "play-ahc-ws-standalone_$scalaVersion", version: '2.0.0'

  testCompile project(':dd-java-agent:instrumentation:java-concurrent')

  // These are to ensure cross compatibility
  testCompile project(':dd-java-agent:instrumentation:netty-4.0')
  testCompile project(':dd-java-agent:instrumentation:netty-4.1')
  testCompile project(':dd-java-agent:instrumentation:akka-http-10.0')

  testCompile group: 'com.typesafe.play', name: "play-ahc-ws-standalone_$scalaVersion", version: '2.0.0'

  // TODO: Revisit when 2.1.x are out of the release candidate stage
  latestDepTestCompile group: 'com.typesafe.play', name: "play-ahc-ws-standalone_$scalaVersion", version: '2.0.+'
}
