plugins {
  id 'java-test-fixtures'
}

apply from: "$rootDir/gradle/java.gradle"

muzzle {
  pass {
    group = 'org.seleniumhq.selenium'
    module = 'selenium-java'
    versions = '[3.13.0,)'
    skipVersions += ["4.10.0", "4.11.0"] // depends on non-existent io.netty:netty-transport-native-epoll:4.1.92.Final
    skipVersions += "4.16.0" // depends on non-existent org.seleniumhq.selenium:selenium-devtools-v119:4.16.0
  }
}

addTestSuite('latestDepTest')

dependencies {
  compileOnly group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.141.59'

  testFixturesApi testFixtures(project(':products:ci-visibility:ci-visibility-agent'))
  testFixturesApi project(':dd-java-agent:instrumentation:junit-5.3')
  testFixturesApi group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.8.2'
  testFixturesApi group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.2'

  testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.141.59'
  testImplementation group: 'org.seleniumhq.selenium', name: 'htmlunit-driver', version: '2.70.0'

  latestDepTestImplementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '+'
  latestDepTestImplementation group: 'org.seleniumhq.selenium', name: 'htmlunit-driver', version: '+'
}

configurations.matching({ it.name.startsWith('test') || it.name.startsWith('latestDepTest') }).each({
  it.resolutionStrategy {
    // There is a conflict between the version used by HTMLUnit and the one used by :dd-java-agent:testing
    force group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.50.v20221201'
  }
})

tasks.matching { it.name.startsWith('latestDepTest') || it.name.contains('LatestDepTest') }.each {
  it.onlyIf {
    // Only run latest dep tests tasks (including compilation) if the current JVM is compatible with the latest Selenium version
    JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_11)
  }
}
