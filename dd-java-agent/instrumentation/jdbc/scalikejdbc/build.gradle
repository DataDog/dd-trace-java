
muzzle {
  pass {
    coreJdk()
  }
}

apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'scala'

addTestSuiteForDir('latestDepTest', 'test')

tasks.named("compileTestGroovy", GroovyCompile) {
  dependsOn "compileTestScala"
  classpath += files(tasks.named('compileTestScala').map { it.destinationDirectory })
}

tasks.named("compileLatestDepTestGroovy", GroovyCompile) {
  dependsOn "compileLatestDepTestScala"
  classpath += files(tasks.named('compileLatestDepTestScala').map { it.destinationDirectory })
}

tasks.withType(ScalaCompile).configureEach {
  // Despite the compatibility matrix, scala compiler doesn't properly
  // get the right compiler flags with JDK11 toolchain. Using 8 works.
  // * https://docs.scala-lang.org/overviews/jdk-compatibility/overview.html
  // * https://github.com/gradle/gradle/issues/19456
  configureCompiler(it, 8)
}

dependencies {
  testImplementation project(':dd-java-agent:instrumentation:jdbc')
  testImplementation group: 'org.scalikejdbc', name: 'scalikejdbc_2.13', version: '3.5.0'
  testImplementation libs.scala

  latestDepTestImplementation group: 'org.scalikejdbc', name: 'scalikejdbc_2.13', version: '3.+'
  latestDepTestImplementation group: 'org.scala-lang', name: 'scala-library', version: '2.+' // scala-lang 3.x requires scala 3.x compiler

  // jdbc unit testing
  testImplementation group: 'com.h2database', name: 'h2', version: '1.3.169'
  testImplementation group: 'org.apache.derby', name: 'derby', version: '10.6.1.0'
  testImplementation group: 'org.hsqldb', name: 'hsqldb', version: '2.0.0'
  testImplementation group: 'org.apache.tomcat', name: 'tomcat-jdbc', version: '7.0.19'
  testImplementation group: 'org.apache.tomcat', name: 'tomcat-juli', version: '7.0.19'
  testImplementation group: 'com.zaxxer', name: 'HikariCP', version: '2.4.0'
  testImplementation group: 'com.mchange', name: 'c3p0', version: '0.9.5'

  latestDepTestImplementation group: 'com.h2database', name: 'h2', version: '2.2.+' // 2.3+ requires Java 11
  latestDepTestImplementation group: 'org.apache.derby', name: 'derby', version: '10.14.+'
  latestDepTestImplementation group: 'org.hsqldb', name: 'hsqldb', version: '2.5+'
  // Tomcat 10.1.+ seems to require Java 11. Limit to fix build.
  latestDepTestImplementation group: 'org.apache.tomcat', name: 'tomcat-jdbc', version: '10.0.+'
  latestDepTestImplementation group: 'org.apache.tomcat', name: 'tomcat-juli', version: '10.0.+'
  latestDepTestImplementation group: 'com.zaxxer', name: 'HikariCP', version: '4.+' // 5+ requires Java 11
  latestDepTestImplementation group: 'com.mchange', name: 'c3p0', version: '+'
}
