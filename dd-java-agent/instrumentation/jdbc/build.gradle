plugins {
  id 'java-test-fixtures'
  id 'me.champeau.jmh'
}

muzzle {
  pass {
    coreJdk()
    extraDependency 'com.ibm.db2:jcc:11.1.4.4'
    extraDependency 'com.zaxxer:HikariCP:2.4.0'
  }
}

apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'call-site-instrumentation'

addTestSuiteForDir('oldH2Test', 'test')
addTestSuiteForDir('oldPostgresTest', 'test')
addTestSuiteForDir('latestDepTest', 'test')
addTestSuiteExtendingForDir('latestDepJava11Test', 'latestDepTest', 'test')

dependencies {
  compileOnly group: 'com.zaxxer', name: 'HikariCP', version: '2.4.0'
  testImplementation(testFixtures(project(':dd-java-agent:agent-iast')))

  // jdbc unit testing
  testImplementation group: 'com.h2database', name: 'h2', version: '[1.3.168,1.3.169]'// first jdk 1.6 compatible
  testImplementation group: 'org.apache.derby', name: 'derby', version: '10.6.1.0'
  testImplementation group: 'org.hsqldb', name: 'hsqldb', version: '2.0.0'
  testImplementation group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.10.0'

  testImplementation group: 'org.apache.tomcat', name: 'tomcat-jdbc', version: '7.0.19'
  // tomcat needs this to run
  testImplementation group: 'org.apache.tomcat', name: 'tomcat-juli', version: '7.0.19'
  testImplementation group: 'com.zaxxer', name: 'HikariCP', version: '2.4.0'
  testImplementation group: 'com.mchange', name: 'c3p0', version: '0.9.5'

  testImplementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.23'
  testImplementation group: 'org.postgresql', name: 'postgresql', version: '[9.4,42.2.18]'
  testImplementation group: 'com.microsoft.sqlserver', name: 'mssql-jdbc', version: '10.2.0.jre8'
  testImplementation group: 'com.oracle.database.jdbc', name: 'ojdbc8', version: '19.19.0.0'

  testImplementation group: 'org.testcontainers', name:'mysql', version: libs.versions.testcontainers.get()
  testImplementation group: 'org.testcontainers', name:'postgresql', version: libs.versions.testcontainers.get()
  testImplementation group: 'org.testcontainers', name:'mssqlserver', version: libs.versions.testcontainers.get()
  testImplementation group: 'org.testcontainers', name:'oracle-xe', version: '1.20.4'

  testRuntimeOnly project(':dd-java-agent:instrumentation:iast-instrumenter')

  // Test pre jdk 1.6 H2
  oldH2TestImplementation(group: 'com.h2database', name: 'h2') {
    version {
      strictly '1.3.168'
    }
  }

  oldPostgresTestImplementation(group: 'org.postgresql', name: 'postgresql') {
    version {
      strictly '9.4-1201-jdbc41'
    }
  }

  latestDepTestImplementation group: 'com.h2database', name: 'h2', version: '2.2.+' // 2.3+ requires Java 11
  latestDepTestImplementation group: 'org.apache.derby', name: 'derby', version: '10.14.+'
  latestDepTestImplementation group: 'org.hsqldb', name: 'hsqldb', version: '2.5+'

  latestDepTestImplementation group: 'org.apache.tomcat', name: 'tomcat-jdbc', version: '10.0.+'
  latestDepTestImplementation group: 'org.apache.tomcat', name: 'tomcat-juli', version: '10.0.+'
  latestDepTestImplementation group: 'com.zaxxer', name: 'HikariCP', version: '4.+' // 5+ requires Java 11
  // Broke from 0.10.+: https://datadoghq.atlassian.net/browse/AIDM-165
  latestDepTestImplementation group: 'com.mchange', name: 'c3p0', version: '0.9.+'

  latestDepJava11TestImplementation group: 'com.h2database', name: 'h2', version: '+'
  latestDepJava11TestImplementation group: 'com.zaxxer', name: 'HikariCP', version: '+'
}

tasks.named("check").configure {
  dependsOn "oldH2Test"
  dependsOn "oldPostgresTest"
}

tasks.named("latestDepJava11Test").configure {
  javaLauncher = getJavaLauncherFor(11)
}

tasks.withType(Test).configureEach {
  usesService(testcontainersLimit)
}

jmh {
  jmhVersion = libs.versions.jmh.get()
  duplicateClassesStrategy = DuplicatesStrategy.EXCLUDE
}
