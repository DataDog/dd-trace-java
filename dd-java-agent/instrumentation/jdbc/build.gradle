muzzle {
  pass {
    coreJdk()
    extraDependency 'com.ibm.db2:jcc:11.1.4.4'
  }
}

apply from: "$rootDir/gradle/java.gradle"

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  oldH2Test {
    dirName = 'test'
  }

  latestDepTest {
    dirName = 'test'
  }

  oldPostgresTest {
    dirName = 'test'
  }

  latestDepJava11Test {
    extendsFrom latestDepTest
    dirName = 'test'
  }
}

dependencies {
  // jdbc unit testing
  testImplementation group: 'com.h2database', name: 'h2', version: '[1.3.168,1.3.169]'// first jdk 1.6 compatible
  testImplementation group: 'org.apache.derby', name: 'derby', version: '10.6.1.0'
  testImplementation group: 'org.hsqldb', name: 'hsqldb', version: '2.0.0'

  testImplementation group: 'org.apache.tomcat', name: 'tomcat-jdbc', version: '7.0.19'
  // tomcat needs this to run
  testImplementation group: 'org.apache.tomcat', name: 'tomcat-juli', version: '7.0.19'
  testImplementation group: 'com.zaxxer', name: 'HikariCP', version: '2.4.0'
  testImplementation group: 'com.mchange', name: 'c3p0', version: '0.9.5'

  testImplementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.23'
  testImplementation group: 'org.postgresql', name: 'postgresql', version: '[9.4,42.2.18]'

  testImplementation "org.testcontainers:mysql:${versions.testcontainers}"
  testImplementation "org.testcontainers:postgresql:${versions.testcontainers}"

  // Test pre jdk 1.6 H2
  oldH2TestImplementation(group: 'com.h2database', name: 'h2') {
    version {
      strictly '1.3.168'
    }
  }

  oldPostgresTestImplementation(group: 'org.postgresql', name: 'postgresql') {
    version {
      strictly '9.4-1201-jdbc41'
    }
  }

  latestDepTestImplementation group: 'com.h2database', name: 'h2', version: '+'
  latestDepTestImplementation group: 'org.apache.derby', name: 'derby', version: '10.14.+'
  latestDepTestImplementation group: 'org.hsqldb', name: 'hsqldb', version: '2.5+'

  // Tomcat 10.1.+ seems to require Java 11. Limit to fix build.
  latestDepTestImplementation group: 'org.apache.tomcat', name: 'tomcat-jdbc', version: '10.0.+'
  latestDepTestImplementation group: 'org.apache.tomcat', name: 'tomcat-juli', version: '10.0.+'
  latestDepTestImplementation group: 'com.zaxxer', name: 'HikariCP', version: '4.+' // 5+ requires Java 11
  latestDepTestImplementation group: 'com.mchange', name: 'c3p0', version: '+'

  latestDepJava11TestImplementation group: 'com.zaxxer', name: 'HikariCP', version: '+'
}

tasks.named("test").configure {
  dependsOn "oldH2Test"
  dependsOn "oldPostgresTest"
}

tasks.named("latestDepJava11Test").configure {
  javaLauncher = getJavaLauncherFor(11)
}

tasks.withType(Test).configureEach {
  usesService(testcontainersLimit)
}
