import java.util.concurrent.Executors
import java.util.concurrent.TimeUnit

ext {
  minJavaVersionForTests = JavaVersion.VERSION_21
  // Structured concurrency is a preview feature in Java 21. Methods (e.g. ShutdownOnFailure) used in this instrumentation test are no longer available in Java 25, so we set the max version to 24.
  // See: https://download.java.net/java/early_access/loom/docs/api/java.base/java/util/concurrent/StructuredTaskScope.html
  maxJavaVersionForTests = JavaVersion.VERSION_24
}

apply from: "$rootDir/gradle/java.gradle"
apply plugin: 'idea'

muzzle {
  pass {
    coreJdk('21')
  }
}

idea {
  module {
    jdkName = '21'
  }
}

/*
 * Declare previewTest, a test suite that requires the Javac/Java --enable-preview feature flag
 */
addTestSuite('previewTest')
// Configure groovy test file compilation
compilePreviewTestGroovy.configure {
  javaLauncher = javaToolchains.launcherFor {
    languageVersion = JavaLanguageVersion.of(21)
  }
  options.compilerArgs.add("--enable-preview")
}
// Configure Java test files compilation
compilePreviewTestJava.configure {
  options.compilerArgs.add("--enable-preview")
}
// Configure tests execution
previewTest.configure {
  jvmArgs = ['--enable-preview']
}
// Require the preview test suite to run as part of module check
tasks.named("check").configure {
  dependsOn "previewTest"
}

dependencies {
  testImplementation project(':dd-java-agent:instrumentation:trace-annotation')
}

// Set all compile tasks to use JDK21 but let instrumentation code targets 1.8 compatibility
project.tasks.withType(AbstractCompile).configureEach {
  setJavaVersion(it, 21)
}
compileJava.configure {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(Test).configureEach {
  doFirst {
    String fullPath = layout.buildDirectory.asFile.get().absolutePath.replace("dd-trace-java/dd-java-agent",
      "dd-trace-java/workspace/dd-java-agent")

    println "DEBUG >> ${fullPath}"

    def outDir = new File(fullPath)
    outDir.mkdirs()
    def outFile = new File(outDir, "${System.currentTimeMillis()}-thread-dump.log")

    // single-thread scheduler (daemon)
    def scheduler = Executors.newSingleThreadScheduledExecutor({ r ->
      Thread t = new Thread(r, "dump-scheduler")
      t.setDaemon(true)
      return t
    })

    // schedule the dump job 10s later
    def future = scheduler.schedule({
      try {
        new ProcessBuilder("jcmd", "0", "Thread.print", "-l")
          .redirectErrorStream(true)
          .redirectOutput(outFile)
          .start().waitFor()

        logger.lifecycle("Thread dump written to: ${outFile.absolutePath}")
      } catch (Throwable t) {
        logger.warn("Thread dump helper failed: ${t.message}", t)
      } finally {
        scheduler.shutdown()
      }
    }, 60, TimeUnit.SECONDS)

    // store handles for cancellation in doLast
    ext.dumpFuture = future
    ext.dumpScheduler = scheduler
  }

  doLast {
    // cancel if task completed in <10s
    try {
      ext.dumpFuture?.cancel(false)
    } finally {
      ext.dumpScheduler?.shutdownNow()
    }
  }
}
