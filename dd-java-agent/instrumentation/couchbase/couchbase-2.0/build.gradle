
apply from: "$rootDir/gradle/java.gradle"

addTestSuiteForDir('latestDepTest', 'test')

muzzle {
  // Version 2.7.5 and 2.7.8 were not released properly and muzzle cannot test against it causing failure.
  // So we have to skip them resulting in this verbose setup.
  pass {
    group = 'com.couchbase.client'
    module = 'java-client'
    versions = "[2.0.0,2.7.5)"
  }
  pass {
    group = 'com.couchbase.client'
    module = 'java-client'
    versions = "[2.7.6,2.7.8)"
  }
  pass {
    group = 'com.couchbase.client'
    module = 'java-client'
    versions = "[2.7.9,3.0.0)"
  }
  fail {
    group = 'com.couchbase.client'
    module = 'couchbase-client'
    versions = "[,]"
  }
}

configurations {
  testArtifacts
}

// Create test artifacts so couchbase-2.6 can reuse tests
artifacts {
  testArtifacts testJar
}

dependencies {
  implementation project(':dd-java-agent:instrumentation:rxjava-1')

  compileOnly group: 'com.couchbase.client', name: 'java-client', version: '2.0.0'

  testImplementation group: 'com.couchbase.mock', name: 'CouchbaseMock', version: '1.5.19'

  testImplementation group: 'org.springframework.data', name: 'spring-data-couchbase', version: '2.0.0.RELEASE'
  // Earliest version that seems to allow queries with CouchbaseMock:
  testImplementation group: 'com.couchbase.client', name: 'java-client', version: '2.5.0'

  latestDepTestImplementation group: 'org.springframework.data', name: 'spring-data-couchbase', version: '3.+'
  latestDepTestImplementation group: 'com.couchbase.client', name: 'java-client', version: '2.+'
  latestDepTestImplementation group: 'com.couchbase.client', name: 'encryption', version: '+'
}
