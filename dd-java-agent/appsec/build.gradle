import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import groovy.json.JsonOutput
import groovy.json.JsonSlurper

plugins {
  id 'com.gradleup.shadow'
  id 'me.champeau.jmh'
}

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/version.gradle"

dependencies {
  api libs.slf4j
  implementation project(':internal-api')
  implementation project(':communication')
  implementation project(':telemetry')
  implementation group: 'io.sqreen', name: 'libsqreen', version: '17.2.0'
  implementation libs.moshi
  implementation libs.bundles.asm

  testImplementation libs.bytebuddy
  testImplementation project(':remote-config:remote-config-core')
  testImplementation project(':utils:test-utils')
  testImplementation group: 'com.flipkart.zjsonpatch', name: 'zjsonpatch', version: '0.4.11'
  testImplementation libs.logback.classic
  testImplementation libs.jackson.databind
}

tasks.named("shadowJar", ShadowJar) {
  exclude '**/*-dbgsym.zip'
  dependencies deps.excludeShared
}

tasks.named("jar", Jar) {
  archiveClassifier = 'unbundled'
}

tasks.named("processResources", ProcessResources) {
  doLast {
    fileTree(dir: outputs.files.asPath, includes: ['**/*.json']).each {
      it.text = JsonOutput.toJson(new JsonSlurper().parse(it))
    }
  }
}

jmh {
  jmhVersion = libs.versions.jmh.get()
  duplicateClassesStrategy = DuplicatesStrategy.EXCLUDE
  jvmArgs = ['-Ddd.appsec.enabled=true -Xms64m -Xmx64m']
  failOnError = false
  forceGC = true
  if (project.hasProperty('jmhIncludes')) {
    includes = [project.jmhIncludes]
  }
}

tasks.named("jmhJar", Jar) {
  exclude 'org/slf4j/impl/**/*'
}

ext {
  minimumBranchCoverage = 0.6
  minimumInstructionCoverage = 0.8
  excludedClassesCoverage = [
    'com.datadog.appsec.config.AppSecConfigServiceImpl.AppSecConfigChangesListener',
    'com.datadog.appsec.ddwaf.WafInitialization',
    'com.datadog.appsec.ddwaf.WAFModule.WAFDataCallback',
    'com.datadog.appsec.config.AppSecModuleConfigurer.Reconfiguration',
    'com.datadog.appsec.report.*',
    'com.datadog.appsec.config.AppSecConfigServiceImpl.SubscribeFleetServiceRunnable.1',
    'com.datadog.appsec.util.StandardizedLogging',
    'com.datadog.appsec.util.AbortStartupException',
    'com.datadog.appsec.config.AppSecConfig.AppSecConfigV1',
    'com.datadog.appsec.config.AppSecConfig.AppSecConfigV2',
    'com.datadog.appsec.config.AppSecConfig.NumberJsonAdapter',
    'com.datadog.appsec.config.AppSecFeatures',
    'com.datadog.appsec.config.AppSecFeatures.Asm',
    'com.datadog.appsec.config.AppSecFeatures.ApiSecurity',
    'com.datadog.appsec.config.AppSecFeatures.AutoUserInstrum',
    'com.datadog.appsec.AppSecModule.AppSecModuleActivationException',
    'com.datadog.appsec.event.ReplaceableEventProducerService',
    'com.datadog.appsec.api.security.ApiSecuritySampler.NoOp',
  ]
  excludedClassesBranchCoverage = [
    'com.datadog.appsec.gateway.GatewayBridge',
    'com.datadog.appsec.event.data.Address',
    'com.datadog.appsec.config.CurrentAppSecConfig',
    // equals() / hashCode() are not well covered
    'com.datadog.appsec.config.AppSecConfig.Helper',
    // assert never fails
    'com.datadog.appsec.util.StandardizedLogging',
    'com.datadog.appsec.util.AbortStartupException',
  ]
  excludedClassesInstructionCoverage = ['com.datadog.appsec.report.ReportServiceImpl.TaskSchedulerImpl',]
}

tasks.withType(Test).configureEach {
  jvmArgs += ['-Ddd.appsec.enabled=true']
}

def rootDir = project.rootDir

spotless {
  java {
    target 'src/**/*.java'
  }
}
