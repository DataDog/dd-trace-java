plugins {
  id "com.github.johnrengelman.shadow"
}
apply from: "$rootDir/gradle/java.gradle"

dependencies {
  api('com.datadoghq:jmxfetch:0.45.3') {
    exclude group: 'org.slf4j', module: 'slf4j-api'
    exclude group: 'org.slf4j', module: 'slf4j-jdk14'
  }
  api deps.slf4j
  api project(':internal-api')
}

shadowJar {
  dependencies deps.sharedInverse
  dependencies {
    exclude(project(':dd-java-agent:agent-bootstrap'))
    exclude(project(':dd-java-agent:agent-logging'))
    exclude(project(':dd-trace-api'))
    exclude(project(':internal-api'))
    exclude(project(':internal-api:internal-api-8'))
    exclude(project(':utils:socket-utils'))
    exclude(dependency('org.slf4j::'))
  }
}

tasks.register("submodulesUpdate", Exec) {
  group 'Build Setup'
  description 'Initializes and updates integrations-core git submodule'
  commandLine 'git', 'submodule', 'update', '--init', 'integrations-core'
  def submoduleHead = file("${project.rootDir}/.git/modules/dd-java-agent/agent-jmxfetch/integrations-core/HEAD")
  if (submoduleHead.exists()) {
    inputs.file "${project.rootDir}/.git/modules/dd-java-agent/agent-jmxfetch/integrations-core/HEAD"
  }
  def integrationsCore = file("$projectDir/integrations-core")
  outputs.dir integrationsCore
  if (integrationsCore.list().length == 0) {
    outputs.upToDateWhen { false }
  }
}

tasks.register("copyMetricConfigs", Exec) {
  group 'Build Setup'
  description 'Copy metrics.yaml files from integrations-core into resources'
  commandLine './copy-metric-configs.sh', 'integrations-core', sourceSets.main.output.resourcesDir
  inputs.dir file("$projectDir/integrations-core")
  outputs.dir sourceSets.main.output.resourcesDir
  doFirst {
    // Ensure the resources directory is available.
    file(sourceSets.main.output.resourcesDir).mkdirs()
  }
  dependsOn "submodulesUpdate"
  // In CI, there seems to be a race condition where processResources overwrites the copied metric config files.
  // Ensure that task runs last to avoid this problem.
  mustRunAfter "processResources"
}

tasks.named("processResources").configure {
  finalizedBy copyMetricConfigs
}
