plugins {
  id 'org.gradle.playframework'
}

apply from: "$rootDir/dd-smoke-tests/play-common/fix-play-routes.gradle"

def playVer = "2.6.20"
def scalaVer = System.getProperty("scala.binary.version", /* default = */ "2.12")

play {
  platform {
    playVersion = playVer
    scalaVersion = scalaVer
    javaVersion = JavaVersion.VERSION_1_8
  }
  injectedRoutesGenerator = true
}

tasks.withType(ScalaCompile).configureEach {
  // Despite the compatibility matrix, scala compiler doesn't properly
  // get the right compiler flags with JDK11 toolchain. Using 8 works.
  // * https://docs.scala-lang.org/overviews/jdk-compatibility/overview.html
  // * https://github.com/gradle/gradle/issues/19456
  configureCompiler(it, 8)
}

dependencies {
  implementation "com.typesafe.play:play-guice_$scalaVer:$playVer"
  implementation "com.typesafe.play:play-logback_$scalaVer:$playVer"
  implementation "com.typesafe.play:filters-helpers_$scalaVer:$playVer"

  implementation project(':dd-trace-api')
  implementation project(':dd-java-agent:benchmark-integration')
  implementation group: 'io.opentracing', name: 'opentracing-api', version: '0.32.0'
  implementation group: 'io.opentracing', name: 'opentracing-util', version: '0.32.0'
}

repositories {
  mavenLocal()
  if (project.rootProject.hasProperty("mavenRepositoryProxy")) {
    maven {
      url project.rootProject.property("mavenRepositoryProxy")
      allowInsecureProtocol = true
    }
  }
  mavenCentral()
  maven {
    name = "lightbend-maven-releases"
    url = "https://repo.lightbend.com/lightbend/maven-release"
  }
  ivy {
    name = "lightbend-ivy-release"
    url = "https://repo.lightbend.com/lightbend/ivy-releases"
    layout("ivy")
  }
}
