plugins {
  id "com.github.johnrengelman.shadow"
}

// Set properties before any plugins get loaded
ext {
  enableJunitPlatform = true
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

apply from: "$rootDir/gradle/java.gradle"

// FIXME: Improve test coverage.
minimumBranchCoverage = 0.0
minimumInstructionCoverage = 0.0

dependencies {
  api deps.slf4j
  api project(':dd-trace-api')
  api project(':internal-api')
  api project(':utils:container-utils')
  api project(':utils:process-utils')
  api project(':utils:socket-utils')
  api project(':utils:version-utils')

  implementation deps.okhttp

  testImplementation group: 'com.squareup.okhttp3', name: 'mockwebserver', version: versions.okhttp
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

configurations {
  // exclude bootstrap dependencies from shadowJar
  runtime.exclude module: deps.slf4j
  runtime.exclude group: 'org.slf4j'
}

shadowJar {
  dependencies deps.sharedInverse
  dependencies {
    exclude(project(':dd-java-agent:agent-bootstrap'))
    exclude(project(':dd-java-agent:agent-logging'))
    exclude(project(':dd-trace-api'))
    exclude(project(':internal-api'))
    exclude(project(':internal-api:internal-api-8'))
    exclude(project(':utils:container-utils'))
    exclude(project(':utils:process-utils'))
    exclude(project(':utils:socket-utils'))
    exclude(project(':utils:time-utils'))
    exclude(project(':utils:version-utils'))
    exclude(dependency('org.slf4j::'))
  }
  exclude {
    if (it.path.startsWith('org/jctools/')) {
      def ret = true
      if (it.path.equals('org/jctools/util') || it.path.equals('org/jctools/maps')) {
        ret = false
      } else {
        ret = !(it.path.contains('/util') || it.path.contains('AbstractEntry') || it.path.contains('ConcurrentAutoTable') || it.path.contains('NonBlockingHashMap'))
      }
      return ret
    }
    return false
  }
}

jar {
  classifier = 'unbundled'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
