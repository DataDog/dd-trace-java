apply from: "$rootDir/gradle/java.gradle"
apply plugin: "idea"

minimumBranchCoverage = 0.6
excludedClassesCoverage += ['datadog.trace.agent.tooling.*']

configurations {
  // classpath used by the instrumentation muzzle plugin
  instrumentationMuzzle.extendsFrom implementation
}

dependencies {
  api(project(':dd-java-agent:agent-bootstrap')) {
    exclude group: 'com.datadoghq', module: 'agent-logging'
  }
  api group: 'com.blogspot.mydailyjava', name: 'weak-lock-free', version: '0.17'
  api group: 'com.googlecode.concurrentlinkedhashmap', name: 'concurrentlinkedhashmap-lru', version: '1.4.2'
  api deps.bytebuddy
  api deps.bytebuddyagent

  api project(':dd-trace-core')
  api project(':dd-trace-core:jfr-openjdk')

  testImplementation project(':dd-java-agent:testing')
  testImplementation group: 'com.google.guava', name: 'guava-testlib', version: '20.0'
}

// Use Java 11 to build a delegating ClassFileTransformer that understands Java modules
sourceSets {
  "main_java8" {
    java.srcDirs "${project.projectDir}/src/main/java8"
  }
  "main_java11" {
    java.srcDirs "${project.projectDir}/src/main/java11"
  }
}
compileMain_java8Java.doFirst {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}
compileMain_java11Java.doFirst {
  if (!System.env.JAVA_11_HOME) {
    throw new GradleException('JAVA_11_HOME must be set to build transformer helpers')
  }
  options.fork = true
  options.forkOptions.javaHome = file(System.env.JAVA_11_HOME)
  sourceCompatibility = JavaVersion.VERSION_1_9
  targetCompatibility = JavaVersion.VERSION_1_9
}
dependencies {
  main_java8CompileOnly deps.bytebuddy
  main_java8CompileOnly sourceSets.main.output
  main_java11CompileOnly sourceSets.main_java8.output
  main_java11CompileOnly deps.bytebuddy
  main_java11CompileOnly sourceSets.main.output
  runtime sourceSets.main_java11.output
}
jar {
  from sourceSets.main_java8.output
  from sourceSets.main_java11.output
}
forbiddenApisMain_java8 {
  failOnMissingClasses = false
}
forbiddenApisMain_java11 {
  failOnMissingClasses = false
}
idea {
  module {
    jdkName = '11'
  }
}
