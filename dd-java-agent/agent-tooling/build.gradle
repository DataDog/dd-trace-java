plugins {
  id 'me.champeau.jmh'
  id 'java-test-fixtures'
}
apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/tries.gradle"

minimumBranchCoverage = 0.6
excludedClassesCoverage += ['datadog.trace.agent.tooling.*']

sourceSets {
  test_java11 {
    java {
      srcDirs = [file('src/test/java11')]
    }
  }
  test {
    groovy {
      compileClasspath += sourceSets.test_java11.output
      runtimeClasspath += sourceSets.test_java11.output
    }
  }
}

configurations {
  instrumentPluginClasspath {
    canBeConsumed = true
    canBeResolved = false
    extendsFrom runtimeElements
  }

  test_java11Implementation {
    extendsFrom testImplementation
  }
}

tasks.named('compileJava').configure { dependsOn 'generateClassNameTries' }
tasks.named('sourcesJar').configure { dependsOn 'generateClassNameTries' }

dependencies {
  api(project(':dd-java-agent:agent-bootstrap')) {
    exclude group: 'com.datadoghq', module: 'agent-logging'
  }
  compileOnly project(':dd-java-agent:agent-profiling')
  api group: 'com.blogspot.mydailyjava', name: 'weak-lock-free', version: '0.17'
  api libs.bytebuddy
  api libs.bytebuddyagent
  implementation group: 'net.java.dev.jna', name: 'jna', version: '5.8.0'
  implementation group: 'net.java.dev.jna', name: 'jna-platform', version: '5.8.0'

  api project(':dd-trace-core')

  implementation project(':dd-java-agent:agent-crashtracking')

  compileOnly group: 'com.github.jnr', name: 'jnr-posix', version: libs.versions.jnr.posix.get()

  testImplementation project(':dd-java-agent:testing')
  testImplementation libs.bytebuddy
  testImplementation group: 'com.google.guava', name: 'guava-testlib', version: '20.0'

  jmhImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.3.5.RELEASE'
}

jmh {
  jmhVersion = '1.32'
  includeTests = true
}
tasks.named("compileJmhJava", JavaCompile).configure {
  dependsOn 'compileTestJava'
}
tasks.named("compileTestJava", JavaCompile).configure {
  dependsOn 'generateTestClassNameTries'
}

tasks.named("forbiddenApisJmh").configure {
  ignoreFailures = true
}

tasks.named("forbiddenApisTest_java11").configure {
  // it will fail due to missing JDK >= 9 classes
  // java.lang.ClassNotFoundException: java.lang.invoke.StringConcatFactory
  failOnMissingClasses = false
}

tasks.named("compileTestGroovy", GroovyCompile).configure {
  dependsOn 'compileTest_java11Java'
}

tasks.named("compileTest_java11Java", JavaCompile).configure {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
  setJavaVersion(it, 11)
}

tasks.named("jmh").configure {
  outputs.upToDateWhen { false }
  dependsOn 'compileTestJava'
}
