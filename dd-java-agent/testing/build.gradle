import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
  id 'com.gradleup.shadow'
}

apply from: "$rootDir/gradle/java.gradle"

tasks.withType(JavaCompile).configureEach {
  configureCompiler(it, 8, JavaVersion.VERSION_1_8, "Need access to sun.misc package")
}

minimumBranchCoverage = 0.5
minimumInstructionCoverage = 0.5
excludedClassesCoverage += [
  'datadog.trace.agent.test.asserts.*Assert',
  'datadog.trace.agent.test.asserts.*Assert.*',
  'datadog.trace.agent.test.base.*',
  'datadog.trace.agent.test.server.http.HttpProxy.AsyncPipe',
  'datadog.trace.agent.test.server.http.TestHttpServer.*',
  'datadog.trace.agent.test.utils.*',
  // Avoid applying jacoco instrumentation to classes instrumented by tested agent
  'context.FieldInjectionTestInstrumentation**',
  'context.ExcludeFilterTestInstrumentation**',
  // profiling
  'datadog.trace.agent.test.TestProfilingContextIntegration',
  'datadog.trace.agent.test.TestProfilingContextIntegration.TestQueueTiming'
]

configurations.named('api') {
  exclude group: 'org.snakeyaml', module: 'snakeyaml-engine' // we vendor this in the agent jar
}

dependencies {
  api libs.bytebuddy
  api libs.bytebuddyagent
  api libs.slf4j
  api libs.bundles.test.logging
  api libs.guava

  // Do not expose jetty as a transitive dependency, as it conflicts
  // with versions used to test instrumentions.
  // Note: 9.4 last to support java 8
  compileOnly group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.56.v20240826'
  runtimeOnly group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.56.v20240826'
  // Since jetty-server is shadowed, it is needed to exports the servlet api as well.
  api group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
  
  api group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: libs.versions.okhttp.legacy.get()

  api project(':dd-java-agent:agent-tooling')
  api project(':dd-java-agent:agent-builder')
  api project(':utils:test-utils')
  api project(':utils:time-utils')

  implementation "org.junit.platform:junit-platform-runner:${libs.versions.junit.platform.get()}"
  implementation project(':dd-java-agent:appsec')

  compileOnly(libs.bundles.groovy)
  compileOnly(libs.bundles.spock)
}

tasks.named("shadowJar", ShadowJar) {
  // Only bundle jetty dependencies into the jar (relocated)
  // All other dependencies remain as transitive dependencies
  dependencies {
    include(dependency {
      it.moduleGroup == 'org.eclipse.jetty'
    })
  }

  // Relocate jetty classes to avoid conflicts with instrumented versions
  relocate "org.eclipse.jetty", "datadog.eclipse.jetty"

  // Make shadowJar replace the main jar in api configuration
  archiveClassifier = ''
}

// Remove the regular jar from api elements and use shadow jar instead
// Also exclude jetty from being exposed as a transitive dependency (it's bundled in the shadow jar)
configurations {
  apiElements {
    outgoing.artifacts.clear()
    outgoing.artifact(tasks.shadowJar)
    exclude group: 'org.eclipse.jetty'
  }
  runtimeElements {
    outgoing.artifacts.clear()
    outgoing.artifact(tasks.shadowJar)
    exclude group: 'org.eclipse.jetty'
  }
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()
}
