apply from: "$rootDir/gradle/java.gradle"

minimumBranchCoverage = 0.5
minimumInstructionCoverage = 0.5
excludedClassesCoverage += [
  'datadog.trace.agent.test.asserts.*Assert',
  'datadog.trace.agent.test.asserts.*Assert.*',
  'datadog.trace.agent.test.base.*',
  'datadog.trace.agent.test.log.*',
  // Groovy generates unreachable lines see:
  // https://issues.apache.org/jira/browse/GROOVY-9610
  'datadog.trace.agent.test.AgentTestRunner',
  'datadog.trace.agent.test.AbortTransformationException',
  'datadog.trace.agent.test.utils.*',
  // Avoid applying jacoco instrumentation to classes instrumented by tested agent
  'context.ContextTestInstrumentation**',
]

dependencies {
  compile deps.bytebuddy
  compile deps.bytebuddyagent
  compile deps.slf4j
  compile deps.spock
  compile deps.testLogging
  compile deps.guava

  compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '8.0.0.v20110901'
  compile group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: versions.okhttp

  compile project(':dd-java-agent:agent-tooling')
  compile project(':utils:test-utils')

  compile deps.groovy

  testCompile project(':utils:test-utils')
  testCompile project(':dd-java-agent:instrumentation:trace-annotation')

  testCompile group: 'cglib', name: 'cglib', version: '3.2.5'
  // test instrumenting java 1.1 bytecode
  testCompile group: 'net.sf.jt400', name: 'jt400', version: '6.1'

  // We have autoservices defined in test subtree, looks like we need this to be able to properly rebuild this
  testAnnotationProcessor deps.autoserviceProcessor
  testCompileOnly deps.autoserviceAnnotation
}

// See comment for FieldBackedProviderFieldInjectionDisabledTest about why this hack is here
tasks.withType(Test).configureEach {
  if (name != "testDisabledFieldInjection") {
    exclude "context/FieldBackedProviderFieldInjectionDisabledTest.class"
  }
}

tasks.register("testDisabledFieldInjection", Test) {
  systemProperties "dd.trace.runtime.context.field.injection": "false"
  include "context/FieldBackedProviderFieldInjectionDisabledTest.class"
}
test.dependsOn(testDisabledFieldInjection)
