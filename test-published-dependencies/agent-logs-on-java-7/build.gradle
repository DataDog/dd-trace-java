plugins {
  id 'java'
  id 'application'
}

java {
  disableAutoTargetJvm()
  sourceCompatibility = JavaVersion.VERSION_1_7
  targetCompatibility = JavaVersion.VERSION_1_7
}

configurations {
  agent
}

dependencies {
  agent("com.datadoghq:dd-java-agent:$version")
  testImplementation(platform("org.junit:junit-bom:5.9.2"))
  testImplementation('org.junit.jupiter:junit-jupiter')
}

tasks.named("test", Test) {
  dependsOn('jar')
  useJUnitPlatform()
  testLogging {
    events 'passed', 'skipped', 'failed'
  }
  jvmArgs "-Dtest.published.dependencies.agent=${configurations.agent.singleFile}"
  jvmArgs "-Dtest.published.dependencies.jar=${tasks.jar.archiveFile.get()}"
}

tasks.named("jar", Jar) {
  manifest {
    attributes('Main-Class': 'test.published.dependencies.App')
  }
}

application {
  mainClass = 'test.published.dependencies.App'
}

tasks.named("compileTestJava", JavaCompile) {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}
