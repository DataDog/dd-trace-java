// this project will run in isolation under the agent's classloader
plugins {
  id "com.github.johnrengelman.shadow"
}

apply from: "${rootDir}/gradle/java.gradle"

// add all subprojects under 'instrumentation' to the agent's dependencies
Project instr_project = project
subprojects { subProj ->
  if (subProj.getParent() == instr_project) {
    instr_project.dependencies {
      compile(project(subProj.getPath()))
    }
  }
}

dependencies {
  compile(project(':sts-java-agent:agent-tooling')) {
    exclude module: ':sts-java-agent:agent-bootstrap'
  }
}

configurations {
  // exclude bootstrap dependencies from shadowJar
  runtime.exclude module: deps.opentracing
  runtime.exclude module: deps.slf4j
  runtime.exclude group: 'org.slf4j'
  runtime.exclude group: 'io.opentracing'
}

shadowJar {
  dependencies {
    exclude(project(':sts-java-agent:agent-bootstrap'))
    exclude(project(':sts-trace-api'))
  }
}

jar {
  classifier = 'unbundled'
}
