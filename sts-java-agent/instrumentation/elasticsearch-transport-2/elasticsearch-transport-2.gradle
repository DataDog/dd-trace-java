apply plugin: 'version-scan'

versionScan {
  group = "org.elasticsearch"
  module = "elasticsearch"
  versions = "[2.0,3)"
  verifyPresent = [
    "org.elasticsearch.plugins.SitePlugin": null,
  ]
}

apply from: "${rootDir}/gradle/java.gradle"

testJava8Only += '**/*Test.class'

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  latestDepTest {
    dirName = 'test'
  }
}

dependencies {
  compileOnly group: 'org.elasticsearch', name: 'elasticsearch', version: '2.0.0'

  compile project(':dd-java-agent:agent-tooling')

  compile deps.bytebuddy
  compile deps.opentracing
  annotationProcessor deps.autoservice
  implementation deps.autoservice

  testCompile project(':dd-java-agent:testing')
  // Ensure no cross interference
  testCompile project(':dd-java-agent:instrumentation:elasticsearch-rest-5')
  testCompile project(':dd-java-agent:instrumentation:elasticsearch-transport-5')
  // TODO: add netty instrumentation when that is complete.

  testCompile group: 'org.elasticsearch', name: 'elasticsearch', version: '2.0.0'

  testCompile group: 'org.springframework.data', name: 'spring-data-elasticsearch', version: '2.0.0.RELEASE'

  testCompile group: 'net.java.dev.jna', name: 'jna', version: '4.5.1'
  testCompile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.0'
  testCompile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.0'

  latestDepTestCompile group: 'org.elasticsearch', name: 'elasticsearch', version: '2.+'
  latestDepTestCompile group: 'org.springframework.data', name: 'spring-data-elasticsearch', version: '2.+'
}
