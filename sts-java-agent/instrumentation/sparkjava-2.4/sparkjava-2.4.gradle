apply plugin: 'version-scan'

versionScan {
  group = "com.sparkjava"
  module = 'spark-core'
  versions = "[2.4,)"
  verifyPresent = [
    "spark.route.Routes": null
  ]
}

apply from: "${rootDir}/gradle/java.gradle"

testJava8Minimum += '**/SparkJavaBasedTest.class'

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  latestDepTest {
    dirName = 'test'
  }
}

latestDepTest {
  sourceCompatibility = "1.8"
  targetCompatibility = "1.8"
}

compileTestJava {
  sourceCompatibility = "1.8"
  targetCompatibility = "1.8"
}

compileJava {
  sourceCompatibility = "1.7"
  targetCompatibility = "1.7"
}

dependencies {
  compileOnly group: 'com.sparkjava', name: 'spark-core', version: '2.4'

  compile project(':dd-java-agent:agent-tooling')
  compile deps.bytebuddy
  compile deps.opentracing
  annotationProcessor deps.autoservice
  implementation deps.autoservice

  testCompile project(':dd-java-agent:instrumentation:jetty-8')
  testCompile project(':dd-java-agent:testing')

  testCompile group: 'com.sparkjava', name: 'spark-core', version: '2.4'
  testCompile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.6.0'

  latestDepTestCompile group: 'com.sparkjava', name: 'spark-core', version: '+'
}

configurations.latestDepTestCompile {
  resolutionStrategy {
    force group: 'com.sparkjava', name: 'spark-core', version: '+'
  }
}
