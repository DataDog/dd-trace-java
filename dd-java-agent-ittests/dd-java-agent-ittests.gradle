apply from: "${rootDir}/gradle/java.gradle"

// Not adding jacoco here as it inexplicably breaks our tests.
// apply from: "${rootDir}/gradle/jacoco.gradle"

description = 'dd-java-agent-ittests'
dependencies {
  testCompile project(':dd-trace-annotations')
  testCompile project(':dd-trace')

  testCompile group: 'io.opentracing', name: 'opentracing-mock', version: '0.30.0'
  testCompile group: 'org.assertj', name: 'assertj-core', version: '3.6.2'

  testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

  testCompile group: 'org.mongodb', name: 'mongo-java-driver', version: '3.4.2'
  testCompile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.1.v20170120'
  testCompile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.4.1.v20170120'
  testCompile group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '8.0.41'
  testCompile group: 'org.apache.tomcat.embed', name: 'tomcat-embed-jasper', version: '8.0.41'
  testCompile(group: 'com.amazonaws', name: 'aws-java-sdk', version: '1.11.119') {
    exclude(module: 'httpclient')
  }
  testCompile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.6.0'
  testCompile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.3'
  testCompile(group: 'com.datastax.cassandra', name: 'cassandra-driver-core', version: '3.2.0')
  testCompile(group: 'org.cassandraunit', name: 'cassandra-unit', version: '3.1.3.2') {
    exclude(module: 'netty-handler')
  }
  testCompile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.8.2'
  testCompile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.8.2'
  testCompile group: 'javax.jms', name: 'javax.jms-api', version: '2.0.1'
  testCompile group: 'org.apache.activemq.tooling', name: 'activemq-junit', version: '5.14.5'
  testCompile group: 'org.apache.activemq', name: 'activemq-broker', version: '5.14.5'
}

configurations.all {
  resolutionStrategy {
    force 'com.google.guava:guava:18.0' // Force to test package renaming in agent jar. (see ShadowPackageRenamingTest)
  }
}

test {
  jvmArgs "-Ddd.trace.configurationFile=${project.buildDir}/resources/test/dd-trace.yaml"
  jvmArgs "-javaagent:${project(':dd-java-agent').buildDir}/libs/dd-java-agent-${project.version}.jar"
  jvmArgs "-Ddd.deps.org.slf4j.simpleLogger.defaultLogLevel=debug"
  jvmArgs "-Dorg.jboss.byteman.verbose=true"

  if (project.hasProperty("disableShadowRelocate") && disableShadowRelocate) {
    exclude 'com/datadoghq/trace/agent/ShadowPackageRenamingTest.class'
  }
}

test.dependsOn project(':dd-java-agent').shadowJar

parent.subprojects.collect { it.tasks.withType(Test) } each {
  test.shouldRunAfter it
}
