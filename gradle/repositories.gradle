repositories {
  mavenLocal()
  if (project.rootProject.name != "dd-trace-java" && project.rootProject.name != "buildSrc") {
    logger.lifecycle("Using maven repository proxy: " + project.rootProject.findProperty("mavenRepositoryProxy"))
  }
  if (project.rootProject.hasProperty("mavenRepositoryProxy")) {
    maven {
      url project.rootProject.property("mavenRepositoryProxy")
      allowInsecureProtocol true
    }
  }
  mavenCentral()
  // add sonatype repository for snapshot dependencies
  maven {
    content {
      includeGroup "com.datadoghq"
    }
    mavenContent {
      snapshotsOnly()
    }
    url 'https://ossrh-staging-api.central.sonatype.com/content/repositories/snapshots/'
  }
  ivy {
    artifactPattern 'https://sqreen-ci-java.s3.amazonaws.com/jars/[organisation]/[artifact]-[revision](-[classifier]).[ext]'
    ivyPattern 'https://sqreen-ci-java.s3.amazonaws.com/jars/[organisation]/[module]-[revision].xml'
    content {
      includeGroup 'io.sqreen'
    }
  }
  maven {
    url 'https://packages.confluent.io/maven/'
    content {
      includeGroupAndSubgroups "io.confluent"
      includeGroupAndSubgroups "org.apache.kafka"
    }
  }
  maven {
    url 'https://repo.spring.io/libs-milestone'
    content {
      includeGroupAndSubgroups "org.springframework"
    }
  }
}
