repositories {
  mavenLocal()
  if (project.rootProject.name != "dd-trace-java" && project.rootProject.name != "buildSrc") {
    logger.lifecycle("Using maven repository proxy: " + project.rootProject.findProperty("mavenRepositoryProxy"))
  }
  if (project.rootProject.hasProperty("mavenRepositoryProxy")) {
    maven {
      url project.rootProject.property("mavenRepositoryProxy")
      allowInsecureProtocol = true
    }
  }
  mavenCentral()
  // add maven central repository for snapshot dependencies
  maven {
    content {
      includeGroup "com.datadoghq"
      includeGroup "io.sqreen"
    }
    mavenContent {
      snapshotsOnly()
    }
    // see https://central.sonatype.org/publish/publish-portal-snapshots/#consuming-via-gradle
    url = 'https://central.sonatype.com/repository/maven-snapshots/'
  }
  maven {
    url = 'https://packages.confluent.io/maven/'
    content {
      includeGroupAndSubgroups "io.confluent"
      includeGroupAndSubgroups "org.apache.kafka"
    }
  }
  maven {
    url = 'https://repo.spring.io/libs-milestone'
    content {
      includeGroupAndSubgroups "org.springframework"
    }
  }
}
