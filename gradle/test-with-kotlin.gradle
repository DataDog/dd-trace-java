// Enable testing kotlin code in groovy spock tests.
project.pluginManager.withPlugin('kotlin') {
  tasks.named("compileTestGroovy", GroovyCompile) {
    //Note: look like it should be `classpath += files(sourceSets.test.kotlin.classesDirectory)`
    //instead, but kotlin plugin doesn't support it (yet?)
    classpath += files(tasks.named('compileTestKotlin').map { it.destinationDirectory })
  }

  // Having Groovy, Kotlin and Java in the same project is a bit problematic
  // this removes Kotlin from main source set to avoid compilation issues
  sourceSets {
    main {
      kotlin {
        srcDirs = []
      }
      java {
        srcDirs = ["src/main/java"]
      }
    }
  }

  // this creates Kotlin dirs to make JavaCompile tasks work
  def createKotlinDirs = tasks.register("createKotlinDirs") {
    def dirsToCreate = ["classes/kotlin/main"]
    doFirst {
      dirsToCreate.forEach {
        new File(project.buildDir, it).mkdirs()
      }
    }

    outputs.dirs(
      dirsToCreate.collect {
        project.layout.buildDirectory.dir(it)
      }
      )
  }

  tasks.withType(JavaCompile).configureEach {
    inputs.files(createKotlinDirs)
  }

  dependencies {
    // prevents Kotlin libs from being included into the tracer JAR
    compileOnly libs.kotlin
  }
}

