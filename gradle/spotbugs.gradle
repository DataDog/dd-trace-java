apply plugin: 'com.github.spotbugs'

spotbugs {
  ignoreFailures = true
  excludeFilter = file("$rootDir/gradle/spotbugFilters/exclude.xml")
}

// configure spotbugs for Main tasks and disable it for all other
afterEvaluate {
  tasks.withType(spotbugsMain.class).configureEach {
    // Temporary disable for `internal-api` module to make CI green.
    // `internal-api` is the only module that failed after update of spotbugs plugin with error:
    // FindBugs2 has been compiled by a more recent version of the Java Runtime (class file version 55.0),
    // this version of the Java Runtime only recognizes class file versions up to 52.0
    if (it.project.name == "internal-api") {
      it.enabled = false
      return
    }

    def name = it.name
    if (name.endsWith("Main") || name.endsWith("Main_java11")) {
      it.ignoreFailures = false
      // detector documentation is in the following link:
      // https://spotbugs-in-kengo-toda.readthedocs.io/en/lqc-list-detectors/detectors.html
      it.omitVisitors = [
        'DefaultEncodingDetector',
        'DoInsideDoPrivileged',
        'DontUseEnum',
        'DroppedException',
        'FindDeadLocalStores',
        'FindHEmismatch',
        'FindNullDeref',
        'FindReturnRef',
        'FindRunInvocations',
        'FindUselessControlFlow',
        'InitializationChain',
        'LazyInit',
        'LoadOfKnownNullValue',
        'LostLoggerDueToWeakReference',
        'MethodReturnCheck',
        'MutableStaticFields',
        'Naming',
        'RuntimeExceptionCapture',
        'SerializableIdiom',
        'UnreadFields',
      ]
      it.reports {
        html {
          enabled = true
          destination = file("$buildDir/reports/spotbugs/${name}.html")
          stylesheet = 'fancy-hist.xsl'
        }
      }
    } else {
      it.enabled = false
    }
  }
}

dependencies {
  compileOnly 'net.jcip:jcip-annotations:1.0'
  compileOnly 'com.github.spotbugs:spotbugs-annotations:4.9.8'

  testImplementation 'net.jcip:jcip-annotations:1.0'
  testImplementation 'com.github.spotbugs:spotbugs-annotations:4.9.8'
}
