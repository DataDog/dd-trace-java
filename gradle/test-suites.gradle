import org.gradle.api.plugins.jvm.JvmTestSuite

ext.addTestSuiteExtendingForDir = { testSuiteName, parentSuiteName, dirName ->
  testing {
    suites {
      create(testSuiteName, JvmTestSuite, {
        sources {
          java {
            srcDirs = ["src/$dirName/java"]
          }
          resources {
            srcDirs = ["src/$dirName/resources"]
          }
          if (project.plugins.hasPlugin('groovy')) {
            groovy {
              srcDirs = ["src/$dirName/groovy"]
            }
          }
          if (project.plugins.hasPlugin('kotlin')) {
            kotlin {
              srcDirs = ["src/$dirName/kotlin"]
            }
          }
          if (project.plugins.hasPlugin('scala')) {
            scala {
              srcDirs = ["src/$dirName/scala"]
            }
          }
        }
      })
    }
  }

  configurations {
    def extendConf = { suffix ->
      def config = named("${testSuiteName}${suffix}")
      def parentConfig = named("${parentSuiteName}${suffix}")
      if (parentConfig.present) {
        config.configure {
          extendsFrom(parentConfig.get())
        }
      }

      if (testSuiteName ==~ /.*ForkedTest\z/) {
        def nonForkedBaseConfName = testSuiteName - ~/Forked/
        def nonForkedConfig = maybeCreate("${nonForkedBaseConfName}${suffix}")
        config.configure {
          extendsFrom(nonForkedConfig)
        }
      }
    }

    extendConf('CompileOnly')
    extendConf('Implementation')
    extendConf('RuntimeOnly')
    extendConf('AnnotationProcessor')
  }

  tasks.register("${testSuiteName}Jar", Jar) {
    dependsOn(tasks.named("${testSuiteName}Classes"))
    from(sourceSets.named(testSuiteName).map { it.output })
    archiveClassifier = testSuiteName
  }

  // The project dependency definition cannot sit inside previous blocks. As of Gradle 8.8, configurations cannot be mutated after they have been used as part of the dependency graph.
  // See: https://docs.gradle.org/current/userguide/upgrading_version_8.html#mutate_configuration_after_locking
  // And related issue: https://github.com/gradle/gradle/issues/28867
  project.dependencies {
    "${testSuiteName}Implementation"(project(project.path))
  }
}

ext.addTestSuiteForDir = { testSuiteName, dirName ->
  ext.addTestSuiteExtendingForDir(testSuiteName, 'test', dirName)
}

ext.addTestSuite = { testSuiteName ->
  ext.addTestSuiteForDir(testSuiteName, testSuiteName)
}
