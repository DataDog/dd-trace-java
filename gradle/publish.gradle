apply plugin: 'maven-publish'
apply plugin: 'signing'

/**
 * Proper publishing requires the following environment variables:
 * MAVEN_CENTRAL_USERNAME
 * MAVEN_CENTRAL_PASSWORD
 * GPG_PRIVATE_KEY
 * GPG_PASSWORD
 */

apply from: "$rootDir/gradle/version.gradle"
apply from: "$rootDir/gradle/maven-pom.gradle"

def isGitlabCI = providers.environmentVariable("GITLAB_CI").isPresent()

// define in ~/.gradle/gradle.properties to override for testing
def forceLocal = project.hasProperty('forceLocal') && forceLocal
assert !forceLocal || forceLocal != isGitlabCI

publishing {
  publications {
    maven(MavenPublication) { MavenPublication publication ->
      if (project.plugins.hasPlugin('com.gradleup.shadow')) {
        publication.artifact(project.tasks.named("shadowJar"))

        // Required by Maven Central:
        publication.artifact sourcesJar
        publication.artifact javadocJar

        afterEvaluate {
          /**
           * Add all the project dependencies, but set as compile instead of runtime
           * (which is what project.shadow.component(publication) would use).
           */
          publication.pom { org.gradle.api.publish.maven.MavenPom pom ->
            pom.withXml { xml ->
              def dependenciesNode = xml.asNode().appendNode('dependencies')

              project.configurations.api.allDependencies.each {
                if (it instanceof ProjectDependency || it instanceof ExternalModuleDependency) {
                  def dependencyNode = dependenciesNode.appendNode('dependency')
                  dependencyNode.appendNode('groupId', it.group)
                  dependencyNode.appendNode('artifactId', it.name)
                  dependencyNode.appendNode('version', it.version)
                  dependencyNode.appendNode("scope", "compile")
                }
              }
            }
          }
        }
      } else {
        publication.from components.java
      }
    }
  }
}

if (project.plugins.hasPlugin('com.gradleup.shadow')) {
  // Disable gradle module metadata to avoid publishing contradictory info.
  tasks.withType(GenerateModuleMetadata).configureEach {
    enabled = false
  }
}

signing {
  useInMemoryPgpKeys(providers.environmentVariable("GPG_PRIVATE_KEY").orNull, providers.environmentVariable("GPG_PASSWORD").orNull)
  sign publishing.publications.maven
}

tasks.withType(Sign).configureEach {
  // Only sign in Gitlab CI
  onlyIf { isGitlabCI || (providers.environmentVariable("GPG_PRIVATE_KEY").isPresent() && providers.environmentVariable("GPG_PASSWORD").isPresent()) }
}

/**
 * State assertions below...
 */

gradle.taskGraph.whenReady { TaskExecutionGraph taskGraph ->
  if (taskGraph.hasTask(publish) || taskGraph.hasTask("publishToSonatype")) {
    assert project.findProperty("removeJarVersionNumbers") != true
    if (taskGraph.hasTask("publishToSonatype")) {
      assert providers.environmentVariable("MAVEN_CENTRAL_USERNAME").isPresent()
      assert providers.environmentVariable("MAVEN_CENTRAL_PASSWORD").isPresent()
      if (isCI) {
        assert providers.environmentVariable("GPG_PRIVATE_KEY").isPresent()
        assert providers.environmentVariable("GPG_PASSWORD").isPresent()
      }
    }
  }
}

afterEvaluate {
  assert description: "Project $project.path is published, must have a description"
}

configurations.configureEach {
  incoming.afterResolve {
    dependencies.withType(ModuleDependency).configureEach { dep ->
      excludeRules.each {
        if ([it.group, it.module].any { it == null }) {
          throw new InvalidUserDataException("Partial exclude for dependency '$dep.group:$dep.name:$dep.version' of $project: [group: $it.group, module: $it.module]\n\nExcludes must specify both group and module and neither can be '*'.")
        }
      }
    }
  }
}

tasks.withType(AbstractPublishToMaven).configureEach { task ->
  rootProject.subprojects {
    task.mustRunAfter tasks.matching { it instanceof VerificationTask }
  }
}
