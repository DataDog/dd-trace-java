import java.time.Duration
import java.time.temporal.ChronoUnit

def isTestingInstrumentation(Project project) {
  return [
    "junit-4.10",
    "cucumber",
    "cucumber-junit-4",
    "junit-4.13",
    "munit-junit-4",
    "junit-5.3",
    "junit-5.8",
    "cucumber-junit-5",
    "spock-junit-5",
    "testng-6",
    "testng-7",
    "karate",
    "scalatest",
    "selenium",
    "weaver"
  ].contains(project.name)
}

def forkedTestLimit = gradle.sharedServices.registerIfAbsent("forkedTestLimit", BuildService) {
  maxParallelUsages = 3
}

testing {
  suites.configureEach {
    // Use JUnit 5 to run tests
    useJUnitJupiter()
  }
}

// Go through the Test tasks and configure them
tasks.withType(Test).configureEach {
  // Disable all tests if skipTests property was specified
  onlyIf { !project.rootProject.hasProperty("skipTests") }

  // Enable force rerun of tests with -Prerun.tests.${project.name}
  outputs.upToDateWhen {
    !project.rootProject.hasProperty("rerun.tests.${project.name}")
  }

  // Avoid executing classes used to test testing frameworks instrumentation
  if (isTestingInstrumentation(project)) {
    exclude("**/TestSucceed*")
    exclude("**/TestFailed*")
    exclude("**/TestFailedWithSuccessPercentage*")
    exclude("**/TestError*")
    exclude("**/TestSkipped*")
    exclude("**/TestSkippedClass*")
    exclude("**/TestInheritance*", "**/BaseTestInheritance*")
    exclude("**/TestFactory*")
    exclude("**/TestParameterized*")
    exclude("**/TestRepeated*")
    exclude("**/TestTemplate*")
    exclude("**/TestDisableTestTrace*")
    exclude("**/TestAssumption*", "**/TestSuiteSetUpAssumption*")
    exclude("**/TestUnskippable*")
    exclude("**/TestWithSetup*")
  }

  // Split up tests that want to run forked in their own separate JVM for generated tasks
  if (name.startsWith("forkedTest") || name.endsWith("ForkedTest")) {
    setExcludes([])
    setIncludes(["**/*ForkedTest*"])
    forkEvery = 1
    // Limit the number of concurrent forked tests
    usesService(forkedTestLimit)
    onlyIf { !project.rootProject.hasProperty("skipForkedTests") }
  } else {
    exclude("**/*ForkedTest*")
  }

  // Set test timeout for 20 minutes. Default job timeout is 1h (configured on CI level).
  timeout = Duration.of(20, ChronoUnit.MINUTES)

  check.dependsOn(it)
}

Task allTestsTask = tasks.maybeCreate('allTests')
Task allLatestDepTestsTask = tasks.maybeCreate('allLatestDepTests')
project.afterEvaluate {
  tasks.withType(Test).each {
    if (it.name.containsIgnoreCase('latest')) {
      allLatestDepTestsTask.dependsOn it
    } else if (it.name != 'traceAgentTest') {
      allTestsTask.dependsOn it
    }
  }
}

// Setup flaky tests jobs. Done in afterEvaluate so that it applies to latestDepTest.
project.afterEvaluate {
  tasks.withType(Test).configureEach {
    // Flaky tests management for JUnit 5
    testFramework {
      if (it instanceof JUnitPlatformOptions) {
        if (project.rootProject.hasProperty("skipFlakyTests")) {
          excludeTags("flaky")
        } else if (project.rootProject.hasProperty("runFlakyTests")) {
          includeTags("flaky")
        }
      }
    }

    // Flaky tests management for Spock
    if (project.rootProject.hasProperty("skipFlakyTests")) {
      jvmArgs += ["-Drun.flaky.tests=false"]
    } else if (project.rootProject.hasProperty("runFlakyTests")) {
      jvmArgs += ["-Drun.flaky.tests=true"]
    }
  }
}

if (!project.property("activePartition")) {
  project.afterEvaluate {
    tasks.withType(Test).configureEach {
      enabled = false
    }
  }
}

tasks.withType(Test).configureEach {
  // https://docs.gradle.com/develocity/flaky-test-detection/
  // https://docs.gradle.com/develocity/gradle-plugin/current/#test_retry
  develocity.testRetry {
    if (providers.environmentVariable("CI").isPresent()) {
      maxRetries = 3
    }
  }
}
