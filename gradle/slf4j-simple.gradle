// Apply this script when `slf4j-simple` should be used instead of `logback`.

configurations.configureEach { cfg ->
  def name = cfg.name

  if (name.containsIgnoreCase("test")) {
    // Exclude Logback from all test-like runtimeClasspath configurations.
    if (name.endsWithIgnoreCase("runtimeClasspath")) {
      cfg.exclude group: "ch.qos.logback"
    }

    // Add slf4j-simple to all test-like runtimeOnly configurations.
    if (name.endsWithIgnoreCase("runtimeOnly")) {
      project.dependencies.add(name, "org.slf4j:slf4j-simple:${libs.versions.slf4j.get()}")
    }
  }
}

// Configure `slf4j-simple` logger.
tasks.withType(Test).configureEach {
  jvmArgs += [
    "-Dorg.slf4j.simpleLogger.defaultLogLevel=debug",
    "-Dorg.slf4j.simpleLogger.showThreadName=true",
    "-Dorg.slf4j.simpleLogger.showDateTime=true",
    "-Dorg.slf4j.simpleLogger.dateTimeFormat=HH:mm:ss.SSS"
  ]
}
