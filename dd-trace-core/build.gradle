plugins {
  id 'me.champeau.jmh'
}

description = 'dd-trace-core'

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/version.gradle"

minimumBranchCoverage = 0.5
minimumInstructionCoverage = 0.6
excludedClassesCoverage += [
  'datadog.trace.civisibility.writer.ddintake.CiTestCovMapperV2.PayloadV2',
  'datadog.trace.common.writer.ddintake.DDIntakeMapperDiscovery',
  'datadog.trace.common.writer.ListWriter',
  'datadog.trace.common.writer.DDAgentWriter.DDAgentWriterBuilder',
  'datadog.trace.common.sampling.PrioritySampling',
  'datadog.trace.core.scopemanager.ScopeInterceptor.DelegatingScope',
  'datadog.trace.common.writer.RemoteMapper.NoopRemoteMapper',
  'datadog.trace.core.monitor.DDAgentStatsDConnection',
  'datadog.trace.core.monitor.LoggingStatsDClient',
  'datadog.trace.core.PendingTraceBuffer.DelayingPendingTraceBuffer.FlushElement',
  'datadog.trace.core.StatusLogger',
  'datadog.trace.core.scopemanager.ContinuableScopeManager.ScopeStackThreadLocal',
  'datadog.trace.core.scopemanager.SingleContinuation',
  // FIXME: more test coverage needed
  'datadog.trace.core.CoreTracer',
  'datadog.trace.core.CoreTracer.1',
  'datadog.trace.core.DDSpan.1',
  'datadog.trace.core.tagprocessor.QueryObfuscator.1',
  'datadog.trace.common.writer.TraceProcessingWorker.NonDaemonTraceSerializingHandler',
  // Interface with an empty defender method
  'datadog.trace.core.propagation.HttpCodec.Extractor',
  'datadog.trace.core.flare.*',
  // FIXME(DSM): test coverage needed
  'datadog.trace.core.datastreams.DataStreamContextInjector',
  'datadog.trace.common.sampling.TraceSamplingRules.RuleAdapter',
  'datadog.trace.common.sampling.SpanSamplingRules.RuleAdapter',
]

addTestSuite('traceAgentTest')

tasks.withType(Test).findByName('forkedTest').configure {
  // Needed for FootprintForkedTest on Java 17
  jvmArgs += ['-Djol.magicFieldOffset=true']
}

dependencies {
  api project(':dd-trace-api')
  api project(':communication')
  api project(':internal-api')
  implementation project(':utils:container-utils')
  implementation project(':utils:socket-utils')

  implementation deps.slf4j
  implementation deps.moshi
  implementation deps.jctools

  implementation group: 'com.datadoghq', name: 'sketches-java', version: '0.8.2'

  implementation group: 'com.google.re2j', name: 're2j', version: '1.7'

  compileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.2.0'

  // We have autoservices defined in test subtree, looks like we need this to be able to properly rebuild this
  testAnnotationProcessor deps.autoserviceProcessor
  testCompileOnly deps.autoserviceAnnotation

  testImplementation project(":dd-java-agent:testing")
  testImplementation group: 'org.msgpack', name: 'msgpack-core', version: '0.8.20'
  testImplementation group: 'org.msgpack', name: 'jackson-dataformat-msgpack', version: '0.8.20'
  testImplementation group: 'org.openjdk.jol', name: 'jol-core', version: '0.16'
  testImplementation group: 'commons-codec', name: 'commons-codec', version: '1.3'
  testImplementation group: 'com.amazonaws', name: 'aws-lambda-java-events', version:'3.11.0'
  testImplementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.14.0'
  testImplementation deps.testcontainers

  traceAgentTestImplementation deps.testcontainers
}

jmh {
  jmhVersion = '1.28'
  duplicateClassesStrategy = DuplicatesStrategy.EXCLUDE
}
