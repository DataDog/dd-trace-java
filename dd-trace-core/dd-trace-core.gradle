plugins {
  id 'me.champeau.jmh'
}

description = 'dd-trace-core'

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/version.gradle"

minimumBranchCoverage = 0.5
minimumInstructionCoverage = 0.6
excludedClassesCoverage += [
  'datadog.trace.common.writer.ListWriter',
  'datadog.trace.common.writer.DDAgentWriter.DDAgentWriterBuilder',
  'datadog.trace.common.sampling.PrioritySampling',
  'datadog.trace.core.scopemanager.ScopeInterceptor.DelegatingScope',
  'datadog.trace.common.writer.RemoteMapper.NoopRemoteMapper',
  'datadog.trace.core.jfr.DDNoopScopeEventFactory',
  'datadog.trace.core.monitor.DDAgentStatsDConnection',
  'datadog.trace.core.monitor.LoggingStatsDClient',
  'datadog.trace.core.PendingTraceBuffer.DelayingPendingTraceBuffer.FlushElement',
  'datadog.trace.core.StatusLogger',
  'datadog.trace.core.scopemanager.ContinuableScopeManager.SingleContinuation',
  'datadog.trace.core.SpanCorrelationImpl',
  'datadog.trace.core.Base64Encoder',
  'datadog.trace.core.DDSpan.1'
]

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  traceAgentTest
}

tasks.withType(Test).findByName('forkedTest').configure {
  // Needed for FootprintForkedTest on Java 17
  jvmArgs += ['-Djol.magicFieldOffset=true']
}

dependencies {
  api project(':dd-trace-api')
  api project(':communication')
  api project(':internal-api')
  implementation project(':utils:histograms')
  implementation project(':utils:container-utils')
  implementation project(':utils:socket-utils')
  implementation project(':dd-cws-tls')

  implementation deps.slf4j
  implementation deps.moshi

  implementation group: 'org.jctools', name: 'jctools-core', version: '3.3.0'
  implementation group: 'com.datadoghq', name: 'sketches-java', version: '0.8.2'

  implementation group: 'com.google.re2j', name: 're2j', version: '1.6'

  compileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.2.0'

  // We have autoservices defined in test subtree, looks like we need this to be able to properly rebuild this
  testAnnotationProcessor deps.autoserviceProcessor
  testCompileOnly deps.autoserviceAnnotation

  testImplementation project(":dd-java-agent:testing")
  testImplementation group: 'org.msgpack', name: 'msgpack-core', version: '0.8.20'
  testImplementation group: 'org.msgpack', name: 'jackson-dataformat-msgpack', version: '0.8.20'
  testImplementation group: 'org.openjdk.jol', name: 'jol-core', version: '0.16'
  testImplementation group: 'commons-codec', name: 'commons-codec', version: '1.3'

  traceAgentTestImplementation deps.testcontainers
}

jmh {
  jmhVersion = '1.28'
  duplicateClassesStrategy = DuplicatesStrategy.EXCLUDE
}
