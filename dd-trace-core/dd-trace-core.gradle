description = 'dd-trace-core'

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/version.gradle"

minimumBranchCoverage = 0.5
minimumInstructionCoverage = 0.6
excludedClassesCoverage += [
  'datadog.trace.common.writer.ListWriter',
  'datadog.trace.common.writer.DDAgentWriter.DDAgentWriterBuilder',
  'datadog.trace.common.sampling.PrioritySampling',
  // This code is copied from okHttp samples and we have integration tests to verify that it works.
  'datadog.trace.common.writer.ddagent.unixdomainsockets.TunnelingUnixSocket',
  'datadog.trace.common.writer.ddagent.unixdomainsockets.UnixDomainSocketFactory',
  'datadog.trace.core.scopemanager.ScopeInterceptor.DelegatingScope',
  'datadog.trace.core.jfr.DDNoopScopeEventFactory',
  'datadog.trace.core.StatusLogger',
]

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  traceAgentTest
}

dependencies {
  compile project(':dd-trace-api')
  compile project(':internal-api')
  compile project(':utils:histograms')
  compile project(':utils:container-utils')

  compile group: 'com.datadoghq', name: 'java-dogstatsd-client', version: "${versions.dogstatsd}"

  compile deps.slf4j
  compile deps.okhttp

  compile group: 'org.hdrhistogram', name: 'HdrHistogram', version: '2.1.12'
  compile group: 'com.squareup.moshi', name: 'moshi', version: '1.9.2'
  compile group: 'com.github.jnr', name: 'jnr-unixsocket', version: "${versions.jnr_unixsocket}"
  compile group: 'org.jctools', name: 'jctools-core', version: '3.2.0'

  // We have autoservices defined in test subtree, looks like we need this to be able to properly rebuild this
  testAnnotationProcessor deps.autoserviceProcessor
  testCompileOnly deps.autoserviceAnnotation

  testCompile project(":dd-java-agent:testing")
  testCompile group: 'org.msgpack', name: 'msgpack-core', version: '0.8.20'
  testCompile group: 'org.msgpack', name: 'jackson-dataformat-msgpack', version: '0.8.20'
  testCompile group: 'org.openjdk.jol', name: 'jol-core', version: '0.14'


  traceAgentTestCompile deps.testcontainers
}
